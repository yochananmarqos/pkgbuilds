# https://aur.archlinux.org/packages/gnome-shell-extension-lockkeys-git

pkgname=gnome-shell-extension-lockkeys-git
_uuid=lockkeys@vaina.lt
pkgver=71.r1.g4bfaa16
pkgrel=1
pkgdesc="Numlock and Capslock indicator for gnome-shell"
arch=('any')
url="https://github.com/kazysmaster/gnome-shell-extension-lockkeys"
license=('LicenseRef-unknown')
depends=(
  'gnome-shell'
  'hicolor-icon-theme'
)
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/kazysmaster/gnome-shell-extension-lockkeys.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "${pkgname%-git}"
  install -d "$pkgdir/usr/share/gnome-shell/extensions"
  cp -r ${_uuid} "$pkgdir/usr/share/gnome-shell/extensions"

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share/"

  install -Dm644 "${_uuid}/schemas/org.gnome.shell.extensions.lockkeys.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -r "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
}
