pkgname=mullvad-vpn-beta
pkgver=2019.9.stable
_pkgver=2019.9
pkgrel=1
pkgdesc="VPN Client for Mullvad.net (latest/beta release)."
url="https://www.mullvad.net"
arch=('x86_64')
license=('GPL3')
depends=('gconf' 'gtk3' 'libnotify' 'libappindicator-gtk2' 'libxss' 'nss')
provides=("${pkgname%-beta}")
conflicts=("${pkgname%-beta}")
install="${pkgname%-beta}.install"
source=("https://github.com/mullvad/mullvadvpn-app/releases/download/${_pkgver}/MullvadVPN-${_pkgver}_${arch}.rpm"{,.asc})
sha256sums=('b6088b08b1aa54842668c9a1f3b36e9ef55ac19a00b3b0f3866b2c455681b79c'
            'SKIP')
validpgpkeys=('A1198702FC3E0A09A9AE5B75D5A1D4F266DE8DDF') # Mullvad (code signing) <admin@mullvad.net>

package() {
	# /opt/ Contents
	install -d "$pkgdir/opt/${pkgname%-beta}"
	cp -a "$srcdir/opt/Mullvad VPN/." "$pkgdir/opt/${pkgname%-beta}"
	chmod -R 755 "$pkgdir/opt/${pkgname%-beta}"
	
	# /usr/bin/ Contents
	install -Dm755 "$srcdir/usr/bin/mullvad" "$pkgdir/usr/bin/mullvad"
	ln -s "/opt/${pkgname%-beta}/mullvad-gui" "$pkgdir/usr/bin/${pkgname%-beta}"
	ln -s "/opt/${pkgname%-beta}/resources/mullvad-problem-report" "$pkgdir/usr/bin/mullvad-problem-report"

	# Systemd Service
	sed -i 's|Mullvad\\x20VPN|mullvad-vpn|g' "$pkgdir/opt/${pkgname%-beta}/resources/mullvad-daemon.service"
	install -Dm644 "$pkgdir/opt/${pkgname%-beta}/resources/mullvad-daemon.service" \
		"$pkgdir/usr/lib/systemd/system/mullvad-daemon.service"

	# Desktop Entry
	sed -i 's|Exec=.*|Exec=/opt/mullvad-vpn/mullvad-vpn U%|g' "$srcdir/usr/share/applications/$pkgname.desktop"
	install -Dm644 "$srcdir/usr/share/applications/${pkgname%-beta}.desktop" \
		"$pkgdir/usr/share/applications/${pkgname%-beta}.desktop"

	# Icons
	install -dm644 "$pkgdir/usr/share/icons/hicolor"
	cp -a "$srcdir/usr/share/icons/hicolor/." "$pkgdir/usr/share/icons/hicolor"
}
