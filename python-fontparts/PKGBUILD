# http://aur.archlinux.org/packages/python-fontparts
pkgname=python-fontparts
_name=fontParts
pkgver=0.9.2
pkgrel=1
pkgdesc="The replacement for RoboFab."
arch=('any')
url="https://github.com/robotools/fontParts"
license=('MIT')
depends=('python-fontmath' 'python-defcon' 'python-booleanoperations' 'python-fs')
makedepends=('python-setuptools')
checkdepends=('python-fontpens')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('134077ecba448ee30d102ce3fb870bcaf3ac5935d08c9ec8ef81e75cef6d9f43')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver/Lib"
	PYTHONPATH=. python fontParts/fontshell/test.py
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

