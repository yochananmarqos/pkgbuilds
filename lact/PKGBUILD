# Maintainer: Ilya Zlobintsev <ilya.zl@protonmail.com>
pkgname=(
  'lact'
  'lact-headless'
  'lact-libadwaita'
)
pkgbase=lact
pkgver=0.5.4
pkgrel=1
pkgdesc="AMDGPU Controller application"
arch=('x86_64' 'aarch64')
url="https://github.com/ilya-zlobintsev/LACT"
license=('MIT')
depends=(
  'hwdata'
  'libdrm'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'clang'
  'dbus'
  'git'
  'libadwaita'
)
install="$pkgbase.install"
source=("git+https://github.com/ilya-zlobintsev/LACT.git#tag=v$pkgver")
sha256sums=('8b8abc3e58e1c2d619b9e472a5b9cbae6a8a2a4077cc56e1da58bb1203b975f5')

prepare() {
  cd LACT
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd LACT
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  # Libadwaita
  cargo build -p "$pkgbase" --frozen --release --features=adw
  mv "target/release/$pkgbase" "target/release/$pkgbase-adw"

  # headless
  cargo build -p "$pkgbase" --frozen --release --no-default-features --features=drm
  mv "target/release/$pkgbase" "target/release/$pkgbase-headless"

  # Gtk 4
  cargo build -p "$pkgbase" --frozen --release
}

check() {
  cd LACT
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --no-default-features
}

package_lact() {
  depends+=('gtk4')

  cd LACT
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dvm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase/"
}

package_lact-headless() {
  pkgdesc+=" (headless)"
  depends+=('dbus')
  provides=("$pkgbase")
  conflicts=("$pkgbase")

  cd LACT
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dvm755 "target/release/$pkgbase-headless" "$pkgdir/usr/bin/$pkgbase"
  install -Dvm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase/"
}

package_lact-libadwaita() {
  pkgdesc+=" (libadwaita edition)"
  provides=("$pkgbase")
  conflicts=("$pkgbase")
  depends+=('libadwaita')

  cd LACT
  install -Dvm755 "target/release/$pkgbase-headless" "$pkgdir/usr/bin/$pkgbase"
  install -Dvm644 res/lactd.service -t "$pkgdir/usr/lib/systemd/system/"
  install -Dvm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase/"
}
