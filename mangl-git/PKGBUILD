# https://aur.archlinux.org/packages/mangl-git
pkgname=mangl-git
pkgver=1.0.2.r4.gb4d00ce
pkgrel=1
pkgdesc="Graphical man page viewer"
arch=('x86_64')
url="https://github.com/zigalenarcic/mangl"
license=('BSD 2-Clause')
depends=('zlib' 'freeglut' 'libgl' 'glu' 'freetype2')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/zigalenarcic/mangl.git'
        "${pkgname%-git}.desktop")
sha1sums=('SKIP'
          '68a39c576a302c55c7dadb86697f8d880e394b9c')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}/mandoc"
	./configure --prefix=/usr #--exec_prefix=/usr
	make -C ..
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm755 "${pkgname%-git}" -t "$pkgdir/usr/bin"
	install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
