# https://aur.archlinux.org/packages/mangl-git
pkgname=mangl-git
pkgver=1.0.2.r8.g3b9a0cd
pkgrel=1
pkgdesc="Graphical man page viewer"
arch=('x86_64')
url="https://github.com/zigalenarcic/mangl"
license=('BSD 2-Clause')
depends=('freeglut' 'freetype2')
makedepends=('git')
optdepends=('fontconfig: set custom font')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/zigalenarcic/mangl.git'
        "${pkgname%-git}.desktop"
        "https://github.com/zigalenarcic/mangl/pull/13.patch")
sha256sums=('SKIP'
            '370e454df24a2bf0bf185988d92083c0ec5bd72548a5fba9c44867e76a1d8d91'
            '88d91f594c04dd326b044c84a5e6f563c6054e7ca9a05560ad79167333481dec')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git apply -3 "$srcdir/13.patch"
}

build() {
	cd "$srcdir/${pkgname%-git}/mandoc"
	./configure --prefix=/usr
	make -C ..
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm755 "${pkgname%-git}" -t "$pkgdir/usr/bin"
	install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
