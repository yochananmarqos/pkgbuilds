# https://aur.archlinux.org/packages/mangl-git
groups=('modified')

pkgname=mangl-git
pkgver=1.1.5.r0.g09097d3
pkgrel=1
pkgdesc="Graphical man page viewer"
arch=('x86_64')
url="https://github.com/zigalenarcic/mangl"
license=('BSD-2-Clause')
depends=('freetype2' 'glfw' 'libglvnd')
makedepends=('git')
optdepends=('fontconfig: set custom font')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/zigalenarcic/mangl.git'
        'manglrc_example')
sha256sums=('SKIP'
            'e2424f8303abb6c84ea9f7b1f8c25a4bee4f5748249fecb57c4ce7d606b405e4')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  ./configure
  make
}

package() {
  cd "${pkgname%-git}"
  make BINDIR=/usr/bin MANDIR=/usr/share/man DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "$srcdir/manglrc_example" -t "$pkgdir/usr/share/doc/${pkgname%-git}/"
}
