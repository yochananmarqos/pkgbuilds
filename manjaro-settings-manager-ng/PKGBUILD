## WIP

# https://forum.manjaro.org/t/my-version-of-manjaro-settings-manager-a-work-in-progress/179921

pkgname=manjaro-settings-manager-ng
pkgver=r108.f8e34be
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool: Next Generation"
arch=('any')
url="https://gitlab.com/feralpenguin/manjarosettingsmanager"
license=('LicenseRef-unknown')
depends=(
  'inxi'
  'pyside6'
  'python-babel'
  'python-dateutil'
  'python-gobject'
  'python-pycountry'
  'python-pygments'
  'python-pytz'
  'python-tzlocal'
)
makedepends=(
  'desktop-file-utils'
  'git'
)
_commit=f8e34be56f0bfbe435cfcf5c28cef5789546e020
source=("git+https://gitlab.com/feralpenguin/manjarosettingsmanager.git#commit=${_commit}"
        "msm-ng.sh")
sha256sums=('1197eeda944f65ba402e56a40e43ddfc735a6d6d543b47306c3c1ef50ddee9c1'
            '94fece808e3fb6513f493879054a561d922dce2f8b71f8e72ea0afd328446c52')

pkgver() {
  cd manjarosettingsmanager
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd manjarosettingsmanager
  desktop-file-edit --set-name="MSM NG" --set-key=Exec --set-value=msm-ng src/msm.desktop
}

package() {
  cd manjarosettingsmanager

  install -d "$pkgdir/opt/$pkgname"
  cp -r Data Design src "$pkgdir/opt/$pkgname"

  install -Dm644 src/msm.desktop "$pkgdir/usr/share/applications/msm-ng.desktop"
  install -Dm755 "$srcdir/msm-ng.sh" "$pkgdir/usr/bin/msm-ng"

  # Fix symlinks
  for f in KernelManager LocaleSettings TimeAndDateSettings UserManager; do
    ln -sf "/opt/$pkgname/src/simple_translator.py" "$pkgdir/opt/$pkgname/src/plugins/${f}/"
  done
  ln -sf "/opt/$pkgname/src/simple_translator.py" "$pkgdir/opt/$pkgname/src/plugins/SystemInformation/"
  rm "$pkgdir/opt/$pkgname/src/plugins/SystemInformation/simple-translator.py"

  # Remove unneeded files
  for f in KernelManager LocaleSettings SystemInformation TimeAndDateSettings UserManager; do
    rm "$pkgdir/opt/$pkgname/src/plugins/${f}"/{launcher.sh,"${f}.desktop"}
  done
  rm "$pkgdir/opt/$pkgname"/src/{launcher.sh,msm.desktop}

  # Compile Python bytecode
  python -m compileall -d / "$pkgdir/opt/$pkgname"
  python -O -m compileall -d / "$pkgdir/opt/$pkgname"
}
