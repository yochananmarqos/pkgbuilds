## WIP

# https://forum.manjaro.org/t/my-version-of-manjaro-settings-manager-a-work-in-progress/179921

pkgname=manjaro-settings-manager-ng
pkgver=0.9.0a6
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool: Next Generation"
arch=('any')
url="https://gitlab.com/feralpenguin/manjarosettingsmanager"
license=('LicenseRef-unknown')
depends=(
  'inxi'
  'pyside6'
  'python-babel'
  'python-dateutil'
  'python-gobject'
  'python-pycountry'
  'python-pygments'
  'python-pytz'
  'python-tzlocal'
)
makedepends=(
  'desktop-file-utils'
  'git'
)
source=("git+https://gitlab.com/feralpenguin/manjarosettingsmanager.git#tag=v$pkgver"
        "msm-ng.sh")
sha256sums=('31448362c2c0be23c451bc4007b1d78f40f8dceb742df6bf6746234d20c6e4f1'
            '94fece808e3fb6513f493879054a561d922dce2f8b71f8e72ea0afd328446c52')

prepare() {
  cd manjarosettingsmanager
  desktop-file-edit --set-name="MSM NG" --set-key=Exec --set-value=msm-ng src/msm.desktop
}

package() {
  cd manjarosettingsmanager
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  install -d "$pkgdir/opt/$pkgname"
  cp -r Data Design src "$pkgdir/opt/$pkgname/"

  install -Dm644 src/simple_translator.py -t "${pkgdir}${site_packages}/"
  install -Dm644 src/msm.desktop "$pkgdir/usr/share/applications/msm-ng.desktop"
  install -Dm755 "$srcdir/msm-ng.sh" "$pkgdir/usr/bin/msm-ng"

  # Remove unneeded files
  for f in TimeAndDateSettings UserManager; do
    rm "$pkgdir/opt/$pkgname/src/plugins/${f}/simple_translator.py"
  done
  for f in KernelManager LocaleSettings SystemInformation TimeAndDateSettings UserManager; do
    rm "$pkgdir/opt/$pkgname/src/plugins/${f}"/{launcher.sh,"${f}.desktop"}
  done
  rm "$pkgdir/opt/$pkgname"/src/{launcher.sh,msm.desktop,simple_translator.py}

  # Compile Python bytecode
  python -m compileall -d / "$pkgdir/opt/$pkgname"
  python -O -m compileall -d / "$pkgdir/opt/$pkgname"
}
