## WIP

# https://forum.manjaro.org/t/my-version-of-manjaro-settings-manager-a-work-in-progress/179921

pkgname=manjaro-settings-manager-ng
pkgver=0.9.0a8
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool: Next Generation"
arch=('any')
url="https://gitlab.com/feralpenguin/manjarosettingsmanager"
license=('LicenseRef-unknown')
depends=(
  'inxi'
  'pyside6'
  'python-babel'
  'python-dateutil'
  'python-gobject'
  'python-pillow'
  'python-pycountry'
  'python-pygments'
  'python-pytz'
  'python-tzlocal'
)
makedepends=(
  'desktop-file-utils'
  'git'
)
source=("git+https://gitlab.com/feralpenguin/manjarosettingsmanager.git#tag=v$pkgver"
        "msm-ng.sh")
sha256sums=('1429d932e5cbcf272a6c6c3dd081556ffb9ff526d6ea817b3120e50c648f59fa'
            '94fece808e3fb6513f493879054a561d922dce2f8b71f8e72ea0afd328446c52')

prepare() {
  cd manjarosettingsmanager
  desktop-file-edit --set-name="MSM NG" --set-key=Exec --set-value=msm-ng src/msm-ng.desktop
}

package() {
  cd manjarosettingsmanager
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  install -d "$pkgdir/opt/$pkgname"
  cp -r Data Design src "$pkgdir/opt/$pkgname/"

  install -Dm644 src/simple_translator.py -t "${pkgdir}${site_packages}/"
  install -Dm644 src/msm-ng.desktop -t "$pkgdir/usr/share/applications/"
  install -Dm755 "$srcdir/msm-ng.sh" "$pkgdir/usr/bin/msm-ng"

  # Remove unneeded files
  for plugin in "$pkgdir/opt/$pkgname/src/plugins"/*; do
    if [ -d "${plugin}" ]; then

      if [ -L "${plugin}/simple_translator.py" ]; then
       rm "${plugin}/simple_translator.py"
      fi

      if [ -f "${plugin}/launcher.sh" ]; then
        rm "${plugin}/launcher.sh"
      fi

      desktop_file=$(find "${plugin}" -maxdepth 1 -type f -name "*.desktop")
      if [ -n "${desktop_file}" ]; then
        rm "${desktop_file}"
      fi

    fi
  done
  rm "$pkgdir/opt/$pkgname"/src/{launcher.sh,msm-ng.desktop,simple_translator.py}

  # Compile Python bytecode
  python -m compileall -d / "$pkgdir/opt/$pkgname"
  python -O -m compileall -d / "$pkgdir/opt/$pkgname"
}
