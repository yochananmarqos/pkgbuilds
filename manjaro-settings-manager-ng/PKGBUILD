## WIP

# https://forum.manjaro.org/t/my-version-of-manjaro-settings-manager-a-work-in-progress/179921

pkgname=manjaro-settings-manager-ng
pkgver=r97.17333a5
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool: Next Generation"
arch=('any')
url="https://gitlab.com/feralpenguin/manjarosettingsmanager"
license=('LicenseRef-unknown')
depends=(
  'inxi'
  'pyside6'
  'python-babel'
  'python-dateutil'
  'python-gobject'
  'python-pycountry'
  'python-pygments'
  'python-pytz'
  'python-tzlocal'
)
makedepends=(
  'desktop-file-utils'
  'git'
)
_commit=17333a5dea90607036ec00cffb4ba528a95eb86c
source=("git+https://gitlab.com/feralpenguin/manjarosettingsmanager.git#commit=${_commit}")
sha256sums=('a5061ca1f19da225d0db173f26d59282868c34d86c17bd5176088bab64d7b013')

pkgver() {
  cd manjarosettingsmanager
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd manjarosettingsmanager
  desktop-file-edit --set-name="MSM NG" --set-key=Exec --set-value=msm-ng src/msm.desktop
}

package() {
  cd manjarosettingsmanager
  install -d "$pkgdir/opt/$pkgname"
  cp -r Data Design src "$pkgdir/opt/$pkgname"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/$pkgname/src/msm.py" "$pkgdir/usr/bin/msm-ng"

  install -Dm644 src/msm.desktop "$pkgdir/usr/share/applications/msm-ng.desktop"

  # Fix symlinks
  for f in KernelManager LocaleSettings SystemInformation TimeAndDateSettings UserManager; do
    ln -sf "/opt/$pkgname/src/Translator.py" "$pkgdir/opt/$pkgname/src/plugins/${f}/"
    ln -sf "/opt/$pkgname/src/translations/" "$pkgdir/opt/$pkgname/src/plugins/${f}/"
  done
}
