## WIP

# https://forum.manjaro.org/t/my-version-of-manjaro-settings-manager-a-work-in-progress/179921

pkgname=manjaro-settings-manager-ng
pkgver=0.9.0a26
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool: Next Generation"
arch=('any')
url="https://gitlab.com/feralpenguin/manjaro-settings-manager-ng"
license=('LicenseRef-unknown')
depends=(
  'inxi'
  'mhwd'
  'pyside6'
  'python-babel'
  'python-dateutil'
  'python-gobject'
  'python-pillow'
  'python-pycountry'
  'python-pygments'
  'python-pyqt6'
  'python-pytz'
  'python-tzlocal'
)
makedepends=(
  'desktop-file-utils'
  'git'
)
source=("git+https://gitlab.com/feralpenguin/manjaro-settings-manager-ng.git#tag=v$pkgver")
sha256sums=('bd9a09633221cadc150112bd6b9d741009fa4eeedac850406fe5a5c1349c252d')

prepare() {
  cd "$pkgname"
  desktop-file-edit \
    --set-name="MSM NG" \
    --set-key=Exec --set-value=msm-ng \
    --set-icon=preferences-desktop \
    src/msm-ng.desktop
}

package() {
  cd "$pkgname"
  install -Dm755 src/msm.py -t "$pkgdir/usr/lib/$pkgname/"
  install -Dm644 src/{__init__.py,__main__.py} -t "$pkgdir/usr/lib/$pkgname/"
  install -Dm644 src/libs/*.py -t "$pkgdir/usr/lib/$pkgname/libs/"
  cp -r src/{icons-dark,icons-light,plugins,translations} "$pkgdir/usr/lib/$pkgname/"

  install -Dm755 src/msm-ng "$pkgdir/usr/bin/msm-ng"
  install -Dm644 src/msm-ng.desktop -t "$pkgdir/usr/share/applications/"

  # Compile Python bytecode
  python -m compileall -d / "$pkgdir/usr/lib/$pkgname"
  python -O -m compileall -d / "$pkgdir/usr/lib/$pkgname"
}
