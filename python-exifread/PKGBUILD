# https://aur.archlinux.org/packages/python-exifread

pkgname=python-exifread
_name=exif-py
pkgver=3.1.0
pkgrel=1
pkgdesc="Python library to extract EXIF data from tiff and jpeg files"
arch=('any')
url="https://github.com/ianare/exif-py"
license=('BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
        'license.patch')
sha256sums=('f47606cbbf115164f8fd5feca67a552c006b30357af625e015d203aec46f1433'
            'b1d8d363bf29a23af9e59074082ff3570421abec39ccbe70d3c9a81807324919')

prepare() {
  cd "$_name-$pkgver"

  # Restore missing LICENSE
  patch -Np1 -i ../license.patch
}

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
