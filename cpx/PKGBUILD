pkgname=cpx
pkgver=0.1.3
pkgrel=2
pkgdesc="cp reimagined"
arch=('x86_64' 'aarch64' 'armv7l')
url="https://github.com/11happy/cpx"
license=('MIT')
depends=('libgcc')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('ca13056223142dba089df2698c47e225a1c1e3453ce5e1767d4870404998f9bf')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable

  # test_symlink_mode_auto_relative fails the first time, succeeds after retry
  cargo test --frozen || :
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 README.md docs/*.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
