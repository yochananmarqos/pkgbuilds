# https://aur.archlinux.org/packages/nexusmods-app-bin

pkgname=nexusmods-app-bin
_app_id=com.nexusmods.app
pkgver=0.7.2
pkgrel=1
pkgdesc="A mod installer, creator and manager for all your popular games."
arch=('x86_64')
url="https://github.com/Nexus-Mods/NexusMods.App"
license=('GPL-3.0-only')
depends=(
  'fontconfig'
  'hicolor-icon-theme'
  'snappy'
  'xdg-utils'
)
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
options=('!strip')
source=("https://github.com/Nexus-Mods/NexusMods.App/releases/download/v${pkgver}/NexusMods.App-${pkgver}-1.linux-x64.zip"
        "${_app_id}.appdata.xml"
        "${_app_id}.desktop"
        "nexus-logo-${pkgver}.svg::https://github.com/Nexus-Mods/NexusMods.App/raw/v${pkgver}/src/NexusMods.App.UI/Assets/nexus-logo.svg")
noextract=("NexusMods.App-${pkgver}-1.linux-x64.zip")
sha256sums=('5924020872258314d3d44ceaf9cd29cb3e16426a3ac4f9a5dbdd5c76b67f56da'
            'e8907d97fcce383275356a748def80303c85b36375f2c74e111912bc07a68d4b'
            '7676ed2a253ac0c711c459238208f621652b032ee12050365c7f8e338d4cee5b'
            '5a22eccfb001eacbf2756f2e21fa371dbafd9e84b20bb6a484d781bde33089cb')

package() {
  install -d "${pkgdir}/opt/NexusMods.App"
  bsdtar -xvf "NexusMods.App-${pkgver}-1.linux-x64.zip" -C "${pkgdir}/opt/NexusMods.App/"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/NexusMods.App/NexusMods.App" "$pkgdir/usr/bin/"

  install -Dm644 "${_app_id}.desktop" -t "${pkgdir}/usr/share/applications/"
  install -Dm644 "nexus-logo-${pkgver}.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${_app_id}.svg"
  install -Dm644 "${_app_id}.appdata.xml" -t "$pkgdir/usr/share/metainfo/"

  rm -rf "$pkgdir"/opt/NexusMods.App/runtimes/{osx,win}-x64
}
