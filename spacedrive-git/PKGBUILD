# https://aur.archlinux.org/packages/spacedrive-git
groups=('modified')

## WIP

pkgname=spacedrive-git
pkgver=0.1.4.r15.g98ff6a798
pkgrel=1
pkgdesc="An open source cross-platform file explorer, powered by a virtual distributed filesystem written in Rust."
arch=('x86_64')
url="https://www.spacedrive.com"
license=('GPL3')
depends=('ffmpeg' 'libheif' 'webkit2gtk')
makedepends=('cargo-nightly' 'clang' 'git' 'pnpm')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!lto')
source=('git+https://github.com/spacedriveapp/spacedrive.git'
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            '70881489eece6c897b03aeab2b5320e5adcf92b5e1be675c705f701566af9826')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  export PNPM_HOME="$srcdir/pnpm-home"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=nightly
  pnpm i --frozen-lockfile
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname%-git}"
  export PNPM_HOME="$srcdir/pnpm-home"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=nightly
  export CARGO_TARGET_DIR=target
  pnpm prep
  pnpm tauri build --bundles app
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "apps/desktop/src-tauri/target/release/${pkgname%-git}" -t \
    "$pkgdir/usr/bin/"

  for i in 32x32 128x128 128x128@2x; do
    install -Dm644 "apps/desktop/src-tauri/icons/${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}/apps/${pkgname%-git}.png"
  done
  install -Dm644 apps/desktop/src-tauri/icons/icon.png \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/${pkgname%-git}.png"

  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t \
    "$pkgdir/usr/share/applications/"
}
