pkgname=spacedrive-git
pkgver=r400.a4637ca
pkgrel=1
_debver=0.1.0
pkgdesc="An open source cross-platform file explorer, powered by a virtual distributed filesystem written in Rust."
arch=('x86_64')
url="https://www.spacedrive.app"
license=('GPL3')
depends=('ffmpeg' 'webkit2gtk')
makedepends=('cargo' 'clang' 'git' 'nodejs>=14.0.0' 'npm' 'pnpm>=6.0.0')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!lto')
source=('git+https://github.com/spacedriveapp/spacedrive.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  pnpm config set store-dir "$srcdir/.pnpm-store"
  export RUSTUP_TOOLCHAIN=stable

  pnpm i

  cargo fetch --target "$CARCH-unknown-linux-gnu"

  # Don't bundle AppImage
  sed -i 's/"targets": "all",/"targets": "deb",/g' \
    apps/desktop/src-tauri/tauri.linux.conf.json
}

build() {
  cd "$srcdir/${pkgname%-git}"
  pnpm config set store-dir "$srcdir/.pnpm-store"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  pnpm prep
  pnpm desktop build
}

package() {
  cd "$srcdir/${pkgname%-git}"
  local targetdir='apps/desktop/src-tauri/target'
  install -Dm755 "$targetdir/release/${pkgname%-git}" -t \
    "$pkgdir/usr/bin/"
  cp -r "$targetdir/release/bundle/deb/${pkgname%-git}_${_debver}_amd64/data/usr/share" \
    "$pkgdir/usr/"
}
