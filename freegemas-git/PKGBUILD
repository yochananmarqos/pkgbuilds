pkgname=freegemas-git
pkgver=22.02.r14.geeca99c
pkgrel=1
pkgdesc="An open source version of the well known Bejeweled written in C++ using SDL2"
arch=('x86_64')
url="https://github.com/JoseTomasTocino/freegemas"
license=('GPL-2.0-or-later')
depends=(
  'jsoncpp'
  'sdl2_image'
  'sdl2_mixer'
  'sdl2_ttf'
)
makedepends=(
  'cmake'
  'git'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/JoseTomasTocino/freegemas.git'
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            'dbe382b8d8d6457fd452430a323171b818c72bf0a5c232e89e551149aa890202')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cmake -B build -S "${pkgname%-git}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm755 "build/${pkgname%-git}" -t "$pkgdir/opt/${pkgname%-git}/"
  install -Dm644 "${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications/"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/${pkgname%-git}/${pkgname%-git}" "$pkgdir/usr/bin"/

  cd "$srcdir/${pkgname%-git}"
  install -Dm644 "platform/unix/icons/64x64/${pkgname%-git}.png" -t \
    "$pkgdir/usr/share/pixmaps/"
  cp -a media "$pkgdir/opt/${pkgname%-git}/"
}
