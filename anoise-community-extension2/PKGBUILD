# https://aur.archlinux.org/packages/anoise-community-extension2

pkgname=anoise-community-extension2
pkgver=0.0.4
pkgrel=5
pkgdesc="Ambient Noise Community Library Package 2"
arch=('any')
url="https://costales.github.io/projects/anoise"
license=('GPL-3.0-or-later')
makedepends=(
  'python-build'
  'python-distutils-extra'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("https://launchpad.net/~costales/+archive/ubuntu/anoise/+sourcefiles/$pkgname/$pkgver/${pkgname}_${pkgver}.tar.gz")
noextract=("${pkgname}_${pkgver}.tar.gz")
sha256sums=('64fc8eecdd00aad0c8d42a8b6016490b49dde64b79232f2f795e69ba8c267f49')

prepare() {
  mkdir -p "$pkgname-$pkgver"
  bsdtar xf "${pkgname}_${pkgver}.tar.gz" -C "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver/${pkgname#anoise-}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver/${pkgname#anoise-}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -rv "${pkgdir}${site_packages}/usr/share" "$pkgdir/usr/"
  rm -rv "${pkgdir}${site_packages}/usr"
}
