pkgname=sdlpop-git
pkgver=1.24.RC.r0.g83c726d
pkgrel=1
pkgdesc="An open-source port of Prince of Persia"
arch=('x86_64')
license=('GPL-3.0-or-later')
url="https://github.com/NagyD/SDLPoP"
depends=(
  'sdl2-compat'
  'sdl2_image'
)
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=("usr/share/${pkgname%-git}/SDLPoP.ini")
source=('git+https://github.com/NagyD/SDLPoP.git'
        "${pkgname%-git}.sh")
sha256sums=('SKIP'
            '3c5d01a27218642646e1d7c66479fd8a414bac953970f168a00054e3084b6767')

pkgver() {
  cd SDLPoP
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd SDLPoP/src

  # Since we don't copy the executable or the data folder anywhere, 
  # the desktop file has to be updated to contain the actual paths.
  sed -e 's|$ROOT|/usr/share/sdlpop|' SDLPoP.desktop.template > SDLPoP.desktop
  sed -i -e '$aStartupWMClass=prince' SDLPoP.desktop
}

build() {
  cd SDLPoP
  make -C src
}

package() {
  cd SDLPoP

  # world-writable for save/load games, config, etc.
  install -dm757 "$pkgdir/usr/share/${pkgname%-git}"
  install -m755 prince -t "$pkgdir/usr/share/${pkgname%-git}/"
  install -m777 SDLPoP.ini -t "$pkgdir/usr/share/${pkgname%-git}/"
  cp -r data mods "$pkgdir/usr/share/${pkgname%-git}/"

  install -d "$pkgdir/usr/share/doc/${pkgname%-git}"
  cp -r doc/* "$pkgdir/usr/share/doc/${pkgname%-git}/"

  install -d "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/share/${pkgname%-git}/data/icon.png" \
    "$pkgdir/usr/share/pixmaps/${pkgname%-git}.png"

  install -Dm644 src/SDLPoP.desktop \
    "$pkgdir/usr/share/applications/${pkgname%-git}.desktop"

  install -Dm755 "$srcdir/${pkgname%-git}.sh" "$pkgdir/usr/bin/prince"
}
