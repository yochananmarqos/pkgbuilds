pkgname=gerbil-git
pkgver=0.1.2.r27.g0febd8f
pkgrel=1
pkgdesc="A little gemini browser, written with mobile linux in mind!"
arch=('any')
url="https://gitlab.com/armen138/gerbil"
license=('MIT')
depends=(
  'gtk3'
  'libhandy'
  'libsecret'
  'python-gobject'
  'python-feedparser'
  'python-requests'
)
makedepends=(
  'git'
  'meson'
)
checkdepends=('appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.com/armen138/gerbil.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  cd "${pkgname%-git}"
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
