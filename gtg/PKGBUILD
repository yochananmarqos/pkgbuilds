# https://aur.archlinux.org/packages/gtg
groups=('modified')

pkgname=gtg
pkgver=0.6
pkgrel=4
pkgdesc="Getting Things GNOME! is a personal tasks and TODO-list items organizer for GNOME"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/GTG"
license=('GPL')
depends=('gtk3' 'gtksourceview4' 'libsecret' 'python-caldav' 'python-gobject'
         'python-liblarch' 'python-lxml')
makedepends=('itstool' 'meson')
checkdepends=('python-mock' 'python-pytest')
optdepends=('python-cheetah3: for the Export and print plugin'
            'texlive-bin: pdflatex, for the Export and print plugin'
            'texlive-core: pdfjam, for the Export and print plugin'
            'pdftk: for the Export and print plugin'
            'hamster-time-tracker: send a task to the Hamster time tracking applet'
            'yelp: view user manual')
source=("https://github.com/getting-things-gnome/gtg/releases/download/v$pkgver/$pkgname-$pkgver.0.tar.xz")
sha256sums=('184bfd08db8328475e25493441c4136aa238139bb2961933d5ee41b41ed104e7')

build () {
  arch-meson "$pkgname-$pkgver.0" build
  meson compile -C build
}

check () {
  cd "$pkgname-$pkgver.0"
  python run-tests
}

package() {
  meson install -C build --destdir "$pkgdir"
}
