pkgname=keyboard-color-switcher-git
pkgver=1.0.r210.gf30b9c1
pkgrel=1
pkgdesc="Change the keyboard backlight color on System76 laptops"
arch=('any')
url="https://github.com/ahoneybun/keyboard-color-switcher"
license=('GPL-3.0-or-later')
depends=(
  'dmidecode'
  'gtk3'
  'polkit'
  'python-cerberus'
  'python-gobject'
  'python-yaml'
  'system76-dkms'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
backup=('etc/system76-backlight-manager.conf')
install='system76-backlight-manager.install'
provides=("${pkgname%-git}" 'system76-backlight-manager')
conflicts=("${pkgname%-git}")
source=("git+https://github.com/ahoneybun/keyboard-color-switcher.git"
        'git+https://github.com/JeffLabonte/System76-Backlight-Manager-cli.git'
        "${pkgname%-git}.desktop"
        "${pkgname%-git}.sh"
        "org.freedesktop.${pkgname%-git}.policy")
sha256sums=('SKIP'
            'SKIP'
            '0e861fb5c67a6520d24845274ccc1858d4502c980f2da8ef2ca3588d63cd15cc'
            '5cc9e33bd128d2c1a07d8efc22942e7f260f5641342f10108f1d929cdc32a020'
            '84db9161bd09ffc7d2e8787236603c96c2ecc038395d37ac9196cc728f1f9e0d')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"

  sed -i 's|git@github.com:|https://github.com/|g' .gitmodules

  git submodule init
  git config submodule.system76_backlight_manager.url \
    "$srcdir/System76-Backlight-Manager-cli"
  git -c protocol.file.allow=always submodule update

  cd system76_backlight_manager

  # Fix service path
  sed -i 's|usr/local|usr|g' service/system76-backlight-manager.service

#  sed -i '/^*bonw14*/i "gaze17-3060-b": SINGLE_BACKLIGHT_PATH,' system76_backlight_manager/keyboard_backlight.py
}

build() {
  cd "${pkgname%-git}/system76_backlight_manager"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname%-git}/system76_backlight_manager"
  pytest
}

package() {
  cd "${pkgname%-git}"
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  install -Dm755 "${pkgname%-git}.py" -t "$pkgdir$site_packages/${pkgname%-git}/"
  cp -r kcc "${pkgdir}${site_packages}/${pkgname%-git}/"

  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm755 "$srcdir/${pkgname%-git}.sh" "$pkgdir/usr/bin/${pkgname%-git}"
  install -Dm644 "$srcdir/org.freedesktop.${pkgname%-git}.policy" -t \
    "$pkgdir/usr/share/polkit-1/actions/"

  cd system76_backlight_manager
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "service/system76-backlight-manager.service" -t \
    "$pkgdir/usr/lib/systemd/system/"
  install -Dm644 "configs/system76-backlight-manager.conf" -t "$pkgdir/etc/"
}
