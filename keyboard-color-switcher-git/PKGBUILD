pkgname=keyboard-color-switcher-git
pkgver=1.0.r5.g5d2e70f
pkgrel=2
pkgdesc="Change the Keyboard Backlight on System76 laptops"
arch=('any')
url="https://github.com/ahoneybun/keyboard-color-chooser"
license=('unknown')
depends=('gtk3' 'python-gobject' 'polkit' 'system76-oled')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/ahoneybun/keyboard-color-switcher.git"
        "${pkgname%-git}.sh"
        "org.freedesktop.${pkgname%-git}.policy")
sha256sums=('SKIP'
            '1ba73e85dd83e75f95b3ee1c653458154d39dabd071ce4d4768208ae83a8bfcc'
            '63d13df412ded3d3e7e212270e2971aba5f7f201438a456342078b882c25f338')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm755 "${pkgname%-git}.py" -t "$pkgdir/opt/${pkgname%-git}"
	install -Dm755 "$srcdir/${pkgname%-git}.sh" "$pkgdir/usr/bin/${pkgname%-git}"
	install -Dm644 "$srcdir/org.freedesktop.${pkgname%-git}.policy" -t \
		"$pkgdir/usr/share/polkit-1/actions"
}
