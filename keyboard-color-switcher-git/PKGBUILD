pkgname=keyboard-color-switcher-git
pkgver=1.0.r5.g5d2e70f
pkgrel=4
pkgdesc="Change the Keyboard Backlight on System76 laptops"
arch=('any')
url="https://github.com/ahoneybun/keyboard-color-chooser"
license=('unknown')
depends=('gtk3' 'python-gobject' 'system76-oled' 'zensu')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/ahoneybun/keyboard-color-switcher.git"
        "${pkgname%-git}.sh")
sha256sums=('SKIP'
            '06fe73d2e2668dd60d31635a86080ac4b8a32af7b6a661cfb5be30fb9a5d9f9d')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "$srcdir/${pkgname%-git}"
	export PYTHONHASHSEED=0
	local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

	install -Dm755 "${pkgname%-git}.py" -t "$pkgdir$site_packages/${pkgname%-git}"
	cp -r kcc kcc_cli "$pkgdir$site_packages/${pkgname%-git}"
	python -O -m compileall "$pkgdir$site_packages/${pkgname%-git}"/{kcc,kcc_cli}

	install -Dm755 "$srcdir/${pkgname%-git}.sh" "$pkgdir/usr/bin/${pkgname%-git}"
}
