# https://archlinux.org/packages/community/x86_64/pulseeffects
# https://github.com/wwmm/pulseeffects/blob/master/PKGBUILD
pkgname=pulseeffects
pkgver=5.0.3
pkgrel=1
pkgdesc='Audio Effects for Pulseaudio Applications'
arch=('x86_64')
url='https://github.com/wwmm/pulseeffects'
license=('GPL3')
depends=('boost-libs' 'glibmm' 'gst-plugin-gtk' 'gst-plugin-pipewire'
         'gst-plugins-bad' 'gst-plugins-base' 'gstreamer' 'gtk3' 'gtkmm3'
         'libebur128' 'libsamplerate' 'libsigc++' 'libsndfile' 'lilv' 'pipewire'
         'pipewire-pulse' 'zita-convolver')
makedepends=('appstream-glib' 'boost' 'calf' 'itstool' 'meson' 'rnnoise'
             'rubberband' 'zam-plugins')
optdepends=('calf: limiter, compressor exciter, bass enhancer and others'
            'lsp-plugins: equalizer, delay'
            'rnnoise: noise remover'
            'rubberband: pitch shifting'
            'yelp: in-app help'
            'zam-plugins: maximizer')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('2e14858918b54bee5f6e4898cc803ae2170b4d624407fef39e0831b6584c4a4f')

build() {
	arch-meson $pkgname-$pkgver build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
