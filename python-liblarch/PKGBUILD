# https://aur.archlinux.org/packages/python-liblarch
groups=('modified')

pkgname=python-liblarch
pkgver=3.2.0
pkgrel=2
pkgdesc="Python library to easily handle data structure"
arch=('any')
url="https://wiki.gnome.org/Projects/liblarch"
license=('LGPL-3.0-or-later')
depends=('gtk3' 'python-gobject')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/liblarch/liblarch/archive/v${pkgver}.tar.gz")
b2sums=('92bf0c51b2f16e84633c75f51ccf321c8756b154bfa76412db5ff7b1879e27f0a24f69e2be94db10c9409b0b326d5ac4477c88eaf8f1be80ef9f3390ea26f879')

build() {
  cd "liblarch-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "liblarch-${pkgver}"
  pytest
}

package() {
  cd "liblarch-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim:set ts=2 sw=2 et:
