# https://aur.archlinux.org/packages/canta-gtk-theme-git
groups=('modified')

pkgname=('canta-gtk-theme-git' 'canta-icon-theme-git')
pkgbase=canta-theme-git
pkgdesc="Flat Material Design theme for GTK 3, GTK 2 and Gnome-Shell"
pkgver=2021.09.08.r6.332fdec
pkgrel=1
arch=('any')
url="https://github.com/vinceliuice/Canta-theme"
license=('GPL2')
makedepends=('git' 'sassc')
options=('!strip')
source=('git+https://github.com/vinceliuice/Canta-theme.git')
sha256sums=('SKIP')

pkgver() {
  cd Canta-theme
  git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

package_canta-gtk-theme-git() {
  optdepends=('gnome-themes-extra: GTK2 theme support'
              'gtk-engine-murrine: GTK2 theme support'
              'gtk-engines: GTK2 theme support'
              'canta-icon-theme: Matching icon theme')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  cd Canta-theme
  install -d "$pkgdir/usr/share/themes"

  # Install theme with Nautilus background image
  for theme in standard blue indigo; do
    ./install.sh -t ${theme} -d "$pkgdir/usr/share/themes"
  done

  # Install theme without Nautilus background image
#  for theme in standard blue indigo; do
#    ./install.sh -o -t ${theme} -d "$pkgdir/usr/share/themes"
#  done
}

package_canta-icon-theme-git() {
  pkgdesc="Flat icons for Canta GTK theme"
  optdepends=('numix-circle-icon-theme: if you want a better experience')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  cd Canta-theme
  install -d "$pkgdir/usr/share/icons"
  ./install.sh -i -d "$pkgdir/usr/share/icons"
}
