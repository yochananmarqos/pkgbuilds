pkgname=ryzen-controller
pkgver=2.2.2
pkgrel=1
pkgdesc="A minimal Electron application to use ryzenAdj through a friendly interface"
arch=('x86_64')
url="https://gitlab.com/le.storm1er/ryzen-controller"
license=('CC0-1.0')
depends=('ryzenadj' 'nss' 'gtk3' 'libxss')
makedepends=('yarn')
conflicts=('ryzencontroller')
source=("https://gitlab.com/le.storm1er/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop"
        "$pkgname.png")
sha256sums=('a0cb6a3e0bb3fb81de3ea43cdf6dbd7808410aff958877ddd6bfa7f463c2aae9'
            'b639208df8be054d41d6e7d7e697a955cfb23c713875ef5be5cfd07716f2ad48'
            '2a2d035008beb7c623e208bf5b6bf06c270157838c40d7375aefa9aee8f5f77f')

build() {
	cd "$pkgname-$pkgver"
	yarn install --cache "$srcdir/yarn-cache"
	yarn run dist-unpack
	yarn run dist-unpack:build
	yarn run dist-unpack:electron
}

package() {
	cd "$pkgname-$pkgver"
	rm -rf dist/linux-unpacked/resources/app.asar.unpacked/build

	install -d "$pkgdir/opt/$pkgname"
	cp -r dist/linux-unpacked/. "$pkgdir/opt/$pkgname"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/$pkgname/$pkgname" -t "$pkgdir/usr/bin"

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}
