pkgname=ryzen-controller
pkgver=2.2.5
pkgrel=1
pkgdesc="A minimal Electron application to use ryzenAdj through a friendly interface"
arch=('x86_64')
url="https://www.ryzencontroller.com"
license=('CC0-1.0')
depends=('ryzenadj' 'nss' 'gtk3' 'libxss')
makedepends=('yarn' 'python2')
conflicts=('ryzencontroller')
source=("https://gitlab.com/ryzen-controller-team/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop"
        "$pkgname.png")
sha256sums=('34569b4cd92e96c2b47f936221c5fd8a8726350709eae9fd0e0915b6a0d1240a'
            'b639208df8be054d41d6e7d7e697a955cfb23c713875ef5be5cfd07716f2ad48'
            '2a2d035008beb7c623e208bf5b6bf06c270157838c40d7375aefa9aee8f5f77f')

build() {
	cd "$pkgname-$pkgver"
	yarn install --cache "$srcdir/yarn-cache"
	yarn run dist-unpack
	yarn run dist-unpack:build
	yarn run dist-unpack:electron
}

package() {
	cd "$pkgname-$pkgver"
	rm -rf dist/linux-unpacked/resources/app.asar.unpacked/build

	install -d "$pkgdir/opt/$pkgname"
	cp -r dist/linux-unpacked/. "$pkgdir/opt/$pkgname"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/$pkgname/$pkgname" -t "$pkgdir/usr/bin"

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}
