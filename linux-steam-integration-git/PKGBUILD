# https://aur.archlinux.org/packages/linux-steam-integration-git
groups=('modified')

pkgname=linux-steam-integration-git
pkgver=0.7.2.r47.g99a5110
pkgrel=1
pkgdesc="Helper for enabling better Steam integration on Linux"
url="https://github.com/getsolus/linux-steam-integration"
arch=('x86_64' 'i686')
license=('LGPL2.1')
depends=('gtk3' 'steam')
makedepends=('git' 'meson' 'gcc-multilib')
optdepends=('steam-native-runtime: A package for installing all required deps for the native runtime')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/getsolus/linux-steam-integration.git'
        'git+https://github.com/intel/libnica.git'
        'partially-revert-2877813.patch')
sha512sums=('SKIP'
            'SKIP'
            'da5aeb435dc031b2be3fc27b8b9e5acbee51afff42f6f4db527ea4f034eb3601348439675e791d359a26070b45dda5a50fa204be2db72625c63d980aeeec01c4')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  git submodule init
  git config submodule.src/libnica.url "$srcdir/libnica"
  git -c protocol.file.allow=always submodule update

  # Partially revert 2877813 | Upstream issue #82
  # https://github.com/clearlinux/linux-steam-integration/pull/2
  patch -Np1 -i "$srcdir/partially-revert-2877813.patch"
}

build() {
  # 64-bit build
  meson --prefix /usr "${pkgname%-git}" build \
    -Dwith-shim=co-exist \
    -Dwith-frontend=true \
    -Dwith-steam-binary=/usr/bin/steam \
    -Dwith-new-libcxx-abi=true
  meson compile -C build

  # 32-bit build
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  meson --prefix /usr "${pkgname%-git}" build32 \
    -Dwith-shim=none \
    --libexecdir /usr/lib32 \
    --libdir /usr/lib32
  meson compile -C build32
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  DESTDIR="$pkgdir" meson install -C build32
}
