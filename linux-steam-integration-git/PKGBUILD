# https://aur.archlinux.org/packages/linux-steam-integration-git
pkgname=linux-steam-integration-git
pkgver=0.7.3.r5.g6137c52
pkgrel=1
pkgdesc="Helper for enabling better Steam integration on Linux"
url="https://github.com/clearlinux/linux-steam-integration"
arch=('x86_64' 'i686')
license=('LGPL2.1')
depends=('gtk3' 'steam')
makedepends=('git' 'meson' 'gcc-multilib')
optdepends=('steam-native-runtime: A package for installing all required deps for the native runtime')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/clearlinux/linux-steam-integration.git'
        'git+https://github.com/intel/libnica.git')
sha512sums=('SKIP'
            'SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git submodule init src/libnica
	git config submodule.libnica.url "$srcdir/libnica"
	git submodule update
}

build() {
	# 64-bit build
	meson --prefix /usr "${pkgname%-git}" build \
		-Dwith-shim=co-exist \
		-Dwith-frontend=true \
		-Dwith-steam-binary=/usr/bin/steam \
		-Dwith-new-libcxx-abi=true
	meson compile -C build

	# 32-bit build
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	meson --prefix /usr "${pkgname%-git}" build32 \
		-Dwith-shim=none \
		--libexecdir /usr/lib32 \
		--libdir /usr/lib32
	meson compile -C build32
}

package() {
	DESTDIR="$pkgdir" meson install -C build
	DESTDIR="$pkgdir" meson install -C build32
}

