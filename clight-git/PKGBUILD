# https://aur.archlinux.org/packages/clight-git
pkgname=clight-git
pkgver=4.0.r72.g767e184
pkgrel=1
pkgdesc="A C daemon that turns your webcam into a light sensor. It can also change display
         gamma temperature, dim your screen and set your dpms. Devel version."
arch=('i686' 'x86_64')
url="https://github.com/FedeDP/Clight"
license=('GPL')
depends=('clightd-git' 'gsl' 'hicolor-icon-theme' 'libconfig' 'libmodule>=5.0.0' 'popt'
         'systemd-libs>=237')
makedepends=('git' 'cmake' 'bash-completion')
optdepends=('geoclue: to retrieve user location through geoclue.'
            'upower: to save energy by increasing timeouts between captures while on battery
             and to autocalibrate keyboard backlight.'
            'bash-completion: to add support for bash automatic completion.')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=('etc/default/clight.conf')
install="${pkgname%-git}.install"
source=("${pkgname%-git}::git+https://github.com/FedeDP/Clight.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/${pkgname%-git}"
    cmake -B build -S . \
        -G "Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev
    make -C build
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make -C build DESTDIR="$pkgdir" install
}
