# https://aur.archlinux.org/packages/python-opentype-feature-freezer
groups=('modified')

pkgname=python-opentype-feature-freezer
_name=${pkgname#python-}
pkgver=1.32.2
pkgrel=1
pkgdesc="Permanently "apply" OpenType features to fonts, by remapping their Unicode assignments"
arch=('any')
url="https://twardoch.github.io/fonttools-opentype-feature-freezer"
license=('Apache-2.0')
depends=('python-fonttools')
makedepends=(
  'python-build'
  'python-configparser'
  'python-installer'
  'python-poetry-core'
  'python-wheel'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz"
        'poetry-core.patch')
sha256sums=('cdc93320bfee4e2f1455476f2b5618d82f47c0d86532f1b69673666adcc2b573'
            'fb608ffd901c8cb98707a31de85decbc0cdcc1da97ad1e48c804c07508675846')

prepare() {
  cd "$_name-$pkgver"

  # Use poetry-core
  patch -Np1 -i ../poetry-core.patch
}

build() {
  cd "$_name-$pkgver"
  GIT_DIR='.' python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
