# https://aur.archlinux.org/packages/journal-viewer
groups=('modified')

pkgname=journal-viewer
pkgver=0.6.0
pkgrel=1
pkgdesc="A modern linux desktop application to visualize systemd logs."
arch=('x86_64')
url="https://github.com/mingue/journal-viewer"
license=('GPL-3.0-or-later')
depends=('systemd-libs' 'webkit2gtk')
makedepends=('cargo' 'npm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('70a5d8ef670d6426ac55643d280f69e3f882befa8ebf59813d500b8b429386b2')

prepare() {
  cd "$pkgname-$pkgver"
  export npm_config_cache="$srcdir/npm_cache"
  npm install

  cd src-tauri
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"

  # Don't bundle AppImage
  sed -i 's/"targets": "all",/"targets": "deb",/g' tauri.conf.json
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export npm_config_cache="$srcdir/npm_cache"
  npm run build
  npm run tauri build
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "src-tauri/target/release/$pkgname" -t "$pkgdir/usr/bin/"

  _bundle_dir="src-tauri/target/release/bundle/deb/${pkgname}_0.0.1_amd64"

  install -Dm644 "${_bundle_dir}/data/usr/share/applications/$pkgname.desktop" -t \
    "$pkgdir/usr/share/applications/"

  for icon_size in 32x32 128x128; do
    install -Dm644 "${_bundle_dir}/data/usr/share/icons/hicolor/${icon_size}/apps/$pkgname.png" -t \
      "$pkgdir/usr/share/icons/hicolor/${icon_size}/apps/"
  done
  install -Dm644 "${_bundle_dir}/data/usr/share/icons/hicolor/256x256@2/apps/$pkgname.png" -t \
      "$pkgdir/usr/share/icons/hicolor/256x256@2/apps/"
}
