# https://aur.archlinux.org/packages/journal-viewer
groups=('modified')

pkgname=journal-viewer
pkgver=0.5.0
pkgrel=1
pkgdesc="A modern linux desktop application to visualize systemd logs."
arch=('x86_64')
url="https://github.com/mingue/journal-viewer"
license=('GPL3')
depends=('systemd-libs' 'webkit2gtk')
makedepends=('cargo' 'npm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('f898027f8c0fc5088ebad1b2d728e0a19de3829e5bddb0056a4bacac85544500')

prepare() {
  cd "$pkgname-$pkgver"
  export npm_config_cache="$srcdir/npm_cache"
  npm install

  cd src-tauri
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"

  # Don't bundle AppImage
  sed -i 's/"targets": "all",/"targets": "deb",/g' tauri.conf.json
}

build() {
  cd "$pkgname-$pkgver"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  export npm_config_cache="$srcdir/npm_cache"
  npm run build
  npm run tauri build
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "src-tauri/target/release/$pkgname" -t "$pkgdir/usr/bin/"

  _bundle_dir="src-tauri/target/release/bundle/deb/${pkgname}_0.0.1_amd64"

  install -Dm644 "${_bundle_dir}/data/usr/share/applications/$pkgname.desktop" -t \
    "$pkgdir/usr/share/applications/"

  for icon_size in 32x32 128x128; do
    install -Dm644 "${_bundle_dir}/data/usr/share/icons/hicolor/${icon_size}/apps/$pkgname.png" -t \
      "$pkgdir/usr/share/icons/hicolor/${icon_size}/apps/"
  done
  install -Dm644 "${_bundle_dir}/data/usr/share/icons/hicolor/256x256@2/apps/$pkgname.png" -t \
      "$pkgdir/usr/share/icons/hicolor/256x256@2/apps/"
}
