# https://www.archlinux.org/packages/community/x86_64/python-pillow
pkgname=python-pillow
_name=Pillow
pkgver=7.2.0
pkgrel=1
pkgdesc="The friendly PIL fork (Python Imaging Library)"
arch=('x86_64')
url="https://python-pillow.github.io"
license=('BSD')
depends=('python' 'lcms2' 'libtiff' 'openjpeg2' 'libimagequant' 'libxcb')
optdepends=('freetype2: for the ImageFont module'
            'libraqm: for complex text scripts'
            'libwebp: for webp images'
            'tk: for the ImageTK module'
            'python-olefile: OLE2 file support'
            'python-pyqt5: for the ImageQt module')
makedepends=('python-setuptools' 'freetype2' 'libraqm' 'libwebp' 'tk')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('97f9e7953a77d5a70f49b9a48da7776dc51e9b738151b22dacf101641594a626')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"

	local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

	export PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}"
	python selftest.py
	pytest -k 'not test_complex_unicode_text and not TestImageFont'
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -dm755 "$pkgdir/usr/include/python$python_version"
	install -m644 src/libImaging/*.h -t "$pkgdir/usr/include/python$python_version"
}
