# https://aur.archlinux.org/packages/rclone-browser

pkgname=rclone-browser
pkgver=1.8.0
pkgrel=3
pkgdesc="Simple cross-platform GUI for rclone"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/kapitainsky/RcloneBrowser"
license=('MIT')
depends=(
  'hicolor-icon-theme'
  'qt5-base'
  'rclone'
)
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('5f8242a011b85477749127b7e94e874035c431c2fa6df817e5603ed891604beb')

prepare() {
  cd "RcloneBrowser-$pkgver"
  sed -i 's/-Werror/-Wno-deprecated-declarations/g' src/CMakeLists.txt
}

build() {
  cmake -B build -S "RcloneBrowser-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_POLICY_VERSION_MINIMUM='3.5' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "RcloneBrowser-$pkgver"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
