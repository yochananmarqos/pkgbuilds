pkgname=libsfdo
pkgver=0.1.3
pkgrel=2
pkgdesc="A collection of libraries which implement some of the freedesktop.org specifications."
arch=('x86_64')
url="https://gitlab.freedesktop.org/vyivel/libsfdo"
license=('BSD-2-Clause')
depends=('glibc')
makedepends=('meson')
source=("https://gitlab.freedesktop.org/vyivel/libsfdo/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('d1f2d8b5edec827fd386c22d6f9151377ec7c194dca4d293e3abad9df9974209')

build() {
  arch-meson "$pkgname-v$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgname-v$pkgver"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
