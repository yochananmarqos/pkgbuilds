# https://aur.archlinux.org/pkgbase/gnome-shell-extension-valent-git/
groups=('modified')

pkgname=gnome-shell-extension-valent-git
pkgver=r100.e9814ff
pkgrel=1
pkgdesc="GNOME Shell integration for Valent"
arch=('any')
url="https://valent.andyholmes.ca"
license=('GPL3')
depends=('gnome-shell>=1:45.0' 'valent')
makedepends=('git' 'meson')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" 'gnome-shell-extension-gsconnect')
source=('git+https://github.com/andyholmes/gnome-shell-extension-valent.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
