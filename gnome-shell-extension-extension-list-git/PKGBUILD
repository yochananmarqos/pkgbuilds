# https://aur.archlinux.org/packages/gnome-shell-extension-extension-list-git
groups=('modified')

pkgname=gnome-shell-extension-extension-list-git
pkgver=45.0.r0.g7f4c9f3
pkgrel=1
pkgdesc="Simple GNOME Shell extension manager in the top panel."
arch=('any')
url="https://github.com/tuberry/extension-list"
license=('GPL3')
depends=('gnome-shell>=1:45.0')
makedepends=('git' 'meson' 'sassc')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/tuberry/extension-list.git'
        'git+https://gitlab.gnome.org/GNOME/gnome-shell-sass.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/extension-list"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/extension-list"
  git submodule init
  git config submodule.res/styles/gnome-shell-sass.url "$srcdir/gnome-shell-sass"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson extension-list build -Dtarget=system
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
