# https://aur.archlinux.org/packages/eddie-ui
groups=('modified')

pkgname=eddie-ui
pkgver=2.24.2
pkgrel=1
pkgdesc='Eddie - VPN tunnel - UI'
arch=('x86_64' 'aarch64' 'armv7l')
url="https://eddie.website"
license=('GPL3')
depends=('gtk3' 'libayatana-appindicator' 'libnotify' 'mono' 'openvpn' 'polkit' 'sudo')
makedepends=('git' 'dotnet-sdk-7.0' 'mono-msbuild' 'patchelf')
optdepends=('hummingbird: increased OpenVPN performance'
            'openssh: VPN over SSH'
            'stunnel: VPN over SSL')
conflicts=('airvpn')
#source=("Eddie-$pkgver.tar.gz::https://github.com/AirVPN/Eddie/archive/$pkgver.tar.gz")
_commit=c201d5605664e3a22db4af1b2b038aaba9a50211  # 2.24.2
source=("git+https://github.com/AirVPN/Eddie.git#commit=${_commit}")
sha256sums=('SKIP')

ARCH=$CARCH
RID=linux-$CARCH
if [ "${RID}" = "linux-x86_64" ]; then
  ARCH=x64
  RID=linux-x64
elif [ "${RID}" = "linux-aarch64" ]; then
  RID=linux-arm64
elif [ "${RID}" = "linux-armv7l" ]; then
  RID=linux-arm
fi

build() {
  cd Eddie

  # CLI
  chmod +x src/App.CLI.Linux/postbuild.sh
  chmod +x src/Lib.Platform.Linux.Native/build.sh

  pushd src/App.CLI.Linux
  dotnet publish App.CLI.Linux.net7.csproj \
    --configuration Release \
    --runtime "${RID}" \
    --self-contained true \
    -p:PublishTrimmed=true \
    -p:EnableCompressionInSingleFile=true
  popd

  # UI
  chmod +x src/App.UI.Linux/build.sh
  src/App.UI.Linux/build.sh Release

  msbuild /verbosity:minimal /p:Configuration=Release /p:Platform=x64 /t:Rebuild src/App.Forms.Linux//App.Forms.Linux.sln /p:DefineConstants="EDDIEMONO4LINUX"

  # msbuild/Mono under Linux doesn't honor the postbuild event, called manually
  src/App.Forms.Linux/postbuild.sh src/App.Forms.Linux/bin/x64/Release/ "${ARCH}" Release

  # mkbundle
  if [ $ARCH = "x64" ]; then
    MKBUNDLECROSSTARGET="mono-6.8.0-debian-10-x64"
  elif [ $ARCH = "armv7l" ]; then
    MKBUNDLECROSSTARGET="mono-6.0.0-raspbian-9-arm"
  elif [ $ARCH = "aarch64" ]; then
    MKBUNDLECROSSTARGET="mono-6.6.0-debian-10-arm64"
  else
    MKBUNDLECROSSTARGET="mono-6.8.0-debian-10-${ARCH}"
  fi

  # Generate man
  "src/App.CLI.Linux/bin/Release/net7.0/${RID}/publish/eddie-cli" --path.resources="../../../../../../resources" --help --help.format=man >"eddie-cli.8"
}

package() {
  cd Eddie
  install -Dm755 "src/App.CLI.Linux.Elevated/bin/eddie-cli-elevated" \
    "$pkgdir/usr/lib/$pkgname/eddie-cli-elevated"
  install -Dm644 "src/Lib.Core/bin/x64/Release/Lib.Core.dll" \
    "$pkgdir/usr/lib/$pkgname/Lib.Core.dll"
  install -Dm644 "src/Lib.Platform.Linux/bin/x64/Release/Lib.Platform.Linux.dll" \
    "$pkgdir/usr/lib/$pkgname/Lib.Platform.Linux.dll"
  install -Dm644 "src/Lib.Platform.Linux.Native/bin/libLib.Platform.Linux.Native.so" \
    "$pkgdir/usr/lib/$pkgname/libLib.Platform.Linux.Native.so"  
  install -Dm755 "repository/linux_arch/bundle/$pkgname/usr/bin/$pkgname" \
    "$pkgdir/usr/bin/$pkgname"
  install -Dm644 common/manifest.json "$pkgdir/usr/share/$pkgname/manifest.json"
  install -Dm644 common/libraries.txt "$pkgdir/usr/share/$pkgname/libraries.txt"
  install -Dm644 common/cacert.pem "$pkgdir/usr/share/$pkgname/cacert.pem"
  install -Dm644 common/icon.png "$pkgdir/usr/share/$pkgname/icon.png"
  install -Dm644 common/icon_gray.png "$pkgdir/usr/share/$pkgname/icon_gray.png"
  install -Dm644 common/icon.png "$pkgdir/usr/share/$pkgname/tray.png"
  install -Dm644 common/icon_gray.png "$pkgdir/usr/share/$pkgname/tray_gray.png"
  install -Dm644 common/iso-3166.json "$pkgdir/usr/share/$pkgname/iso-3166.json"
  install -Dm644 common/lang/inv.json "$pkgdir/usr/share/$pkgname/lang/inv.json"
  install -Dm644 common/providers/AirVPN.json "$pkgdir/usr/share/$pkgname/providers/AirVPN.json"
  install -Dm644 common/providers/OpenVPN.json "$pkgdir/usr/share/$pkgname/providers/OpenVPN.json"
  install -Dm644 common/providers/WireGuard.json \
    "$pkgdir/usr/share/$pkgname/providers/WireGuard.json"
  install -Dm644 "repository/linux_arch/bundle/$pkgname/usr/share/polkit-1/actions/org.airvpn.eddie.ui.elevated.policy" \
    "$pkgdir/usr/share/polkit-1/actions/org.airvpn.eddie.ui.elevated.policy"

  if [ "ui" = "cli" ]; then
    install -Dm755 "src/App.CLI.Linux/bin/x64/Release/App.CLI.Linux.exe" \
      "$pkgdir/usr/lib/$pkgname/$pkgname.exe"
  elif [ "ui" = "ui" ]; then
    install -Dm755 "src/App.Forms.Linux/bin/x64/Release/App.Forms.Linux.exe" \
      "$pkgdir/usr/lib/$pkgname/$pkgname.exe"
    install -Dm644 "src/App.Forms.Linux/bin/x64/Release/Lib.Forms.dll" \
      "$pkgdir/usr/lib/$pkgname/Lib.Forms.dll"
    install -Dm644 "src/App.Forms.Linux/bin/x64/Release/Lib.Forms.Skin.dll" \
      "$pkgdir/usr/lib/$pkgname/Lib.Forms.Skin.dll"
    install -Dm755 "src/UI.GTK.Linux.Tray/bin/eddie-tray" "$pkgdir/usr/lib/$pkgname/eddie-tray"
    install -Dm644 "repository/linux_arch/bundle/$pkgname/usr/share/pixmaps/$pkgname.png" \
      "$pkgdir/usr/share/pixmaps/$pkgname.png"
  fi
}
