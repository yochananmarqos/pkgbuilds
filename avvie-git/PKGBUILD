# https://aur.archlinux.org/packages/avvie-git
groups=('modified')

pkgname=avvie-git
_app_id=com.github.taiko2k.avvie
pkgver=2.0.r17.gdcf94d9
pkgrel=1
pkgdesc="A GTK app for quick image cropping"
arch=('any')
url="https://github.com/Taiko2k/avvie"
license=('GPL3')
depends=('libadwaita' 'libnotify' 'python-cairo' 'python-gobject' 'python-piexif' 'python-pillow')
makedepends=('meson' 'git')
checkdepends=('appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Taiko2k/avvie.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
