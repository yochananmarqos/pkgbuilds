# https://aur.archlinux.org/packages/layan-gtk-theme-git

pkgname=layan-gtk-theme-git
pkgver=2023.05.23.r37.ge7b9b36
pkgrel=1
pkgdesc="A flat Material Design theme for GTK 3, GTK 2 and Gnome-Shell"
arch=('any')
url="https://github.com/vinceliuice/Layan-gtk-theme"
license=('GPL-3.0-or-later')
makedepends=('git')
optdepends=(
  'gtk-engine-murrine: GTK2 theme support'
  'gtk-engines: GTK2 theme support'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
install="${pkgname%-git}.install"
source=('git+https://github.com/vinceliuice/Layan-gtk-theme.git')
sha256sums=('SKIP')

pkgver(){
  cd Layan-gtk-theme
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd Layan-gtk-theme
  install -d "$pkgdir/usr/share/themes"
  ./install.sh -d "$pkgdir/usr/share/themes"

  # Plank theme
  install -Dm644 src/plank/dock.theme -t "$pkgdir/usr/share/plank/themes/Layan/"

  # Wallpapers
  install -Dm644 src/wallpaper/*.png -t "$pkgdir/usr/share/backgrounds/layan/"
}
