# https://aur.archlinux.org/packages/qt-sudo

pkgname=qt-sudo
pkgver=r7.176507a
pkgrel=1
pkgdesc="A clone of LXQt sudo tool, without LXQt libs"
arch=('x86_64')
url="https://github.com/aarnt/qt-sudo"
license=('LGPL-2.1-or-later')
depends=('qt6-base')
makedepends=('git' 'qt6-tools')
_commit=176507aa64f384e661400a164cd633d324555f6c  # branch/master
source=("git+https://github.com/aarnt/qt-sudo.git#commit=${_commit}")
sha256sums=('52c461385416eba0cf50424df83c7e18f5f0999ffff28f3f1098e476bcfdb85a')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname"
  qmake6 \
    PREFIX=/usr \
    QMAKE_CFLAGS="${CFLAGS}" \
    QMAKE_CXXFLAGS="${CXXFLAGS}" \
    QMAKE_LFLAGS="${LDFLAGS}"
  make
}

package() {
  cd "$pkgname"
  make INSTALL_ROOT="${pkgdir}" install
}
