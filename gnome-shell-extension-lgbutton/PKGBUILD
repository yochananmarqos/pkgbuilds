pkgname=gnome-shell-extension-lgbutton
_uuid=lgbutton@glerro.gnome.gitlab.io
pkgver=2.3.0
pkgrel=1
pkgdesc="Toggle the Looking Glass visibility by clicking on a panel icon."
arch=('any')
url="https://gitlab.gnome.org/glerro/gnome-shell-extension-lgbutton"
license=('GPL-3.0-or-later')
depends=('gnome-shell')
makedepends=('git' 'meson')
source=("git+https://gitlab.gnome.org/glerro/gnome-shell-extension-lgbutton.git#tag=v$pkgver")
sha256sums=('3e096e606f8e3c25694e5bb49bcb84c3899ef616b64211beb24415a98181ccc7')

build() {
  arch-meson "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share/"

  install -Dm644 "$pkgname/${_uuid}/schemas/org.gnome.shell.extensions.lgbutton.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -vr "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
}
