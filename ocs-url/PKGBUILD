# https://aur.archlinux.org/pkgbase/ocs-url
groups=('modified')

pkgname=ocs-url
pkgver=3.1.0
pkgrel=8
pkgdesc='An install helper program for items served via OpenCollaborationServices (ocs://).'
arch=('x86_64')
url="https://www.opendesktop.org/p/1136805"
license=('GPL3')
depends=('qt5-declarative' 'qt5-quickcontrols' 'qt5-svg')
makedepends=('git')
provides=('xdgurl')
conflicts=('xdgurl')
source=("git+https://www.opencode.net/dfn2/ocs-url.git#tag=release-$pkgver"
        'git+https://github.com/akiraohgaki/qtil.git#tag=v0.4.0')
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname"
  ln -s "$srcdir/qtil" lib/qtil
}

build() {
  cd "$srcdir/$pkgname"
  qmake PREFIX='/usr'
  make
}

package() {
  cd "$srcdir/$pkgname"
  make INSTALL_ROOT="$pkgdir" install
}
