# https://aur.archlinux.org/packages/clapgrep

pkgname=clapgrep
pkgver=25.05
pkgrel=1
pkgdesc="One app to search through all your files"
arch=('x86_64')
url="https://github.com/luleyleo/clapgrep"
license=('GPL-3.0-or-later')
depends=(
  'gtk4'
  'gtksourceview5'
  'libadwaita'
  'poppler-glib'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'meson'
)
checkdepends=('appstream-glib')
optdepends=('nautilus-python: Nautilus extension')
source=("${pkgname}-${pkgver}+1.tar.gz::${url}/archive/refs/tags/v${pkgver}+1.tar.gz")
sha256sums=('8fba493b7398ed01979c0ba9450308c219f498d6da8570799f0c2a1ac7d10d9d')

prepare() {
  cd "${pkgname}-${pkgver}-1"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"

  # This is not a Flatpak
  sed -i 's|flatpak run de.leopoldluley.Clapgrep|/usr/bin/clapgrep|g' \
    "extension/${pkgname}.py"
}

build() {
	export RUSTUP_TOOLCHAIN=stable
	arch-meson "${pkgname}-${pkgver}-1" build
  meson compile -C build
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  cd "${pkgname}-${pkgver}-1"
  install -Dvm644 "extension/${pkgname}.py" -t \
    "${pkgdir}/usr/share/nautilus-python/extensions/"
}
