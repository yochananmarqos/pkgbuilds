# https://aur.archlinux.org/packages/clapgrep

pkgname=clapgrep
pkgver=25.03
pkgrel=1
pkgdesc="One app to search through all your files"
arch=('x86_64')
url="https://github.com/luleyleo/clapgrep"
license=('GPL-3.0-or-later')
depends=(
  'gtk4'
  'gtksourceview5'
  'libadwaita'
  'poppler-glib'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'meson'
)
checkdepends=('appstream-glib')
optdepends=('nautilus-python: Nautilus extension')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('15e559c3a3cf250350e64c13a14b3808128880449d12e37b4dd28c21e9796cda')

prepare() {
  cd "${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"

  # This is not a Flatpak
  sed -i 's|flatpak run de.leopoldluley.Clapgrep|/usr/bin/clapgrep|g' \
    "extension/${pkgname}.py"
}

build() {
	export RUSTUP_TOOLCHAIN=stable
	arch-meson "${pkgname}-${pkgver}" build
  meson compile -C build
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  cd "${pkgname}-${pkgver}"
  install -Dvm644 "extension/${pkgname}.py" -t \
    "${pkgdir}/usr/share/nautilus-python/extensions/"
}
