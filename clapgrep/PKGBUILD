# https://aur.archlinux.org/packages/clapgrep

pkgname=clapgrep
_app_id=de.leopoldluley.Clapgrep
pkgver=1.3.1
pkgrel=2
pkgdesc="One app to search through all your files"
arch=('x86_64')
url="https://github.com/luleyleo/clapgrep"
license=('GPL-3.0-or-later')
depends=(
  'gtk4'
  'libadwaita'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'just'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('1a0ca6e3c939956fa634c95b442d690137c1ad1e1b76fdcfdbf45f4b6a3a2603')

prepare() {
  cd "${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	just release=true build --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  appstreamcli validate --no-net "assets/${_app_id}.metainfo.xml"
  desktop-file-validate "assets/${_app_id}.desktop"
}

package() {
	cd "${pkgname}-${pkgver}"
	just release=true rootdir="${pkgdir}" install
}
