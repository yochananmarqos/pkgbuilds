# https://aur.archlinux.org/packages/netpeek

pkgname=netpeek
pkgver=0.2.4
pkgrel=1
pkgdesc="Modern network scanner for GNOME"
arch=('any')
url="https://github.com/ZingyTomato/NetPeek"
license=('GPL-3.0-or-later')
depends=(
  'gtk4'
  'libadwaita'
  'python-gobject'
  'python-nmap'
)
makedepends=(
  'git'
  'meson'
)
source=("git+https://github.com/ZingyTomato/NetPeek.git#tag=v${pkgver}")
sha256sums=('9e6f8c11480de1477483ca9c04905dbc609220feba8315e3e1b40e59eb670cc4')

build() {
  arch-meson NetPeek build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
