# https://aur.archlinux.org/packages/python-colored

pkgname=python-colored
_name=${pkgname#python-}
pkgver=2.3.0
pkgrel=1
pkgdesc="Simple python library for color and formatting to terminal"
arch=('any')
url="https://dslackw.gitlab.io/colored"
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-flit'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("https://gitlab.com/dslackw/colored/-/archive/$pkgver/$_name-$pkgver.tar.gz"
        'tests-set-tty-aware-false.patch')
sha256sums=('ffc3821b951fa22d834062608adbcf17b0347c4fb81aea867a4965e22d06682f'
            '0946c4592ac78b89f18e88b55717e65e7a55ba48983ee6b086f134c6764514ff')

prepare() {
  cd "$_name-$pkgver"
  patch -Np1 -i ../tests-set-tty-aware-false.patch

  # remove shebangs
  sed -i '/#!\/usr\/bin\/env python/d' colored/*.py
}

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_name-$pkgver"
  pytest
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
