# https://www.archlinux.org/packages/community/any/nicotine+
pkgname=nicotine+
pkgver=2.1.1
pkgrel=1
pkgdesc="Soulseek music-sharing client, written in Python"
arch=('any')
url="https://github.com/Nicotine-Plus/nicotine-plus"
license=('GPL')
depends=('python-pytaglib' 'python-gobject' 'gtk3' 'libnotify')
optdepends=('python-miniupnpc: UPnP support'
            'python-feedparser: for Reddit plugin'
            'gspell: for spell checking in chat'
            'nuspell: for spell checking in chat'
            'libappindicator-gtk3: for tray icon'
            'gsound: for sound effects')
source=("$url/archive/$pkgver/nicotine-$pkgver.tar.gz")
sha256sums=('311ef0dea8c4a8d2df3e362befd1ea6a37bc0b272f74b81d416130fc0bd45560')

build() {
	cd nicotine-plus-$pkgver
	python setup.py build
}

package() {
	cd nicotine-plus-$pkgver
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
