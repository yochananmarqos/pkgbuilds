# https://aur.archlinux.org/packages/python-apt
groups=('modified')

pkgname=python-apt
pkgver=2.8.0
pkgrel=1
pkgdesc="Python 3 interface to libapt-pkg"
arch=('x86_64')
url="https://tracker.debian.org/pkg/python-apt"
license=('GPL-2.0-or-later')
depends=(
  'apt'
  'python'
)
makedepends=(
  'python-build'
  'python-distutils-extra'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("https://salsa.debian.org/apt-team/python-apt/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('8bd8c8304d3dab00d5645e1652d3e057da9dcd093e2b589daa8d35865c9485f7')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
