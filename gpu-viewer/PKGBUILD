# https://aur.archlinux.org/packages/gpu-viewer
groups=('modified')

pkgname=gpu-viewer
_app_id=io.github.arunsivaramanneo.GPUViewer
pkgver=3.04
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer"
license=('GPL-3.0-or-later')
depends=(
  'clinfo'
  'gtk4'
  'lsb-release'
  'mesa-utils'
  'python-gobject'
  'vdpauinfo'
  'vulkan-tools'
)
makedepends=('meson')
checkdepends=('appstream')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('cb45a5e8bcd43e678f5a19e9f001573dea2ebca4fb5f8d5d3909c6b4a72827b0')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstreamcli validate --no-net "data/$_app_id.metainfo.xml"
  desktop-file-validate "data/$_app_id.desktop"
}

package() {
  meson install -C build --destdir "$pkgdir"
}
