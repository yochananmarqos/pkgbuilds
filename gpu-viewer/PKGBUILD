# https://aur.archlinux.org/packages/gpu-viewer

pkgname=gpu-viewer
_app_id=io.github.arunsivaramanneo.GPUViewer
pkgver=3.10
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer"
license=('GPL-3.0-or-later')
depends=(
  'clinfo'
  'libadwaita'
  'lsb-release'
  'mesa-utils'
  'python-gobject'
  'vdpauinfo'
  'vulkan-tools'
)
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('228204e165f20cb311d6874aa846be695593f8063fa28f148369edd2bacf35ec')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstreamcli validate --no-net "data/${_app_id}.metainfo.xml"
  desktop-file-validate "data/${_app_id}.desktop"
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
