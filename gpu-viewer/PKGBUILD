# https://aur.archlinux.org/packages/gpu-viewer
groups=('modified')

pkgname=gpu-viewer
_app_id=io.github.arunsivaramanneo.GPUViewer
pkgver=3.06
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer"
license=('GPL-3.0-or-later')
depends=(
  'clinfo'
  'libadwaita'
  'lsb-release'
  'mesa-utils'
  'python-gobject'
  'vdpauinfo'
  'vulkan-tools'
)
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('6d108fe4c206caea717c29d03742aef4c73c582e1f4cb9a9a88bcc3ee3b706f8')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstreamcli validate --no-net "data/${_app_id}.metainfo.xml"
  desktop-file-validate "data/${_app_id}.desktop"
}

package() {
  meson install -C build --destdir "$pkgdir"
}
