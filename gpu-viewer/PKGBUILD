# https://aur.archlinux.org/packages/gpu-viewer
groups=('modified')

pkgname=gpu-viewer
pkgver=2.24
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer/"
license=('GPL3')
depends=('clinfo' 'gtk4' 'lsb-release' 'mesa-demos' 'mesa-utils' 'python'
         'python-gobject' 'vdpauinfo' 'vulkan-tools')
makedepends=('meson')
checkdepends=('appstream-glib')
optdepends=('nvidia: Vulkan NVIDIA drivers'
            'vulkan-radeon: Vulkan AMD drivers'
            'vulkan-intel: Vulkan Intel drivers')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('db25dd4b3792fc52d6cb344abe5e733ac445b045ed067f9a44258439f2e04bba')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstream-util validate-relax --nonet data/*.metainfo.xml
  desktop-file-validate data/*.desktop
}

package() {
  meson install -C build --destdir "$pkgdir"
}
