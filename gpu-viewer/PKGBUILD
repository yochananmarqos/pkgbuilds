# https://aur.archlinux.org/packages/gpu-viewer

pkgname=gpu-viewer
_app_id=io.github.arunsivaramanneo.GPUViewer
pkgver=3.23
pkgrel=1
pkgdesc="A front-end to glxinfo, vulkaninfo, clinfo and es2_info"
arch=('any')
url="https://github.com/arunsivaramanneo/GPU-Viewer"
license=('GPL-3.0-or-later')
depends=(
  'clinfo'
  'gtk4'
  'libadwaita'
  'lsb-release'
  'mesa-utils'
  'python-gobject'
  'vdpauinfo'
  'vulkan-tools'
  'xorg-xdpyinfo'
)
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('848a00023cdfb1fee4887ae3f000c1767532e4b03368684ea31c6413ce6e11c4')

build() {
  arch-meson GPU-Viewer-$pkgver build
  meson compile -C build
}

check() {
  cd GPU-Viewer-$pkgver
  appstreamcli validate --no-net "data/${_app_id}.metainfo.xml"
  desktop-file-validate "data/${_app_id}.desktop"
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
