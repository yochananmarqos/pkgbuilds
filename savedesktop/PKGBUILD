pkgname=savedesktop
_app_id=io.github.vikdevelop.SaveDesktop
pkgver=2.4.4
pkgrel=1
pkgdesc="Save and load KDE Plasma, Xfce and GNOME-based DE configuration"
arch=('any')
url="https://github.com/vikdevelop/SaveDesktop"
license=('GPL3')
depends=('hicolor-icon-theme' 'libadwaita' 'python-gobject')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
        "$pkgname.sh"
        'main_window.diff')
sha256sums=('9fc2056cbb91daf149d8636e767427a1f048151792f6096de78ea5acb709784d'
            '0ca4158d352a04643b0161750b7c5c8fefe9c4066ff4e5129a2f8c38cb53bf69'
            '4cbb7a762a9b7666c6b5136c40476017c3e83e091b7d7641b749cca3766786b8')

prepare() {
  cd "SaveDesktop-$pkgver"

  # Desktop file Exec path
  sed -i "s|Exec=run.sh|Exec=/usr/share/$pkgname/main_window.py|g" \
    "flatpak/${_app_id}.desktop"

  # Correct translations path
  patch --strip=1 src/main_window.py < ../main_window.diff

  # Correct version
#  sed -i 's|version = "2.4"|version = "2.4.1"|g' src/main_window.py
}

check() {
  cd "SaveDesktop-$pkgver"
  appstream-util validate-relax --nonet "flatpak/${_app_id}.metainfo.xml"
  desktop-file-validate "flatpak/${_app_id}.desktop"
}

package() {
  cd "SaveDesktop-$pkgver"
  install -Dm755 src/main_window.py -t "$pkgdir/usr/share/$pkgname/"
  install -m644 src/periodic_saving.py -t "$pkgdir/usr/share/$pkgname/"
  cp -R translations "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "flatpak/${_app_id}.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "flatpak/${_app_id}.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"
  install -Dm644 "flatpak/icons/${_app_id}.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
  install -Dm644 "flatpak/icons/${_app_id}-symbolic.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/symbolic/apps/"
  install -Dm644 flatpak/symbolic-icons/exclamation_mark.png -t \
    "$pkgdir/usr/share/icons/hicolor/symbolic/actions/"
  install -Dm644 flatpak/symbolic-icons/desktop-symbolic.svg -t \
     "$pkgdir/usr/share/icons/hicolor/symbolic/actions/"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
