pkgname=savedesktop
_app_id=io.github.vikdevelop.SaveDesktop
pkgver=2.4.1
pkgrel=1
pkgdesc="Save and load KDE Plasma, Xfce and GNOME-based DE configuration"
arch=('any')
url="https://github.com/vikdevelop/SaveDesktop"
license=('GPL3')
depends=('hicolor-icon-theme' 'libadwaita' 'python-gobject')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
        "$pkgname.sh")
sha256sums=('235d27820f69dc48925af0539eaae5011997eb6990dc36e0bddd61030c5c673c'
            '0ca4158d352a04643b0161750b7c5c8fefe9c4066ff4e5129a2f8c38cb53bf69')

prepare() {
  cd "SaveDesktop-$pkgver"

  # Desktop file Exec path
  sed -i "s|Exec=run.sh|Exec=/usr/share/$pkgname/main_window.py|g" \
    "flatpak/${_app_id}.desktop"

  # Correct translations path
  sed -i "s|/app/translations/|/usr/share/$pkgname/translations/|g" \
    src/main_window.py
  sed -i 's|locale = open(f"translations/{lang}")|locale = open(f"/usr/share/savedesktop/translations/{lang}")|g' \
    src/main_window.py

  # Correct version
  sed -i 's|version = "2.4"|version = "2.4.1"|g' src/main_window.py
}

check() {
  cd "SaveDesktop-$pkgver"
  appstream-util validate-relax --nonet "flatpak/${_app_id}.metainfo.xml"
  desktop-file-validate "flatpak/${_app_id}.desktop"
}

package() {
  cd "SaveDesktop-$pkgver"
  install -Dm755 src/{main_window,periodic_saving}.py -t "$pkgdir/usr/share/$pkgname/"
  cp -R translations "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "flatpak/${_app_id}.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "flatpak/${_app_id}.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"
  install -Dm644 "flatpak/icons/${_app_id}.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
  install -Dm644 "flatpak/icons/${_app_id}-symbolic.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/symbolic/apps/"
  install -Dm644 flatpak/symbolic-icons/exclamation_mark.png -t \
    "$pkgdir/usr/share/icons/hicolor/symbolic/actions/"
  install -Dm644 flatpak/symbolic-icons/desktop-symbolic.svg -t \
     "$pkgdir/usr/share/icons/hicolor/symbolic/actions/"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
