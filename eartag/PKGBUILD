# https://aur.archlinux.org/packages/eartag
groups=('modified')

pkgname=eartag
pkgver=0.6.2
pkgrel=1
pkgdesc="Simple music tag editor"
arch=('any')
url="https://gitlab.gnome.org/World/eartag"
license=('MIT')
depends=(
  'chromaprint'
  'libadwaita'
  'python-gobject'
  'python-magic'
  'python-mutagen'
  'python-pillow'
  'python-pyacoustid'
  'python-requests'
)
makedepends=(
  'blueprint-compiler'
  'meson'
)
checkdepends=(
  'python-pytest'
)
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('eb633d85596cf247eaf9cf94319d6e360b1e776c1e715d99b6506acff3a4a41a5a6de762e166d93c97e859368804ac626ebb17f2c8a8b4e3c0f4481c13a0a2ba')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgname-$pkgver"
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
