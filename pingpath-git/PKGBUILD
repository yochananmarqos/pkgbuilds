# https://aur.archlinux.org/packages/pingpath

pkgname=pingpath-git
pkgver=r156.c4d5707
pkgrel=1
pkgdesc="Ping wrapper for network diagnostic"
arch=('x86_64')
url="https://github.com/yvs2014/pingpath"
license=('GPL-2.0-or-later')
depends=(
  'cglm'
  'gtk4'
)
makedepends=(
  'git'
  'meson'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/yvs2014/pingpath.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs

  cd "${pkgname%-git}"
  desktop-file-validate "assets/${pkgname%-git}.desktop"
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
