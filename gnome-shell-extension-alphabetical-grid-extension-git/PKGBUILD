# https://aur.archlinux.org/packages/gnome-shell-extension-alphabetical-grid-extension-git

pkgname=gnome-shell-extension-alphabetical-grid-extension-git
pkgver=43.0.r0.gf3b1595
pkgrel=1
pkgdesc="Alphabetically order GNOME's app grid and folders"
arch=('any')
url="https://github.com/stuarthayhurst/alphabetical-grid-extension"
license=('GPL-3.0-or-later')
depends=('gnome-shell')
makedepends=(
  'git'
  'jq'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/stuarthayhurst/alphabetical-grid-extension.git')
sha256sums=('SKIP')

pkgver() {
  cd alphabetical-grid-extension
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd alphabetical-grid-extension
  make build
}

package() {
  cd alphabetical-grid-extension
  _uuid=$(jq -r .uuid extension/metadata.json)

  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "build/${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv -v "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share/"

  install -Dvm644 "extension/schemas/org.gnome.shell.extensions.AlphabeticalAppGrid.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"

  rm -rv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
  rm -v "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/LICENCE.txt"
}
