# https://aur.archlinux.org/packages/noti

pkgname=noti
pkgver=3.7.0
pkgrel=2
pkgdesc="Monitor a process and trigger a notification"
arch=('x86_64')
url="https://github.com/variadico/noti"
license=('MIT')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('f970a4dd242e6b58edf51320aa237bb20d689bbc8fd0f7d0db5aa1980a2dc269')

prepare(){
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"
  go mod tidy
}

build() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o "$pkgname" "github.com/variadico/$pkgname/cmd/$pkgname"
}

check() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"
  make test

  # Clean module cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 "docs/man/dist/$pkgname.1" -t "$pkgdir/usr/share/man/man1/"
  install -Dm644 "docs/man/dist/$pkgname.yaml.5" -t "$pkgdir/usr/share/man/man5/"
  install -Dm644 "docs/$pkgname.md" -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
