pkgname=hardcode-tray-git
pkgver=4.3.r58.g4752f16
pkgrel=1
pkgdesc="Fixes Hardcoded Tray Icons"
arch=('i686' 'x86_64')
url="https://github.com/bil-elmoussaoui/Hardcode-Tray"
license=('GPL3')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
makedepends=('git' 'meson')
depends=('python-gobject' 'python-cairosvg' 'librsvg' 'gtk3' 'gobject-introspection')
optdepends=('sni-qt-patched-git: patched qt4 sni plugin to enable icon modification'
            'inkscape: convert svg to png'
            'imagemagick: convert svg to png')
optdepends_x86_64=('lib32-sni-qt-patched-git: 32-bit patched qt4 sni plugin to enable icon modification')
source=('git+https://github.com/bil-elmoussaoui/Hardcode-Tray.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/Hardcode-Tray"
	printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/Hardcode-Tray"
	# DEPRECATION: Project targetting '>= 0.40.0' but tried to use feature deprecated
	# since '0.48.0': python3 module
	sed -i "s/meson_version: '>= 0.40.0',/meson_version: '>= 0.49',/g" meson.build
}


build() {
	cd "$srcdir/Hardcode-Tray"
	arch-meson builddir
	ninja -C builddir
}

package() {
	cd "$srcdir/Hardcode-Tray"
	DESTDIR="${pkgdir}" ninja -C builddir install
}
