# https://aur.archlinux.org/packages/hardcode-tray-git
pkgname=hardcode-tray-git
pkgver=4.3.r101.gbedee82
pkgrel=1
pkgdesc="Fixes Hardcoded Tray Icons"
arch=('i686' 'x86_64')
url="https://github.com/bil-elmoussaoui/Hardcode-Tray"
license=('GPL3')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
makedepends=('git' 'meson')
depends=('python-gobject' 'python-cairosvg' 'librsvg' 'gtk3' 'gobject-introspection')
optdepends=('inkscape: convert svg to png'
            'imagemagick: convert svg to png')
source=("${pkgname%-git}::git+https://github.com/bil-elmoussaoui/Hardcode-Tray.git"
        "90-${pkgname%-git}.hook"
        "hook.py")
sha256sums=('SKIP'
            'c16ba6a82d4fe523cb91bf472b44296db82ff301866e3bce18bb4149d0aae5ea'
            '6bbb987b02ff9cd412566b2d0536eb33997015a49d3eab1bb4c551d8203e4989')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" builddir
	ninja -C builddir
}

package() {
	DESTDIR="$pkgdir" ninja -C builddir install

	# Add pacman hook
	# https://github.com/bilelmoussaoui/Hardcode-Tray/issues/454
	install -Dm644 "$srcdir/90-${pkgname%-git}.hook" -t "$pkgdir/etc/pacman.d/hooks"
	install -Dm644 "$srcdir/hook.py" -t "$pkgdir/usr/share/${pkgname%-git}"
}
