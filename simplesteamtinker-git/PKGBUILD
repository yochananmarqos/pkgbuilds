# https://github.com/JordanViknar/SimpleSteamTinker/blob/main/install/archlinux/PKGBUILD
groups=('modified')

pkgname=simplesteamtinker-git
pkgver=r33.eab556b
pkgrel=1
pkgdesc="A work-in-progress simple, fast and modern Adwaita alternative to SteamTinkerLaunch."
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url="https://github.com/JordanViknar/SimpleSteamTinker"
license=('MPL2')
depends=(
  'gtk4'
  'libadwaita'
  'libnotify' # Used for notifications
  'lua'
  'lua-dkjson' # Used for json parsing
  'lua-filesystem' # Used for some filesystem operations
  'lua-lgi-git' # Required for Libadwaita support
  'lua-sec' # Used for secure https interactions
  'lua-socket' # Used for network operations
  'xclip' # Used for copying to clipboard
)
makedepends=('git')
optdepends=(
  'gamemode: Enable GameMode support'
  'gamescope: Enable Gamescope support'
  'mesa: Enable Zink support'
  'mangohud: Enable MangoHud support'
  'protonplus: Recommended to install with SimpleSteamTinker'
  'switcheroo-control: Launch games with your dedicated GPU'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/JordanViknar/SimpleSteamTinker')
md5sums=('SKIP')

pkgver() {
  cd SimpleSteamTinker
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd SimpleSteamTinker
  install -Dm755 sst -t "${pkgdir}/usr/bin/"
  install -Dm644 assets/desktop/system.desktop \
    "${pkgdir}/usr/share/applications/SimpleSteamTinker.desktop"
  install -Dm644 assets/icons/256x256/${pkgname%-git}.png -t \
    "${pkgdir}/usr/share/icons/hicolor/256x256/apps/"
  install -Dm644 assets/icons/scalable/${pkgname%-git}.svg -t \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  install -Dm644 main.lua -t "${pkgdir}/usr/share/SimpleSteamTinker/"
  cp -r modules "${pkgdir}/usr/share/SimpleSteamTinker/modules"
  install -Dm644 ui/main.ui -t "${pkgdir}/usr/share/SimpleSteamTinker/ui/"
}
