# https://github.com/JordanViknar/SimpleSteamTinker/blob/main/install/archlinux/PKGBUILD

pkgname=simplesteamtinker-git
pkgver=r46.f16d9cd
pkgrel=1
pkgdesc="A work-in-progress simple, fast and modern Adwaita alternative to SteamTinkerLaunch."
arch=('any')
url="https://github.com/JordanViknar/SimpleSteamTinker"
license=('MPL-2.0')
depends=(
  'gtk4'
  'libadwaita'
  'libnotify'
  'lua'
  'lua-dkjson'
  'lua-filesystem'
  'lua-lgi-git'
  'lua-sec'
  'lua-socket'
  'xclip'
)
makedepends=('git')
optdepends=(
  'gamemode: Enable GameMode support'
  'gamescope: Enable Gamescope support'
  'mesa: Enable Zink support'
  'mangohud: Enable MangoHud support'
  'protonplus: Recommended to install with SimpleSteamTinker'
  'switcheroo-control: Launch games with your dedicated GPU'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/JordanViknar/SimpleSteamTinker')
sha256sums=('SKIP')

pkgver() {
  cd SimpleSteamTinker
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd SimpleSteamTinker

  desktop-file-edit --set-key=Exec --set-value=sst \
    --set-key=StartupWMClass --set-value=JordanViknar.SimpleSteamTinker \
    assets/desktop/system.desktop
}

package() {
  cd SimpleSteamTinker
  make BUILD_FOLDER="$pkgdir/" system
}
