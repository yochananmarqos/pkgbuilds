# https://aur.archlinux.org/packages/vkbasalt
pkgname=('vkbasalt' 'lib32-vkbasalt')
pkgbase=vkbasalt
pkgver=0.3.2.3
pkgrel=1
pkgdesc="A Vulkan post processing layer to enhance the visual graphics of games"
arch=('x86_64')
url="https://github.com/DadSchoorse/vkBasalt"
license=('zlib')
makedepends=('meson' 'glslang')
backup=(etc/vkBasalt/vkBasalt.conf)
_commit=ca921c88f17c154d27583edc0240c172a66da445
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('8cc65a69a20ac16802a8ff46629efa6540dee22daeef9db4ad0a22b63d2a4368')

prepare() {
	cd vkBasalt-$pkgver
	sed -i 's|/path/to/reshade-shaders/Textures|/usr/share/reshade/textures|g' \
		config/vkBasalt.conf
	sed -i 's|/path/to/reshade-shaders/Shaders|/usr/share/reshade/shaders|g' \
		config/vkBasalt.conf
}

build() {
	arch-meson \
		-Dappend_libdir_vkbasalt=true \
		vkBasalt-$pkgver build64
	meson compile -C build64

	export ASFLAGS=--32
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
	arch-meson \
		-Dappend_libdir_vkbasalt=true \
		--libdir=lib32 \
		-Dwith_json=false \
		vkBasalt-$pkgver build32
	meson compile -C build32
}

package_vkbasalt() {
	depends=('gcc-libs' 'libx11')
	optdepends=('reshade-shaders-git')

	DESTDIR="$pkgdir" meson install -C build64

	cd vkBasalt-$pkgver
	install -Dm644 config/vkBasalt.conf -t "$pkgdir/etc/vkBasalt"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase"
}

package_lib32-vkbasalt() {
	depends=('vkbasalt' 'lib32-gcc-libs' 'lib32-libx11')

	DESTDIR="$pkgdir" meson install -C build32
}
