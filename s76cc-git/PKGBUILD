pkgname=s76cc-git
pkgver=r2.db4ce2c
pkgrel=1
pkgdesc="A GUI wrapper for system76-power for adjusting charge thresholds"
arch=('x86_64')
url="https://github.com/hkdb/s76cc"
license=('MIT')
depends=(
  'gtk3'
  'system76-power'
)
makedepends=(
  'git'
  'go'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/hkdb/s76cc.git'
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            '70e1267d6c145de31009d59768a92f898a0d9461c546fdb2ad5f95aa8f67aefc')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare () {
  cd "${pkgname%-git}"

  # Remove precompiled binary
  rm -f "${pkgname%-git}"
}

build() {
  cd "${pkgname%-git}"
  export GOPATH="$srcdir/gopath"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go mod init "${pkgname%-git}"
  go mod tidy
  go build -v .

  # Clean module cache for makepkg -C
  go clean -modcache
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "${pkgname%-git}" -t "$pkgdir/usr/bin/"
  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "${pkgname%-git}.png" -t "$pkgdir/usr/share/pixmaps/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
