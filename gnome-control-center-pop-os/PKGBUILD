pkgname=gnome-control-center-pop-os
_pkgname=gnome-control-center
pkgver=3.34.2
pkgrel=1
pkgdesc="GNOME Control Center with System76's Firmware Manager built-in"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/gnome-control-center"
license=('GPL2')
groups=('gnome')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop'
         'gnome-online-accounts' 'gnome-settings-daemon' 'gsettings-desktop-schemas' 'gtk3'
         'libgtop' 'nm-connection-editor' 'sound-theme-freedesktop' 'upower' 'libpwquality'
         'gnome-color-manager' 'smbclient' 'libmm-glib' 'libgnomekbd' 'grilo' 'libibus'
         'cheese' 'libgudev' 'bolt' 'udisks2' 'libhandy' 'gsound' 'firmware-manager')
makedepends=('docbook-xsl' 'modemmanager' 'git' 'python' 'meson')
checkdepends=('python-dbusmock' 'python-gobject' 'xorg-server-xvfb')
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: Bluetooth and WebDAV file sharing'
            'gnome-remote-desktop: screen sharing'
            'rygel: media sharing'
            'openssh: remote login')
provides=("$_pkgname")
conflicts=("$_pkgname")
_commit=049df20eca021449eadc61960770e8d9709da16e  # tags/3.34.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-control-center.git#commit=$_commit"
        'git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git'
        'system76_firmware.patch')
sha256sums=('SKIP'
            'SKIP'
            '43b35a5576fd722a8850f0fdd68e7528e684b4231a67edad839c44530e8f2cb4')

pkgver() {
	cd "$_pkgname"
	git describe --tags | sed 's/^GNOME_CONTROL_CENTER_//;s/_/./g;s/-/+/g'
}

prepare() {
	cd "$_pkgname"
	git submodule init
	git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
	git submodule update

	patch -p1 -i "$srcdir/system76_firmware.patch"
}

build() {
	arch-meson "$_pkgname" build -D documentation=true
	ninja -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
	install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
