# https://aur.archlinux.org/packages/waydroid-settings
groups=('modified')

pkgname=waydroid-settings-git
pkgver=r85.bac6cd8
pkgrel=1
pkgdesc="GTK app written in Python to control Waydroid settings"
arch=('any')
url="https://github.com/axel358/Waydroid-Settings"
license=('GPL3')
depends=('libadwaita' 'polkit' 'python-gobject' 'vte4' 'waydroid' 'webkit2gtk-5.0')
makedepends=('blueprint-compiler' 'git' 'meson')
checkdepends=('appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/axel358/Waydroid-Settings.git#branch=gtk4-port")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/Waydroid-Settings"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson Waydroid-Settings build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
