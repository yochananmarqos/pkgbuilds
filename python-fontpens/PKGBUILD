pkgname=python-fontpens
_name=fontPens
pkgver=0.2.4
pkgrel=1
pkgdesc="A collection of classes implementing the pen protocol for manipulating glyphs."
arch=('any')
url="https://github.com/robotools/fontPens"
license=('BSD')
depends=('python-fonttools')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-booleanoperations') # fontParts will be downloaded
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('a6d9a14573b3450f3313d69523f9006028c21fc7aef5d35333b87aab7f2b41fd')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
    cd "$srcdir/$_name-$pkgver"
    python setup.py test
}

package() {
    cd "$_name-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

    install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}

