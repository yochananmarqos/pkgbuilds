# https://aur.archlinux.org/packages/icoextract

pkgname=icoextract
pkgver=0.1.6
pkgrel=1
pkgdesc="Thumbnailer for Windows PE files (.exe/.dll)"
arch=('any')
url="https://github.com/jlu5/icoextract"
license=('MIT')
depends=('python-pefile')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
optdepends=('python-pillow: required for the optional thumbnailer')
conflicts=('exe-thumbnailer')
source=("git+https://github.com/jlu5/icoextract.git#tag=$pkgver?signed")
sha256sums=('4df019b0581aa3b854eec3e3b6d6e71afad62b75dee9956cf6b769f167a7c364')
validpgpkeys=('D5D568B2D34AB32A337944D22EC3F60DE71C0B9D') # James Lu <james@overdrivenetworks.com>

build() {
  cd "${pkgname}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 exe-thumbnailer.thumbnailer -t "$pkgdir/usr/share/thumbnailers/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
