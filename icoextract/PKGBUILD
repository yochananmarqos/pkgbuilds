# https://aur.archlinux.org/packages/icoextract
groups=('modified')

pkgname=icoextract
pkgver=0.1.2
pkgrel=2
pkgdesc="Thumbnailer for Windows PE files (.exe/.dll)"
arch=('any')
url="https://github.com/jlu5/icoextract"
license=('MIT')
depends=('python-pefile')
makedepends=('python-setuptools')
optdepends=('python-pillow: required for the optional thumbnailer')
conflicts=('exe-thumbnailer')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('3818ad889160b33d360da4083da0a32df8d1955fb77a64bfbac49edc15d5ad3b')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    export PYTHONHASHSEED=0
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build

    local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
    rm -rf "$pkgdir$site_packages/tests"

    install -Dm644 exe-thumbnailer.thumbnailer -t "$pkgdir/usr/share/thumbnailers/"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses"
}
