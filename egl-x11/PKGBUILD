pkgname=egl-x11
pkgver=1.0.0
pkgrel=1
pkgdesc="The X11/XCB external platform library"
arch=('x86_64')
url="https://github.com/NVIDIA/egl-x11"
license=('Apache-2.0')
depends=(
  'eglexternalplatform'
  'libx11'
  'libdrm'
  'libxcb'
  'mesa'
)
makedepends=(
  'git'
  'meson'
)
provides=('libnvidia-egl-xcb.so=1' 'libnvidia-egl-xlib.so=1')
source=("git+https://github.com/NVIDIA/egl-x11.git#tag=v$pkgver")
sha256sums=('d122650b9119f0ab9b7aa84595a5ccfaff83cea6a0c83ce6543af3433c23fdab')

build() {
  arch-meson "$pkgname" build \
    -D xcb=true \
    -D xlib=enabled
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
