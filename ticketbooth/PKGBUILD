# https://aur.archlinux.org/packages/ticketbooth

pkgname=ticketbooth
pkgver=1.1.1
pkgrel=1
pkgdesc="Keep track of your favorite shows"
arch=('any')
url="https://github.com/aleiepure/ticketbooth"
license=('CC0-1.0 AND LGPL-3.0-only AND GPL-3.0-or-later')
depends=(
  'libadwaita'
  'python-gobject'
  'python-pillow'
  'python-tmdbsimple'
)
makedepends=(
  'blueprint-compiler'
  'meson'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('4a73febf7306ff63587454bfc05a297426d2390b7afd6bf6f82fc94ed0a04a5d')

prepare() {
  cd "$pkgname-$pkgver"

  # This is not a Flatpak
  sed -i 's/app\/bin/usr\/share\/ticketbooth/g' "install/$pkgname-run-script.in"
}

build() {
  arch-meson "$pkgname-$pkgver" build -Dprerelease=false
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  # Fix runscript location
  cd "$pkgname-$pkgver"
  mv "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/share/$pkgname/$pkgname-bin"
  mv "$pkgdir/usr/$pkgname/$pkgname-run-script" "$pkgdir/usr/bin/$pkgname"
  chmod 0755 "$pkgdir/usr/bin/$pkgname"
  rm -rf "$pkgdir/usr/$pkgname"
}
