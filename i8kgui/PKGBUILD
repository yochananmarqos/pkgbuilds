## WIP

pkgname=i8kgui
pkgver=0.3+160+g2c4e334
pkgrel=1
pkgdesc="A Dell thermal management GUI to control fan speeds and monitor temperatures"
arch=('any')
url="https://github.com/razman786/i8kgui"
license=('GPL3')
depends=('dell-bios-fan-control-git' 'i8kutils' 'libsmbios' 'pyside6' 'python-psutil'
         'python-py-cpuinfo')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('cpupower-gui: change the CPU Governor'
            'python-undervolt')
install="$pkgname.install"
_commit=2c4e334ee9bfbf2456bbfda4a82893e0932ff004  # branch/development
source=("git+https://github.com/razman786/i8kgui.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/$pkgname"

  # Correct euid
  sed -i 's/== 0/== 1000/g' setup.py

  # Correct service path
  sed -i 's|etc/systemd|usr/lib/systemd|g' "$pkgname/${pkgname}_thermal_control"
}

build() {
 cd "$srcdir/$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -r "${pkgdir}${site_packages}/usr/share" "$pkgdir/usr/"
  rm -rf "${pkgdir}${site_packages}/usr"

  install -Dm644 i8kmon_sample_conf/i8kmon.conf \
    "$pkgdir/usr/share/doc/$pkgname/i8kmon_sample.conf"
}
