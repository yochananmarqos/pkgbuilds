pkgname=scrcpy
pkgver=1.12
pkgrel=1
pkgdesc='Display and control your Android device'
arch=('i686' 'x86_64')
url='https://github.com/Genymobile/scrcpy'
license=('Apache')
depends=('adb' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('95aab189448582870d8e7d8a27a2c9b40838bd45ee2737be0f255f7a11647870'
            'b6595262c230e9773fdb817257abcc8c6e6e00f15b1c32b6a850ccfd8176dc10')

prepare() {
	cd "$pkgname-$pkgver"
	# Remove old build directory if present to ensure clean build
	if [ -d build ]
	then
		rm -rf build
	fi
}

build() {
    cd "$pkgname-$pkgver"
    meson \
    	--prefix /usr \
    	--buildtype release \
    	--strip -Db_lto=true \
        -Dprebuilt_server="../$pkgname-server-v$pkgver" \
        build
    ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
}
