# https://aur.archlinux.org/packages/turnon

pkgname=turnon
_app_id="de.swsnr.$pkgname"
pkgver=2.7.4
pkgrel=1
pkgdesc="Turn on devices in your network"
arch=('x86_64' 'aarch64')
url="https://codeberg.org/swsnr/turnon"
license=('EUPL-1.2')
depends=(
  'gtk4'
  'libadwaita'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'git'
  'just'
)
source=("git+https://codeberg.org/swsnr/turnon.git#tag=v$pkgver"
        'schemas.patch')
sha256sums=('ad5515c8a1f766d8c8e28f16f13ffc5c2f387e746a838bf158a9c0b4ad03c985'
            '0a72154c9fc6712b24120c887f59daa20b980832691100e76ee20e659f2668a9')

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # Don't compile schemas after installation
  patch -Np1 -i ../schemas.patch
}

build() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  just compile
  cargo build --frozen --release
}

check() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen

  appstreamcli validate --no-net "${_app_id}.metainfo.xml"
  desktop-file-validate "${_app_id}.desktop"
}

package() {
  cd "$pkgname"
  just DESTPREFIX="$pkgdir/usr" install

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  ln -s "/usr/bin/${_app_id}" "$pkgdir/usr/bin/$pkgname"
}
