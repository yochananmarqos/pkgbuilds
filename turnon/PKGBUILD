# https://aur.archlinux.org/packages/turnon

pkgname=turnon
_app_id="de.swsnr.$pkgname"
pkgver=2.5.1
pkgrel=1
pkgdesc="Turn on devices in your local network"
arch=('x86_64' 'aarch64')
url="https://github.com/swsnr/turnon"
license=('MPL-2.0')
depends=('libadwaita')
makedepends=(
  'blueprint-compiler'
  'cargo'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('09e8ddf23be96b65615c0013f7a848f49c9a190fc8e20dba808878646a925410')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release

  make msgfmt
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  make DESTPREFIX="$pkgdir/usr" install

  ln -s "/usr/bin/${_app_id}" "${pkgdir}/usr/bin/$pkgname"
}
