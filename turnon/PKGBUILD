# https://aur.archlinux.org/packages/turnon

pkgname=turnon
_app_id="de.swsnr.$pkgname"
pkgver=2.6.0
pkgrel=1
pkgdesc="Turn on devices in your local network"
arch=('x86_64' 'aarch64')
url="https://github.com/swsnr/turnon"
license=('MPL-2.0')
depends=('libadwaita')
makedepends=(
  'blueprint-compiler'
  'cargo'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('2c4a7e0edc206b85840af0bbb971379ada1c1765972704099aabafdb96c08afb')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release

  make msgfmt
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen

  appstreamcli validate --no-net "resources/${_app_id}.metainfo.xml"
  desktop-file-validate "${_app_id}.desktop"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTPREFIX="$pkgdir/usr" install

  ln -s "/usr/bin/${_app_id}" "$pkgdir/usr/bin/$pkgname"
}
