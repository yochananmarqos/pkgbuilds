# Maintainer : vmavromatis <bill.mavromatis@protonmail.com>
# Contributors : https://github.com/vmavromatis/absolutely-proprietary/graphs/contributors
_pkgname=absolutely-proprietary
pkgname=${_pkgname}-git
pkgver=r84.f498710
pkgrel=1
pkgdesc="Proprietary package detector for arch-based distros."
arch=('any')
url="https://github.com/yochananmarqos/${_pkgname}"
license=('GPL3')
depends=('python'
         'python-setuptools')
source=("git+https://github.com/yochananmarqos/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "${_pkgname}"
    python setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
    install -Dm644 "$srcdir/${_pkgname}/LICENSE.md" \
                 "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
