# https://aur.archlinux.org/packages/r-linux
pkgname=r-linux
pkgver=5.6.175721
pkgrel=2
pkgdesc="A free file recovery utility for the Ext2/Ext3/Ext4 FS file system"
arch=('x86_64')
url="https://www.r-studio.com/free-linux-recovery"
license=('custom')
depends=('qt5-imageformats' 'qtav' 'freetype2' 'portaudio')
makedepends=('patchelf')
source=("RLinux-${pkgver}_x64.deb::https://www.r-studio.com/downloads/RLinux5_x64.deb"
        'LICENSE')
sha256sums=('62b8f65deca693829b8135aab342ad35277ea4d780abc8b6cc7a5285ed2a824f'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855')

package() {
	bsdtar -xvf data.tar.gz -C "$pkgdir"
	install -d "$pkgdir/opt"
	mv "$pkgdir/usr/local/R-Linux" "$pkgdir/opt"
	rm -rf "$pkgdir/usr/local"

	# Fix insecure RPATH in binaries
    patchelf --shrink-rpath \
    	"$pkgdir"/opt/R-Linux/bin/{ErrorReport,R-Linux,R-Viewer,helpview}

    # Fix insecure RPATH in libs
    patchelf --shrink-rpath \
    	"$pkgdir"/opt/R-Linux/lib/{libQtCLucene*,libQtHelp*}

    # Remove static libs
    rm -rf "$pkgdir"/opt/R-Linux/lib/{iconengines,imageformats,sqldrivers}
    rm "$pkgdir"/opt/R-Linux/lib/{libportaudio.so.2,libQtAV*}

	sed -i 's/usr\/local/opt/g' \
		"$pkgdir/opt/R-Linux/share/rtt-rlinux.desktop" \
		"$pkgdir/opt/R-Linux/bin/rlinux" \
		"$pkgdir/usr/share/menu/rtt-rlinux.menu"
	sed -i "s/rtt-rlinux/$pkgname/g" "$pkgdir/opt/R-Linux/share/rtt-rlinux.desktop"

	install -Dm644 "$pkgdir/opt/R-Linux/share/rtt-rlinux.desktop" \
		"$pkgdir/usr/share/applications/$pkgname.desktop"

	rm "$pkgdir/usr/bin/rlinux"
	ln -s "$pkgdir/opt/R-Linux/bin/rlinux" "$pkgdir/usr/bin/rlinux"

	for icon_size in 16 24 32 48 64 256; do
		icons_dir=usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d "$pkgdir/$icons_dir"
		install -m644 "$pkgdir/opt/R-Linux/share/logo_${icon_size}.png" -t \
			"$pkgdir/$icons_dir"
	done

	install -Dm644 "$srcdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
