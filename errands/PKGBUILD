# https://aur.archlinux.org/packages/errands
groups=('modified')

pkgname=errands
pkgver=45.1.9
pkgrel=1
pkgdesc="Todo application for those who prefer simplicity."
arch=('any')
url="https://github.com/mrvladus/Errands"
license=('MIT')
depends=('gtksourceview5' 'hicolor-icon-theme' 'libadwaita' 'libsecret' 'python-caldav'
         'python-gobject' 'python-icalendar' 'python-pycryptodomex')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('bf2f5cd985fa70d79626e20776d343130df7fe655a31ae4ed2f4411c4b12ab64')

build() {
  arch-meson "Errands-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Fix permissions
  chmod 0755 "$pkgdir/usr/bin/$pkgname"

  cd "Errands-$pkgver"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
