# https://aur.archlinux.org/packages/errands
groups=('modified')

pkgname=errands
pkgver=46.0
pkgrel=1
pkgdesc="Todo application for those who prefer simplicity."
arch=('any')
url="https://github.com/mrvladus/Errands"
license=('MIT')
depends=(
  'gtksourceview5'
  'hicolor-icon-theme'
  'libadwaita'
  'libgoa'
  'libportal'
  'libsecret'
  'python-caldav'
  'python-gobject'
  'python-icalendar'
  'python-pycryptodomex'
)
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('4376853db8827a2d3a5ad6bd7f97f8d5a978e91515193f213bb02acc1aff944a')

build() {
  arch-meson "Errands-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Fix permissions
  chmod 0755 "$pkgdir/usr/bin/$pkgname"

  cd "Errands-$pkgver"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
