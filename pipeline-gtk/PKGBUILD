# https://aur.archlinux.org/packages/pipeline-gtk
groups=('modified')

pkgname=pipeline-gtk
pkgver=1.15.0
pkgrel=1
pkgdesc="Watch YouTube and PeerTube videos in one place"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/schmiddi-on-mobile/pipeline"
license=('GPL-3.0-or-later')
depends=('libadwaita')
makedepends=('blueprint-compiler' 'cargo' 'meson')
conflicts=('tubefeeder')
source=("$url/-/archive/v$pkgver/pipeline-v$pkgver.tar.gz")
sha256sums=('bc8ff1ac2cc4c4993382229aeb247b3b8164a807878f63ec764dcdb216efe616')

prepare() {
  cd "pipeline-v$pkgver"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "pipeline-v$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
