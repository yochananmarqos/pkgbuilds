pkgname=plane-theme-git
pkgver=r162.0d90247
pkgrel=1
pkgdesc='A beautiful GTK theme for GNOME and Xfce'
arch=('any')
url="https://www.pling.com/p/1198768"
license=('GPL')
makedepends=('git' 'npm' 'nvm')
optdepends=('plane-icon-theme: matching icon theme'
            'breeze-snow-cursor-theme: recommended cursor theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/wfpaisa/plane-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	export npm_config_cache="$srcdir/npm_cache"
	npm config delete prefix
	source /usr/share/nvm/init-nvm.sh
	_npm_prefix=$(npm config get prefix)
	_nodeversion='8.16.2'
	nvm install ${_nodeversion} && nvm use ${_nodeversion}

	cd "$srcdir/${pkgname%-git}"
	npm install --cache "$srcdir/npm-cache"
	npm start

	# Restore node config from nvm
	npm config set prefix ${_npm_prefix}
	nvm unalias default
}

package() {
	install -dm755 "$pkgdir/usr/share/themes"

	cd "$srcdir/${pkgname%-git}/build"
	cp -r Plane "$pkgdir/usr/share/themes/Plane"
	cp -r Plane-dark "$pkgdir/usr/share/themes/Plane-dark"
}
