# https://aur.archlinux.org/packages/auto-cpufreq-git
pkgname=auto-cpufreq-git
pkgver=1.3.r0.g6f729ed
pkgrel=1
pkgdesc="Automatic CPU speed & power optimizer"
arch=('any')
url="https://github.com/AdnanHodzic/auto-cpufreq"
license=('LGPL3')
depends=('python-distro' 'python-psutil' 'python-click' 'lm_sensors')
makedepends=('git' 'python-setuptools')
optdepends=('cpufreqctl: CPU Power Manager'
            'gnome-shell-extension-cpufreq: CPU Power Manager for GNOME Shell')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install="${pkgname%-git}.install"
source=('git+https://github.com/AdnanHodzic/auto-cpufreq.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
#	local srcversion="$(grep "version: '" snap/snapcraft.yaml | cut -d "'" -f 2)"
#	printf "%s.r%s.%s" $srcversion "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i 's|usr/local|usr|g' "scripts/${pkgname%-git}.service" source/core.py
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm755 scripts/cpufreqctl.sh -t \
		"$pkgdir/usr/share/${pkgname%-git}/scripts"
	install -Dm644 "scripts/${pkgname%-git}.service" -t \
		"$pkgdir/usr/lib/systemd/system"
}
