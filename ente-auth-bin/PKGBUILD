# https://aur.archlinux.org/packages/ente-auth-bin

pkgname=ente-auth-bin
pkgdesc="2FA app with free end-to-end encrypted backup and sync"
pkgver=4.2.8
pkgrel=1
arch=('x86_64')
url="https://ente.io/auth"
license=('AGPL-3.0-or-later')
depends=(
  'gtk3'
  'libappindicator-gtk3'
  'libsecret'
  'libsodium-1.0.18'
  'sqlite'
)
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("https://github.com/ente-io/ente/releases/download/auth-v$pkgver/${pkgname%-bin}-v$pkgver-${CARCH}.deb")
sha256sums=('28eeba29f28da72f4487f489b1f9b1449e9c4064679945e92544b513fa26988a')

package() {
  bsdtar -xvf data.tar.xz -C "$pkgdir/"

  # add StartupWMClass to desktop file
  desktop-file-edit --set-key=StartupWMClass --set-value=io.ente.auth \
    "$pkgdir/usr/share/applications/enteauth.desktop"

  install -d "$pkgdir/usr/bin"
  ln -s /usr/share/enteauth/enteauth "$pkgdir/usr/bin/"
}
