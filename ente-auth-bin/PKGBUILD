# https://aur.archlinux.org/packages/ente-auth-bin
groups=('modified')

pkgname=ente-auth-bin
pkgdesc="2FA app with free end-to-end encrypted backup and sync"
pkgver=4.1.6
pkgrel=1
arch=('x86_64')
url="https://ente.io/auth"
license=('AGPL-3.0-or-later')
depends=(
  'gtk3'
  'libayatana-appindicator'
  'libsecret'
  'libsodium-1.0.18'
  'sqlite'
)
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("https://github.com/ente-io/ente/releases/download/auth-v$pkgver/${pkgname%-bin}-v$pkgver-${CARCH}.deb")
sha256sums=('86120f56c0e75ee9dfdbeaf5b8a239ee091483faa10119b5d3ee091b5eeebc57')

package() {
  bsdtar -xvf data.tar.xz -C "$pkgdir/"

  # add StartupWMClass to desktop file
  desktop-file-edit --set-key=StartupWMClass --set-value=Io.ente.auth \
    "$pkgdir/usr/share/applications/enteauth.desktop"

  install -d "$pkgdir/usr/bin"
  ln -s /usr/share/enteauth/enteauth "$pkgdir/usr/bin/"
}
