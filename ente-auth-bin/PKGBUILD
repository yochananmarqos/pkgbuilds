# https://aur.archlinux.org/packages/ente-auth-bin
groups=('modified')

pkgname=ente-auth-bin
pkgdesc="2FA app with free end-to-end encrypted backup and sync"
pkgver=4.2.2
pkgrel=1
arch=('x86_64')
url="https://ente.io/auth"
license=('AGPL-3.0-or-later')
depends=(
  'gtk3'
  'libappindicator-gtk3'
  'libsecret'
  'libsodium-1.0.18'
  'sqlite'
)
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("https://github.com/ente-io/ente/releases/download/auth-v$pkgver/${pkgname%-bin}-v$pkgver-${CARCH}.deb")
sha256sums=('8afa9490f617cd2cbf8efeea04c5869fcd69c0500f242299b0ddd08bce1c34c7')

package() {
  bsdtar -xvf data.tar.xz -C "$pkgdir/"

  # add StartupWMClass to desktop file
  desktop-file-edit --set-key=StartupWMClass --set-value=io.ente.auth \
    "$pkgdir/usr/share/applications/enteauth.desktop"

  install -d "$pkgdir/usr/bin"
  ln -s /usr/share/enteauth/enteauth "$pkgdir/usr/bin/"
}
