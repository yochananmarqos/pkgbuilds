# https://aur.archlinux.org/packages/ente-auth-bin

pkgname=ente-auth-bin
pkgdesc="2FA app with free end-to-end encrypted backup and sync"
pkgver=4.3.5
pkgrel=1
arch=('x86_64')
url="https://ente.io/auth"
license=('AGPL-3.0-or-later')
depends=(
  'gtk3'
  'libayatana-appindicator'
  'libsecret'
  'libsodium-1.0.18'
)
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("https://github.com/ente-io/ente/releases/download/auth-v$pkgver/${pkgname%-bin}-v$pkgver-${CARCH}.deb")
sha256sums=('967af850e4123fafc7816b257a0b076a836675cf9361156028b2dca444f85789')

package() {
  bsdtar -xvf data.tar.zst -C "$pkgdir/"

  # add StartupWMClass to desktop file
  desktop-file-edit --set-key=StartupWMClass --set-value=io.ente.auth \
    "$pkgdir/usr/share/applications/enteauth.desktop"

  install -d "$pkgdir/usr/bin"
  ln -s /usr/share/enteauth/enteauth "$pkgdir/usr/bin/"
}
