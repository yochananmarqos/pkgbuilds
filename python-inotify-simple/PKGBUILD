# https://aur.archlinux.org/packages/python-inotify-simple

pkgname=python-inotify-simple
_name=inotify_simple
pkgver=1.3.5
pkgrel=5
pkgdesc="A simple Python wrapper around inotify"
arch=('any')
url="https://github.com/chrisjbillington/inotify_simple"
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('ac595f699a6a6089e328b048e3cacad2851bd461ecbaa39ec22207bcd957e649')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
