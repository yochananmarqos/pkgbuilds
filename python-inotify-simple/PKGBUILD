# https://aur.archlinux.org/packages/python-inotify-simple

pkgname=python-inotify-simple
_name=inotify_simple
pkgver=2.0.1
pkgrel=1
pkgdesc="A simple Python wrapper around inotify"
arch=('any')
url="https://github.com/chrisjbillington/inotify_simple"
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('4357a52d20cb3bffe0fb2fca8fe8279771bc4636cec76410e278c40784a492a5')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
