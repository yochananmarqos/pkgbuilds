# https://aur.archlinux.org/packages/media-downloader
groups=('modified')

pkgname=media-downloader
pkgver=4.5.0
pkgrel=1
pkgdesc="A Qt/C++ front end to yt-dlp, youtube-dl, gallery-dl, lux, you-get, svtplay-dl, aria2c, wget and safari books."
arch=('x86_64')
url="https://github.com/mhogomchungu/media-downloader"
license=('GPL-2.0-only OR GPL-3.0-or-later')
depends=('qt6-base')
makedepends=('cmake')
optdepends=(
  'aria2: aria2c backend'
  'ffmpeg: video post-processing'
  'python-lxml: required for Safari books plugin'
  'python-requests: required for Safari books plugin'
  'svtplay-dl: svtplay-dl plugin'
  'wget: download regular large files'
)
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('0121574f7450211b8c3d1cf50aecd01ce03e0b0c88b61264bf8b5b95cbdb0c6a'
            'SKIP')
validpgpkeys=('6855E493B5B2DF96E319BB6D16E2E1ACC6F51242') # Francis Banyikwa (A key for signing packages) <banyikwafb@gmail.com>

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBUILD_WITH_QT6='ON' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
