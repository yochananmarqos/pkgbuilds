pkgname=mp3tag
pkgver=3.0.0
pkgrel=1
pkgdesc="The universal tag editor."
arch=('i686' 'x86_64')
url="https://www.mp3tag.de"
license=('custom')
depends=('wine' 'samba' 'lib32-gnutls')
makedepends=('p7zip')
options=('!strip')
source=("https://download.mp3tag.de/${pkgname}v${pkgver//./}setup.exe"
        'LICENSE'
        "$pkgname"
        "$pkgname.desktop"
        "$pkgname.png")
sha256sums=('9d2a9c680f9e0c1e0eda17d138ed766f239931c4adfd7b7cf722802a25b1b310'
            '18967b634e69d8ccb08383d42a49ced3c0b11c632649a15c3a6a55e3a27f62e9'
            '662d6b27590e34aabda9ee06efa08ecb10e107d4d30322abcc15f1b86c6e4ef8'
            'bc0c7b8a7a9f9ee92dfe2f1880ef5d91920473713b5d60e4afa361d69a446798'
            'a3e09f7cda34bc31b3b5b1d7cf2010c3b17847c141ef5a074472eb72f760f6bf')

prepare() {
	7z -y -o"$srcdir/$pkgname-$pkgver" x "${pkgname}v${pkgver//./}setup.exe"
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/usr/share/$pkgname"
	cp -a * "$pkgdir/usr/share/$pkgname"
	rm -rf "$pkgdir/usr/share/$pkgname/\$PLUGINSDIR" "$pkgdir/usr/share/$pkgname/\$R0"
	find "$pkgdir/usr/share/$pkgname" -type d -exec chmod 755 "{}" \;
	find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 "{}" \;

	install -Dm644 "$srcdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm755 "$srcdir/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}
