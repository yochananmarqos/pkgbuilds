# http://aur.archlinux.org/packages/mp3tag
# https://gitlab.com/n4r3n/pkgbuilds/-/tree/master/mp3tag
groups=('modified')

pkgname=mp3tag
pkgver=3.24
pkgrel=1
pkgdesc="The universal tag editor."
arch=('x86_64')
url="https://www.mp3tag.de"
license=('MPL-2.0' 'custom')
depends=('wine')
makedepends=('p7zip')
options=('!strip')
source=("https://download.mp3tag.de/${pkgname}v${pkgver/./}-x64-setup.exe"
        "license-$pkgver.html::https://help.mp3tag.de/misc_license.html"
        "$pkgname"
        "$pkgname.desktop"
        "$pkgname.png")
sha256sums=('0390b28ad8831d4ff9676145bbafcb7e77fd9724070f27d48bb714e1709af64e'
            'd50dc1b7913804e12e77afff95636a1e713ae2bb37c8b2c1f04507108ddf9333'
            '22d512a612804e9e165b96291b3dc07b096863e25de2c7c581258c9545dcb28d'
            'bc0c7b8a7a9f9ee92dfe2f1880ef5d91920473713b5d60e4afa361d69a446798'
            'a3e09f7cda34bc31b3b5b1d7cf2010c3b17847c141ef5a074472eb72f760f6bf')

prepare() {
  7z -y -o"$srcdir/$pkgname-$pkgver" x "${pkgname}v${pkgver/./}-x64-setup.exe"
}

package() {
  cd "$pkgname-$pkgver"
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a * "$pkgdir/usr/share/$pkgname"
  rm -rf "$pkgdir/usr/share/$pkgname/"{\$PLUGINSDIR,\$R0}
  find "$pkgdir/usr/share/$pkgname" -type d -exec chmod 755 "{}" \;
  find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 "{}" \;

  install -Dm755 "$srcdir/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"

  install -Dm644 "$srcdir/license-$pkgver.html" \
    "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
