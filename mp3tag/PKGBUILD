# http://aur.archlinux.org/packages/mp3tag
# https://gitlab.com/n4r3n/pkgbuilds/-/tree/master/mp3tag
pkgname=mp3tag
pkgver=3.02
pkgrel=1
pkgdesc="The universal tag editor."
arch=('i686' 'x86_64')
url="https://www.mp3tag.de"
license=('custom')
depends=('wine-mono-bin' 'fuse-overlayfs')
makedepends=('p7zip')
options=('!strip')
source=("https://download.mp3tag.de/${pkgname}v${pkgver/./}setup.exe"
        "license-$pkgver.html::https://help.mp3tag.de/misc_license.html"
        "$pkgname"
        "$pkgname.desktop"
        "$pkgname.png")
sha256sums=('059cb10ad1a7ed163f84d851c60ef612479f58c02b10f9d85bc619ec0a275650'
            '858f8deae1c450165e8614dbeb913bc33345bda37ccaf458f20deac4b6951f15'
            '7bc6616d22adb32c360bedd888d5a0371170f52ee428889aaa85c6218c8659e0'
            'bc0c7b8a7a9f9ee92dfe2f1880ef5d91920473713b5d60e4afa361d69a446798'
            'a3e09f7cda34bc31b3b5b1d7cf2010c3b17847c141ef5a074472eb72f760f6bf')

prepare() {
	7z -y -o"$srcdir/$pkgname-$pkgver" x "${pkgname}v${pkgver/./}setup.exe"
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/usr/share/$pkgname"
	cp -a * "$pkgdir/usr/share/$pkgname"
	rm -rf "$pkgdir/usr/share/$pkgname/"{\$PLUGINSDIR,\$R0}
	find "$pkgdir/usr/share/$pkgname" -type d -exec chmod 755 "{}" \;
	find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 "{}" \;

	install -Dm644 "$srcdir/license-$pkgver.html" \
		"$pkgdir/usr/share/licenses/$pkgname/license.html"
	install -Dm755 "$srcdir/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}
