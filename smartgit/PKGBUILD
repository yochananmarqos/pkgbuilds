# https://aur.archlinux.org/packages/smartgit

pkgname=smartgit
_app_id=com.syntevo.SmartGit
_pkgver=25_1_110
pkgver=${_pkgver//_/.}
pkgrel=1
pkgdesc="Graphical Git client"
arch=('x86_64')
url="https://www.smartgit.dev"
license=(
  'Apache-2.0'
  'BSD-2-Clause'
  'BSD-3-Clause'
  'CC0-1.0'
  'EPL-1.0'
  'LGPL-2.1-or-later'
  'LGPL-3.0-or-later'
  'LicenseRef-custom'
  'MIT'
)
_jre_ver=21  # JRE 17 or higher required
depends=(
  'bash'
  'curl'
  'expat'
  'git'
  'gtk3'
  'hicolor-icon-theme'
  "java-runtime=${_jre_ver}"
  'lsb-release'
  'openssh'
#  'openssl-1.1'   ## AUR, deprecated
  'perl'
  'which'
  'zlib'
)
optdepends=(
  'git-lfs: Large file support'
  'mercurial: hg repositories support'
)
source=("https://download.smartgit.dev/${pkgname}/${pkgname}-${_pkgver}-linux-amd64.tar.gz"
        "${_app_id}.desktop")
noextract=("${pkgname}-${_pkgver}-linux-amd64.tar.gz")
sha256sums=('d344e47de963f0ba0ad74ee9d3d54a8e95fc091c00c5f45762935f6b65e34cae'
            '440cdb52d8f4dbd856abdfcf27eb580e157591c10b71a265407e697eef323b57')

prepare() {
  mkdir -p "${pkgname}-${pkgver}"
  bsdtar xf "${pkgname}-${_pkgver}-linux-amd64.tar.gz" -C "${pkgname}-${pkgver}"
}

package() {
  cd "${pkgname}-${pkgver}/${pkgname}"

  # Install application files
  install -Dm755 "bin/${pkgname}.sh" -t "${pkgdir}/opt/${pkgname}/bin/"
  install -m644 "bin/${pkgname}.vmoptions" -t "${pkgdir}/opt/${pkgname}/bin/"
  cp -r dictionaries git lib "${pkgdir}/opt/${pkgname}/"

  # Install licenses
  install -Dm644 license.html -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 licenses/* -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Install icon files
  for i in 32 48 64 128 256; do
    install -Dm644 "bin/${pkgname}-${i}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
  done
  install -Dm644 "bin/${pkgname}.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  # Add symlink to /usr/bin
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install desktop file
  install -Dm644 "${srcdir}/${_app_id}.desktop" -t "${pkgdir}/usr/share/applications/"

  # install metainfo file
  install -Dm644 "${_app_id}.metainfo.xml" -t "${pkgdir}/usr/share/metainfo/"

  # Symlink system JVM
  install -d "${pkgdir}"/{usr/lib,opt/"${pkgname}"/jre}
  ln -s "/usr/lib/jvm/java-${_jre_ver}-openjdk/"* "${pkgdir}/opt/${pkgname}/jre/"
}
