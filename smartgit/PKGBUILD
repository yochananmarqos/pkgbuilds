# https://aur.archlinux.org/packages/smartgit
groups=('modified')

pkgname=smartgit
pkgver=22.1.5
pkgrel=2
pkgdesc="Graphical Git client"
arch=('x86_64')
url="https://www.syntevo.com/smartgit"
license=('custom')
_jre_ver=20  # JRE 17 or higher required
depends=('bash' 'curl' 'expat' 'git' 'gtk3' 'hicolor-icon-theme' "java-runtime=${_jre_ver}"
         'lsb-release' 'openssh' 'openssl-1.1' 'perl' 'which' 'zlib')
optdepends=('git-lfs: Large file support'
            'mercurial: hg repositories support')
source=("https://www.syntevo.com/downloads/${pkgname}/${pkgname}-linux-${pkgver//\./_}.tar.gz"
        "${pkgname}.desktop")
sha256sums=('b37d6c6040b583b78b3214fd5249a36c19c778f63dc2740f9a01905e0557e23e'
            '440cdb52d8f4dbd856abdfcf27eb580e157591c10b71a265407e697eef323b57')

package() {
  cd "${srcdir}/${pkgname}"

  # Install application files
  install -Dm755 bin/*.sh -t "${pkgdir}/opt/${pkgname}/bin/"
  install -m644 "bin/${pkgname}.vmoptions" -t "${pkgdir}/opt/${pkgname}/bin/"
  cp -r dictionaries git lib opt "${pkgdir}/opt/${pkgname}/"

  # Install licenses
  install -Dm644 license.html -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 licenses/*-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Install docs
  install -Dm644 changelog.txt checksums known-issues.txt readme-linux.txt "${pkgname}.url" -t \
    "$pkgdir/usr/share/doc/${pkgname}/"

  # Install icon files
  for _size in 32 48 64 128 256; do
    install -Dm644 "bin/${pkgname}-${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -Dm644 "bin/${pkgname}.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"

  # Add symlink to /usr/bin
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"

  # Symlink system JVM
  install -d "${pkgdir}"/{usr/lib,opt/"${pkgname}"/jre}
  ln -s "/usr/lib/jvm/java-${_jre_ver}-openjdk/"* "${pkgdir}/opt/${pkgname}/jre/"
}
