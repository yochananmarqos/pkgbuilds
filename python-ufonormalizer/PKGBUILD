# https://aur.archlinux.org/packages/python-ufonormalizer
pkgname=python-ufonormalizer
_name=${pkgname#python-}
pkgver=0.4.2
pkgrel=1
pkgdesc="A tool that will normalize XML and other data inside of a UFO."
arch=('any')
url="https://github.com/unified-font-object/ufoNormalizer"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
#checkdepends=()
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('f2739eaa8df3cd4e60894b22679a882505252b93f12657a0993894108122c6e6')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	python setup.py test
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	# somehow the files end up with 640 permissions

	local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

	find "$pkgdir/$site_packages/$_name-$pkgver-py${python_version}.egg-info" \
		-type f -exec chmod 644 {} \;

	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
