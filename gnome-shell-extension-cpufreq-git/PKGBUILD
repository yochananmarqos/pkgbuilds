# https://aur.archlinux.org/packages/gnome-shell-extension-cpufreq-git
groups=('modified')

pkgname=gnome-shell-extension-cpufreq-git
pkgver=50.0.r19.gfaf7fa3
pkgrel=1
pkgdesc="System Monitor and Power Manager"
arch=('any')
url="https://konkor.github.io/cpufreq"
license=('GPL3')
depends=('gnome-shell<=1:44.6')
makedepends=('git' 'gnome-common')
optdepends=('ttf-roboto' 'ttf-lato')
provides=("${pkgname%-git}" 'cpufreqctl')
conflicts=("${pkgname%-git}" 'cpufreqctl')
install='cpufreq.install'
source=('git+https://github.com/konkor/cpufreq.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/cpufreq"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/cpufreq"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/cpufreq"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/cpufreq@konkor/schemas/"
}
