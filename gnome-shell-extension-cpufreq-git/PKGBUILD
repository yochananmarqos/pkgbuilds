# https://aur.archlinux.org/packages/gnome-shell-extension-cpufreq-git
pkgname=gnome-shell-extension-cpufreq-git
_pkgname=cpufreq
pkgver=45.0.r0.g2a0f5b1
pkgrel=1
pkgdesc="System Monitor and Power Manager"
arch=('any')
url="https://konkor.github.io/cpufreq"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git' 'gnome-common')
optdepends=('ttf-roboto' 'ttf-lato')
provides=("${pkgname%-git}" 'cpufreqctl')
conflicts=("${pkgname%-git}" 'cpufreqctl')
install="$_pkgname.install"
source=('git+https://github.com/konkor/cpufreq.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$_pkgname"
	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir" install
}
