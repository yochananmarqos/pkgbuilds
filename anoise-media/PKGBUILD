# https://aur.archlinux.org/packages/anoise-media
groups=('modified')

pkgname=anoise-media
pkgver=0.0.17
pkgrel=3
pkgdesc="Ambient Noise Library. Sounds and icons for Anoise Player"
arch=('any')
url="https://costales.github.io/projects/anoise"
license=('GPL3')
makedepends=('python-build' 'python-distutils-extra' 'python-installer'
             'python-setuptools' 'python-wheel')
source=("https://launchpad.net/~costales/+archive/ubuntu/anoise/+sourcefiles/$pkgname/$pkgver/${pkgname}_${pkgver}.tar.gz")
sha256sums=('7ad6b20438687107d850fd4bad760f103e7f2eb5ec7b1b45296ad29433cc0f36')

build() {
  cd "$srcdir/media"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/media"
  python -m installer --destdir="$pkgdir" dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -r "${pkgdir}${site_packages}/usr/share" "$pkgdir/usr/"
  rm -rf "${pkgdir}${site_packages}/usr"
}
