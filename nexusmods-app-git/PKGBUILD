## WIP

pkgname=nexusmods-app-git
pkgver=0.4.9.3.r488.g6211646
pkgrel=1
pkgdesc="A mod installer, creator and manager for all your popular games"
arch=('x86_64')
url="https://nexus-mods.github.io/NexusMods.App"
license=('GPL-3-or-later')
depends=('dotnet-runtime>=8' 'fontconfig' 'xdg-utils')
makedepends=('dotnet-sdk>=8' 'git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=('git+https://github.com/Nexus-Mods/NexusMods.App.git'
        'git+https://github.com/Nexus-Mods/NexusMods.MkDocsMaterial.Themes.Next.git'
        'git+https://github.com/Pathoschild/SMAPI.git'
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '85e4c1f05348f7108e11264fc75d96df58828e44f31d1d0376e649541f37c240')

pkgver() {
  cd NexusMods.App
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd NexusMods.App
  git submodule init
  git config submodule.docs/Nexus.url "$srcdir/NexusMods.MkDocsMaterial.Themes.Next"
  git config submodule.extern/SMAPI.url "$srcdir/SMAPI"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd NexusMods.App
  # https://nexus-mods.github.io/NexusMods.App/Contributing/#for-package-maintainers
#  export NEXUSMODS_APP_USE_SYSTEM_EXTRACTOR=1  # requires 7zz to be in $PATH
  export INSTALLATION_METHOD_PACKAGE_MANAGER
  dotnet clean
  dotnet restore
  dotnet build
  dotnet publish -p:Version=${pkgver} \
    --self-contained true \
    -c Release \
    -p:TieredCompilation=true \
    -p:PublishReadyToRun=true \
    -p:PublishSingleFile=true
}

package() {
  cd NexusMods.App
  install -d "$pkgdir/opt/NexusMods.App"
  cp -av bin/linux-x64/* "$pkgdir/opt/NexusMods.App/"

  # Expects installation directory to be writable
  chmod 0777 "$pkgdir/opt/NexusMods.App"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/NexusMods.App/NexusMods.App" "$pkgdir/usr/bin/"

  install -Dm644 Nexus-Icon.png "$pkgdir/usr/share/pixmaps/NexusMods.App.png"
  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications/"

  rm -rf "$pkgdir"/opt/NexusMods.App/runtimes/{osx,win}-x64
}
