# https://aur.archlinux.org/packages/gnome-shell-extension-cast-to-tv
pkgname=gnome-shell-extension-cast-to-tv
pkgdesc="Cast files to Chromecast, web browser or media player app over local network."
pkgver=15
pkgrel=1
arch=('x86_64')
url="https://rafostar.github.io/gnome-shell-extension-cast-to-tv"
license=('GPL2')
depends=('gnome-shell' 'ffmpeg' 'nodejs')
makedepends=('npm')
optdepends=('python-nautilus: Nautilus integration')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Rafostar/$pkgname/archive/v$pkgver.tar.gz"
        'install_node_modules.patch')
sha256sums=('d6cfb02291d8b6edc4b65b59006606bf052aceb0c43843ec654f528a2d4fdaf3'
            'b24fa4e6b04cae500ec267345ac48f9c92452e0d9ad798ac28132984387b98cc')

prepare() {
	cd "$pkgname-$pkgver"
	patch --forward --strip=1 --input "$srcdir/install_node_modules.patch"
}

build() {
	cd "$pkgname-$pkgver"
	npm install --cache "$srcdir/npm-cache"
	make
}

package() {
	cd "$pkgname-$pkgver"
	make PKGDIR="$pkgdir/" install
	make CUSTOMPATH=PKGDIR/usr/share PKGDIR="$pkgdir/" install

	rm -rf "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"

	install -Dm644 nautilus/nautilus-cast-to-tv.py -t \
		"$pkgdir/usr/share/nautilus-python/extensions"
}
