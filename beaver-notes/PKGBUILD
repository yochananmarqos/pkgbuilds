# https://aur.archlinux.org/packages/beaver-notes
groups=('modified')

pkgname=beaver-notes
pkgver=2.8.0
pkgrel=1
_electronversion=28
pkgdesc="A privacy-focused, cross-platform note-taking application."
arch=('x86_64')
url="https://www.beavernotes.com"
license=('MIT')
depends=("electron${_electronversion}")
makedepends=('libxcrypt-compat' 'yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Daniele-rolli/Beaver-Notes/archive/refs/tags/$pkgver.tar.gz"
        "$pkgname.desktop"
        "$pkgname.sh")
sha256sums=('7a454d9509c21897602112ef09ec4687e1947f1f52a1f4d6a20628453eace447'
            'ed1109a5e1c72456970ba21539a6d02cf7187eec7cc60f4599cea519a29cbfe3'
            'd21c665d70ad43c88f2fdd7581042b8a862d29c8e3398f3ebafad03b8f65c1e3')

prepare() {
  cd "Beaver-Notes-$pkgver"
  sed -i "s|@ELECTRONVERSION@|${_electronversion}|" "$srcdir/$pkgname.sh"
}

build() {
  cd "Beaver-Notes-$pkgver"
  export YARN_CACHE_FOLDER="$srcdir/yarn-cache"
  electronDist="/usr/lib/electron${_electronversion}"
  electronVer="$(sed s/^v// /usr/lib/electron${_electronversion}/version)"
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  yarn install
  yarn build
  yarn electron-builder build --config electron-builder.config.cjs --linux deb \
    $dist -c.electronDist=$electronDist -c.electronVersion=$electronVer
}

package() {
  cd "Beaver-Notes-$pkgver"
  install -Dm644 dist/linux-unpacked/resources/app.asar -t \
    "$pkgdir/usr/lib/$pkgname/"

  for i in 32 64 128 256 512 1024; do
    install -Dm644 "dist/.icon-set/icon-linux_${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
}
