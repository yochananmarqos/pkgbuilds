# https://aur.archlinux.org/packages/tangram
pkgname=tangram
pkgver=1.3.1
pkgrel=1
pkgdesc="Run web apps on your desktop."
arch=('any')
url="https://github.com/sonnyp/Tangram"
license=('GPL3')
depends=('gjs' 'libsoup' 'webkit2gtk')
conflicts=('gigagram')
makedepends=('git' 'npm' 'meson')
source=("$pkgname::git+https://github.com/sonnyp/Tangram.git#tag=v$pkgver"
        'git+https://github.com/sonnyp/troll.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
	cd "$srcdir/$pkgname"
	git submodule init
	git config submodule.src/troll.url $srcdir/troll
	git submodule update
}

build() {
	cd "$srcdir/$pkgname"
	npm install --cache "$srcdir/npm-cache"
	./node_modules/.bin/rollup -c

	arch-meson . build
	meson compile -C build
}

package(){
	cd "$srcdir/$pkgname"
	DESTDIR="$pkgdir" meson install -C build

	ln -s /usr/bin/re.sonny.Tangram "$pkgdir/usr/bin/$pkgname"
}
