# https://aur.archlinux.org/packages/typora
pkgname=typora
pkgver=0.9.95
pkgrel=1
pkgdesc="A minimal Markdown reading & writing app"
arch=('x86_64')
url="https://typora.io"
license=('custom')
depends=('electron')
optdepends=('noto-fonts-emoji: Or some other emoji font to see emojis'
            'pandoc: Import/export for extra file formats')
source=("$pkgname-$pkgver.tar.gz::https://typora.io/linux/Typora-linux-x64.tar.gz"
        'copyright'
        "$pkgname.desktop"
        "$pkgname.js"
        "$pkgname.png")
sha256sums=('52a2865e856f1c6073dfa24da301e283c49642e809e4ad8d26ba8e3b474c7364'
            'b32d0347b505604c4d297a9b8e2aa60c6d73b8cd88c25bcd0ab6ff1724d80979'
            '994058363cc5eda0121902360bc67ded59293faa12416cbe5431c2a0c74577a1'
            '047b1aa632e572e1802d823459e0a7d1a4fecc2f21f28f82d78b469d739278fe'
            'd8e8b6ed786f2880e4c50aaabdc722e340ce568c609c0ea820b571bb72ffd719')

package() {
	install -d "$pkgdir/usr/lib/$pkgname"
	cp -r bin/Typora-linux-x64/resources "$pkgdir/usr/lib/$pkgname"

	install -Dm644 copyright -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm755 "$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm755 "$pkgname.js" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 "$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}
