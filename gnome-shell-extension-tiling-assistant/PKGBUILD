# https://aur.archlinux.org/packages/gnome-shell-extension-tiling-assistant
groups=('modified')

pkgname=gnome-shell-extension-tiling-assistant
_uuid=tiling-assistant@leleat-on-github
pkgver=47
pkgrel=1
pkgdesc="A GNOME Shell extension to expand GNOME's native 2 column design."
arch=('any')
url="https://github.com/Leleat/Tiling-Assistant"
license=('GPL2')
depends=('gnome-shell')
makedepends=('git')
install='tiling-assistant.install'
_commit=8a176de3ea4b56b4a6fa491fa0f3f173c7a164cb  # tags/v47^0
source=("git+https://github.com/Leleat/Tiling-Assistant.git#commit=${_commit}")
sha256sums=('3679cb0765072f8a246d0946caf5e283f0f8ea2e3121a944c30ec4611ae73fda')

pkgver() {
  cd Tiling-Assistant
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd Tiling-Assistant
  gnome-extensions pack "${_uuid}" \
    --podir=../translations/ \
    --extra-source=media/ \
    --extra-source=src/ \
    --force
}

package() {
  cd Tiling-Assistant
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share"

  install -Dm644 "${_uuid}/schemas/org.gnome.shell.extensions.tiling-assistant.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas"
}
