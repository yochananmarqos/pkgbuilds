## WIP

pkgname=xplorer
pkgver=0.3.1+137+g2eb6747
pkgrel=1
pkgdesc="A customizable, modern file manager"
arch=('x86_64')
url="https://xplorer.space"
license=('Apache-2.0')
depends=('gtk3' 'webkit2gtk')
makedepends=('cargo' 'git' 'yarn')
_commit=2eb6747a51b796e63911ec4e63374fc463ae59d0  # branch/master
source=("git+https://github.com/kimlimjustin/xplorer.git#commit=${_commit}"
        "$pkgname.desktop")
sha256sums=('de1ce33105c20b5f68526d9d947bc1c64bddd399ba5bbb73f8e1b6906901fa40'
            'cd0d2fb362668661b0ed2dc7e0637028fb5a9e27fd30f4771467bccab1468249')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$pkgname"

  # Remove husky
  sed -i '/husky/d' package.json

  export YARN_CACHE_FOLDER="$srcdir/yarn-cache"
  yarn install

  pushd src-tauri

  # Don't bundle AppImage
  sed -i 's/"targets": "all",/"targets": "deb",/g' tauri.conf.json

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
  popd
}

build() {
  cd "$pkgname"
  CFLAGS+=" -ffat-lto-objects"
  export YARN_CACHE_FOLDER="$srcdir/yarn-cache"
  export RUSTUP_TOOLCHAIN=stable
  yarn compile
  yarn build
}

check() {
  cd "$pkgname"
  export YARN_CACHE_FOLDER="$srcdir/yarn-cache"
  yarn test

  export RUSTUP_TOOLCHAIN=stable
  pushd src-tauri
  cargo test
  popd
}

package() {
  cd "$pkgname"
  install -Dm755 "src-tauri/target/release/$pkgname" -t "$pkgdir/usr/bin/"

  for i in 32x32 128x128 128x128@2x; do
    install -Dm644 src-tauri/icons/${i}.png \
      "$pkgdir/usr/share/icons/hicolor/${i}/apps/$pkgname.png"
  done

  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
}
