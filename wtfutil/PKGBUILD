# https://aur.archlinux.org/packages/wtfutil
pkgname=wtfutil
pkgver=0.34.0
pkgrel=1
pkgdesc="Personal information dashboard for your terminal"
arch=('x86_64' 'aarch64' 'armv6h')
url="https://wtfutil.com"
license=('MPL2')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wtfutil/wtf/archive/v$pkgver.tar.gz")
sha256sums=('5fccf617851940362b5ab2d10051917ce5ab8ba3de94897f2cea47176e9e2e29')

prepare() {
	export GOPATH="$srcdir/gopath"
	go clean -modcache

	# Set the Go proxy
	export GOPROXY="https://proxy.golang.org,direct"

	# Disable the Go checksum database
	export GOSUMDB=off

	# Enable Go modules
	export GO111MODULE=on
}

build() {
	cd "wtf-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build \
		-ldflags "-X 'main.version=$pkgver' -X 'main.date=$(date)'" \
		-v -o $pkgname .

	# Clean now to ensure makepkg --clean works
	go clean -modcache
}

package(){
	cd "wtf-$pkgver"
	install -Dm755 "$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$pkgname"
	cp -r _sample_configs "$pkgdir/usr/share/doc/$pkgname/sample_configs"
}
