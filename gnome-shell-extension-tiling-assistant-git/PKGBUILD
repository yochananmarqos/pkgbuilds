# https://aur.archlinux.org/packages/gnome-shell-extension-tiling-assistant-git
groups=('modifed')

pkgname=gnome-shell-extension-tiling-assistant-git
_uuid=tiling-assistant@leleat-on-github
pkgver=51.r0.g9373d10
pkgrel=1
pkgdesc="A GNOME Shell extension which adds a Windows-like snap assist to the GNOME desktop"
arch=('any')
url="https://github.com/Leleat/Tiling-Assistant"
license=('GPL2')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install='tiling-assistant.install'
source=('git+https://github.com/Leleat/Tiling-Assistant.git'
        'shell-version.patch')
sha256sums=('SKIP'
            '65a36204f0ee6993873f2126704c6f40b5baaa430728c26cdc2388eaf6b316fb')

pkgver() {
  cd Tiling-Assistant
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd Tiling-Assistant

  # Keep support for GNOME 47
  patch -Np1 -i ../shell-version.patch
}

build() {
  cd Tiling-Assistant
  gnome-extensions pack "${_uuid}" \
    --podir=../translations/ \
    --extra-source=media/ \
    --extra-source=src/ \
    --force
}

package() {
  cd Tiling-Assistant
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share"

  install -Dm644 "${_uuid}/schemas/org.gnome.shell.extensions.tiling-assistant.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas"
}
