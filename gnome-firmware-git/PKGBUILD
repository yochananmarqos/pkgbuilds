# https://aur.archlinux.org/packages/gnome-firmware-git
groups=('modified')

pkgname=gnome-firmware-git
pkgver=3.36.0.r21.gf528107
pkgrel=1
pkgdesc="Manage firmware on devices supported by fwupd"
arch=('x86_64')
url="https://gitlab.gnome.org/hughsie/gnome-firmware-updater"
license=('GPL2')
depends=('gtk3' 'fwupd' 'libxmlb' 'libsoup' 'systemd')
makedepends=('git' 'meson' 'help2man')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://gitlab.gnome.org/hughsie/gnome-firmware-updater.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson ${pkgname%-git} build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}

