# https://archlinux.org/packages/community/any/cameractrls/
groups=('modified')

pkgname=cameractrls
_app_id="hu.irl.$pkgname"
pkgver=0.5.2
pkgrel=1
pkgdesc="Camera controls for Linux"
arch=('any')
url="https://github.com/soyersoyer/cameractrls"
license=('MIT')
depends=('gtk4' 'libjpeg-turbo' 'python' 'python-gobject' 'sdl2' 'systemd')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('323677d5f40091fcf205d8f5eda6f8f30a4e54d40ff994d294b60d2579fca5ce')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's/Exec=cameractrlsgtk.py/Exec=cameractrlsgtk/g' "pkg/${_app_id}.desktop"
}

package() {
  cd "$pkgname-$pkgver"
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -Dm755 cameractrls.py cameraview.py cameractrlsgtk4.py -t \
    "${pkgdir}${site_packages}/CameraCtrls/"
  install -Dm644 pkg/${_app_id}.svg -t "${pkgdir}${site_packages}/CameraCtrls/images/"
  install -Dm644 "pkg/${_app_id}.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"
  install -Dm644 "pkg/${_app_id}.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  install -d "$pkgdir"/usr/{bin,share/icons/hicolor/scalable/apps}
  ln -s "${site_packages}/CameraCtrls/$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  ln -s "${site_packages}/CameraCtrls/${pkgname}gtk4.py" "$pkgdir/usr/bin/${pkgname}gtk"
  ln -s "${site_packages}/CameraCtrls/images/${_app_id}.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
}
