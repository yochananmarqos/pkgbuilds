# http://aur.archlinux.org/packages/mangohud
pkgname=('mangohud' 'lib32-mangohud' 'mangohud-common')
pkgbase=mangohud
pkgver=0.4.1
pkgrel=2
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('git' 'meson' 'python-mako' 'glslang' 'libglvnd' 'lib32-libglvnd'
             'vulkan-headers' 'libxnvctrl')
source=("$pkgbase::git+https://github.com/flightlessmango/MangoHud.git#tag=v$pkgver"
        'git+https://github.com/flightlessmango/imgui.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
	cd "$srcdir/$pkgbase"
	git submodule init modules/ImGui/src
	git config submodule.imgui.url "$srcdir/imgui"
	git submodule update
}

build() {
	arch-meson \
		-Duse_system_vulkan=enabled \
		-Dinclude_doc=false \
		"$pkgbase" build64
	meson compile -C build64

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	export LLVM_CONFIG="/usr/bin/llvm-config32"
	arch-meson \
		-Duse_system_vulkan=enabled \
		-Dinclude_doc=false \
		"$pkgbase" build32 --libdir lib32
	meson compile -C build32
}

package_mangohud() {
	depends=('gcc-libs' 'libx11' 'vulkan-icd-loader' 'mangohud-common')
	optdepends=('libxnvctrl: Support for older NVIDIA GPUs')

	DESTDIR="$pkgdir" meson install -C build64
}

package_lib32-mangohud() {
	depends=('lib32-gcc-libs' 'lib32-libx11' 'lib32-vulkan-icd-loader'
	         'mangohud-common')
	optdepends=('lib32-libxnvctrl: Support for older NVIDIA GPUs')

	DESTDIR="$pkgdir" meson install -C build32
}

package_mangohud-common() {
	cd "$srcdir/$pkgbase"
	install -Dm664 bin/MangoHud.conf \
		"$pkgdir/usr/share/doc/$pkgbase/MangoHud.conf.example"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase"
}
