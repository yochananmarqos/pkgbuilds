pkgname=('mangohud' 'lib32-mangohud' 'mangohud-common')
pkgbase=mangohud
pkgver=0.3.1
pkgrel=1
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('git' 'meson' 'python-mako' 'glslang' 'libglvnd' 'lib32-libglvnd'
             'vulkan-headers' 'vulkan-icd-loader' 'lib32-vulkan-icd-loader')
source=("$pkgbase::git+https://github.com/flightlessmango/MangoHud.git#tag=v$pkgver"
        'git+https://github.com/flightlessmango/imgui.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
	cd "$srcdir/$pkgbase"
	git submodule init modules/ImGui/src
	git config submodule.imgui.url "$srcdir/imgui"
	git submodule update
}

build() {
	arch-meson \
		-Duse_system_vulkan=enabled \
		-Dinclude_doc=false \
		"$pkgbase" build64
	ninja -C build64

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	export LLVM_CONFIG="/usr/bin/llvm-config32"
	arch-meson \
		-Duse_system_vulkan=enabled \
		-Dinclude_doc=false \
		"$pkgbase" build32 --libdir lib32
	ninja -C build32
}

package_mangohud() {
	depends=('gcc-libs' 'libx11' 'mangohud-common')

	DESTDIR="$pkgdir" ninja -C build64 install
}

package_lib32-mangohud() {
	depends=('lib32-gcc-libs' 'lib32-libx11' 'mangohud-common')

	DESTDIR="$pkgdir" ninja -C build32 install
}

package_mangohud-common() {
	cd "$srcdir/$pkgbase"
	install -Dm664 bin/MangoHud.conf "$pkgdir/usr/share/doc/$pkgbase/MangoHud.conf.example"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
