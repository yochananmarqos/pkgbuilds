# https://aur.archlinux.org/packages/libloot

pkgname=libloot
pkgver=0.27.0
pkgrel=1
pkgdesc="A C++ library for accessing LOOT's metadata and sorting functionality."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-only')
depends=(
  'fmt'
  'icu'
  'onetbb'
  'spdlog'
)
makedepends=(
  'boost'
  'cargo'
  'cbindgen'
  'cmake'
#  'doxygen'  # docs
  'git'
#  'python-breathe'  # docs
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
)
provides=('libloot.so=0')
# Use the Git repository so that libloot's commit hash gets embedded in the build
source=("git+https://github.com/loot/libloot.git#tag=$pkgver")
sha256sums=('9844946eca8c09fdded8c1f30d9fa4eb86faed03725abf7723b654c17f20c806')

build() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLIBLOOT_BUILD_TESTS='OFF' \
    -DLIBLOOT_INSTALL_DOCS='OFF' \
    -DCMAKE_SKIP_RPATH=TRUE \
    -Wno-dev
  cmake --build build

  # docs
#  sphinx-build -b html docs build/docs/html
}

#check() {
#  cd "$pkgname-$pkgver"
#  ctest --test-dir build --output-on-failure
#}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir" cmake --install build
}
