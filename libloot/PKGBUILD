# https://aur.archlinux.org/packages/libloot

pkgname=libloot
pkgver=0.25.3
pkgrel=1
pkgdesc="A C++ library for accessing LOOT's metadata and sorting functionality."
arch=('x86_64')
url="https://github.com/loot/libloot"
license=('GPL-3.0-only')
depends=(
  'fmt'
  'icu'
  'onetbb'
)
makedepends=(
  'boost'
  'cargo'
  'cbindgen'
  'cmake'
#  'doxygen'  # docs
#  'python-breathe'  # docs
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
)
provides=('libloot.so=0')
source=("$pkgname-$pkgver.tar.gz::https://github.com/loot/libloot/archive/$pkgver.tar.gz")
sha256sums=('97426381126e8551cc2e850596a093cc2919e0109e9757315286341214de3020')

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLIBLOOT_BUILD_TESTS='OFF' \
    -DLIBLOOT_INSTALL_DOCS='OFF' \
    -Wno-dev
  cmake --build build

  # docs
#  sphinx-build -b html docs build/docs/html
}

#check() {
#  cd "$pkgname-$pkgver"
#  ctest --test-dir build --output-on-failure
#}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir" cmake --install build
}
