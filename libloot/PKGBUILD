# https://aur.archlinux.org/packages/libloot

pkgname=libloot
pkgver=0.28.4
pkgrel=1
pkgdesc="A C++ library for accessing LOOT's metadata and sorting functionality."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-only')
depends=('gcc-libs')
makedepends=(
  'cargo'
  'cmake'
#  'doxygen'  # docs
  'git'
#  'python-breathe'  # docs
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
)
provides=('libloot.so=0')
source=("git+https://github.com/loot/libloot.git#tag=$pkgver")
sha256sums=('40272c324151ac6eb87ab9541d61787cba7de0f58309df88f8282bd7e048d825')

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cmake -B build -S cpp \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLIBLOOT_BUILD_TESTS='OFF' \
    -DLIBLOOT_INSTALL_DOCS='OFF' \
    -Wno-dev
  cmake --build build

  # docs
#  sphinx-build -b html docs build/docs/html
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir" cmake --install build
}
