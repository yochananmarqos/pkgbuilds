# https://aur.archlinux.org/packages/gdm-settings-git
groups=('modified')

pkgname=gdm-settings-git
pkgver=4.3.r36.g523fbf4
pkgrel=1
pkgdesc="A settings app for Gnome's Login Manager, GDM"
arch=('any')
url="https://gdm-settings.github.io"
license=('AGPL-3.0-or-later')
depends=('gdm' 'glib2-devel' 'libadwaita' 'polkit' 'python-gobject')
makedepends=('blueprint-compiler' 'git' 'meson')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/gdm-settings/gdm-settings.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="$pkgdir"
}
