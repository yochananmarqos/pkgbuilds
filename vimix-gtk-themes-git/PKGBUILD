# https://aur.archlinux.org/packages/vimix-gtk-themes-git
pkgname=vimix-gtk-themes-git
pkgver=2020.02.24.r24.g9970d40
pkgrel=1
pkgdesc="A flat Material Design theme for GTK 3, GTK 2, GNOME Shell, etc."
arch=('any')
url="https://vinceliuice.github.io/theme-vimix.html"
license=('GPL3')
depends=('gtk3' 'gtk-engine-murrine' 'gtk-engines')
makedepends=('git')
optdepends=('kvantum-theme-vimix: Matching Kvantum theme'
            'vimix-icon-theme: Matching icon theme'
            'vimix-cursors: Matching cursor theme'
            'tela-icon-theme: Recommended icon theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=('git+https://github.com/vinceliuice/vimix-gtk-themes.git'
        '183.patch')
sha256sums=('SKIP'
            '52d0619549de68043bbd1d9baeaf4bb3f6d63b14746a21ef89e910cf1ee66260')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git apply -3 "$srcdir/183.patch"
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/usr/share/themes"
	./install.sh -a -d "$pkgdir/usr/share/themes"
}
