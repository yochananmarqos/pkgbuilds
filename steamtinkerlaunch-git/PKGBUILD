# https://aur.archlinux.org/packages/steamtinkerlaunch
pkgname=steamtinkerlaunch-git
pkgver=2.0.6.r1.ge564f44
pkgrel=1
pkgdesc="Wrapper script for Steam custom launch options"
arch=('any')
url="https://github.com/frostworx/steamtinkerlaunch"
license=('GPL3')
depends=('bash')
makedepends=('git')
optdepends=(
    'strace: write a strace log of the launched game'
    'gamemode: for using GameMode per game'
    'mangohud: for using MangoHUD per game'
    'vkbasalt: for using vkBasalt per game'
    'winetricks: winetricks support'
    'wget: for optional reshade download'
    'unzip: for optional reshade download'
    'git: for pulling optional shaders'
    'xdotool: for playing regular games side-by-side in VR'
    'xorg-xwininfo: for playing regular games side-by-side in VR'
    'vr-video-player: for playing regular games side-by-side in VR'
    'nyrna: for using Nyrna per game'
    'replay-sorcery: for using Replay-Sorcery per game'
    'wmctrl: for optional minimize/maximize of all open windows on game start/stop'
    'net-tools: for optional network monitoring'
    'boxtron: for optional Boxtron support'
    'scummvm: for optional ScummVM support via Roberta'
    'wine: for optional Vortex Mod Manager support'
    'gameconqueror: for optional cheating'
    'xorg-xprop: for optional window toggle and side-by-side VR'
    'gamescope: for optional GameScope support'
    'libnotify: for optional Notifier'
    'yad: for the optional Settings Menu'
    'zenity: optional for the Editor Dialog'
    'xorg-xrandr: optional for automatic resolution detection'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/frostworx/steamtinkerlaunch.git')
sha1sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm755 stl -t "$pkgdir/usr/bin"

	install -d "$pkgdir/usr/share/stl"
	cp -r categories misc regs tweaks "$pkgdir/usr/share/stl"

	install -Dm644 README.md -t "$pkgdir/usr/share/doc/${pkgname%-git}"
}
