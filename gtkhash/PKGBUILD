# https://aur.archlinux.org/packages/gtkhash

# Only for Nautilus. For other file mangers, see
#  https://gitlab.manjaro.org/packages/community/gtkhash

pkgname=('gtkhash' 'gtkhash-nautilus')
pkgbase=gtkhash
pkgver=1.2
pkgrel=3
pkgdesc="A GTK+ utility for computing message digests or checksums"
arch=('i686' 'x86_64')
url="https://github.com/tristanheaven/gtkhash"
license=('GPL')
makedepends=('intltool' 'librsvg' 'libnautilus-extension')
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('bd870bac6e14babfb6268b617a42e4bcd776559dd80dd62ad9f7cc28b773b8b2')

build() {
	cd "$pkgbase-$pkgver"

	./configure \
		--prefix=/usr \
		--disable-schemas-compile \
		--enable-gtkhash \
		--enable-linux-crypto \
		--enable-nettle \
		--disable-blake2 \
		--with-gtk=3.0 \
		--enable-nautilus
	make
}

package_gtkhash() {
	depends=('dconf' 'nettle' 'gtk3')

	make -C "$pkgbase-$pkgver" DESTDIR="$pkgdir/" install

	# remove filemanager plugins
	rm -rf "$pkgdir"/usr/lib
}

package_gtkhash-nautilus() {
	pkgdesc+=' (Nautilus filemanager plugin)'
	depends=('gtkhash' 'nautilus')

	make -C "$pkgbase-$pkgver/src/nautilus" DESTDIR="$pkgdir/" install
}
