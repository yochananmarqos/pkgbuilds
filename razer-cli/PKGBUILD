# https://aur.archlinux.org/packages/razer-cli

pkgname=razer-cli
pkgver=2.3.0
pkgrel=1
pkgdesc="CLI for configuring Razer devices."
arch=('any')
url="https://github.com/lolei/razer-cli"
license=('GPL-3.0-or-later')
depends=(
  'python-openrazer'
  'xorg-xrdb'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('0f2a0d1f5b187209e33f4476807de226fe8c3215ca9a4f43021fb49d70adbbbe')

build() {
	cd "$pkgname-$pkgver"
	python -m build --wheel --no-isolation
}

check() {
	cd "$pkgname-$pkgver"
	xvfb-run -a -s "-screen 0 1024x768x24" python -m unittest discover -v
}

package() {
	cd "$pkgname-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl

  # Remove installed tests
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -r "${pkgdir}${site_packages}/razer_cli/tests/"
}
