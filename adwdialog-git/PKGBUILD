# https://aur.archlinux.org/packages/adwdialog-git

pkgname=adwdialog-git
pkgver=0.2.0.r14.g289cd34
pkgrel=1
pkgdesc="Display GTK4/libadwaita dialogs from terminal and scripts."
arch=('any')
url="https://github.com/Vanilla-OS/AdwDialog"
license=('GPL-3.0-or-later')
depends=(
  'libadwaita'
  'libjpeg-turbo'
  'python-gobject'
)
makedepends=(
  'git'
  'meson'
)
checkdepends=('appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Vanilla-OS/AdwDialog.git')
sha256sums=('SKIP')

pkgver() {
  cd AdwDialog
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson AdwDialog build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
