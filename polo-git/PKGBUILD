pkgname=polo-git
pkgver=18.8.beta.r44.ge27f7a4
pkgrel=1
pkgdesc="A modern file manager for Linux"
arch=('x86_64')
url="https://teejee2008.github.io/polo/"
license=('GPL2')
depends=('bzip2' 'gtk3' 'gvfs' 'libgee' 'libsoup' 'p7zip' 'pbzip2' 'pigz' 'pv'
         'rclone' 'trash-cli' 'vte3' 'wget')
makedepends=('git' 'lsb-release' 'vala')
checkdepends=('appstream-glib')
optdepends=('clamav: ClamAV antivirus scanner'
            'cryptsetup: Unlock encrypted LUKS devices'
            'diffuse: File compare utility'
            'ffmpeg: Generate thumbnails for video'
            'fish: Terminal shell'
            'ghostscript: Convert PDF files'
            'gnome-disk-utility: GNOME Disk Utility'
            'groot: Groot Utility for chroot'
            'imagemagick: Convert images and PDF documents'
            'lzop: Read and extract LZO archives'
            'mediainfo: Read media properties from audio and video files'
            'pdftk: Convert PDF files'
            'perl-image-exiftool: read EXIF properties from JPG/TIFF/PNG/PDF files'
            'pngcrush: Reduce file size of PNG files'
            'polo-donation-plugins: ClamAV, Image ISO, PDF, Video Download plugins'
#            'qemu-kvm: Virtual machine emulator' ?
            'tar: Read and extract TAR archives'
            'udisks2: Mount and unmount devices'
            'util-linux: Read device information'
            'xdg-utils: Set file type associations'
            'youtube-dl: YouTube downloader')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/teejee2008/polo.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  # Recursive make commands should always use the variable MAKE, 
  # not the explicit command name make
  # https://real-world-systems.com/docs/make.1.html#SEC59
  sed -i 's/make/$(MAKE)/g' makefile
}

build() {
  cd "$srcdir/${pkgname%-git}"
  export CFLAGS="${CFLAGS} --std=c99"
  make
}

check() {
  cd "$srcdir/${pkgname%-git}"
  appstream-util validate-relax --nonet "debian/${pkgname%-git}-gtk.appdata.xml"
  desktop-file-validate "src/${pkgname%-git}-gtk.desktop"
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir/usr/bin/${pkgname%-git}-uninstall"
}

