# https://aur.archlinux.org/packages/system76-scheduler-git
groups=('modified')

pkgname=system76-scheduler-git
pkgver=2.0.0.r1.g38fb688
pkgrel=1
pkgdesc="Auto-configure CFS and process priorities for improved desktop responsiveness"
arch=('x86_64')
url="https://github.com/pop-os/system76-scheduler"
license=('MPL2')
depends=('bcc-tools' 'gcc-libs' 'libpipewire' 'python-bcc')
makedepends=('cargo' 'git' 'just')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/pop-os/system76-scheduler.git'
        'manjaro.kdl')
backup=("etc/${pkgname%-git}/config.kdl")
sha256sums=('SKIP'
            'fd775a24918b14965b5692e2defb5e2aa64966a859b21aac65dbdd5e7bc3c341')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}


build() {
  cd "$srcdir/${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  export EXECSNOOP_PATH=/usr/share/bcc/tools/execsnoop
  just build-release
}

package() {
  cd "$srcdir/${pkgname%-git}"
  just rootdir="$pkgdir" install

  # Remove default Pop!_OS config & install Manjaro config
  rm -rf "$pkgdir/etc/${pkgname%-git}/process-scheduler"
  install -Dm644 "$srcdir/manjaro.kdl" -t \
    "$pkgdir/usr/share/${pkgname%-git}/process-scheduler/"
}
