pkgname=python-smmap
_name=${pkgname#python-}
pkgver=3.0.1
pkgrel=1
pkgdesc="A pure git implementation of a sliding window memory map manager"
arch=('any')
url="https://github.com/gitpython-developers/smmap"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz") #{,.asc})
sha256sums=('171484fe62793e3626c8b05dd752eb2ca01854b0c55a1efc0dc4210fccb65446')
#            'SKIP')
#validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60')
#              # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	nosetests
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --skip-build \
		--optimize=1 \
		--prefix=/usr \
		--root="$pkgdir"
	install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
	install -vDm 644 README.md -t "$pkgdir}/usr/share/doc/$pkgname/"
}
