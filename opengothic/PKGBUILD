# https://aur.archlinux.org/packages/opengothic/
groups=('modified')

pkgname=opengothic
pkgver=0.35
pkgrel=1
pkgdesc="Open source remake of Gothic 2: Night of the raven"
arch=('x86_64')
url="https://github.com/Try/OpenGothic"
license=('MIT')
depends=('alsa-lib' 'bullet' 'libsquish' 'libx11' 'physfs' 'vulkan-icd-loader')
makedepends=('cmake' 'git' 'glslang' 'libglvnd' 'vulkan-headers')
optdepends=('doxygen: build documentation')
provides=('libTempest.so')
source=("git+https://github.com/Try/OpenGothic#tag=v${pkgver}"
        'git+https://github.com/Try/Tempest.git'
        'git+https://github.com/schellingb/TinySoundFont.git'
        'git+https://github.com/Try/ZenLib.git'
        'git+https://github.com/bulletphysics/bullet3.git'
        'git+https://github.com/tito/libsquish.git'
        'git+https://github.com/physfs-mirror/PhysicsFS.git')
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd OpenGothic
  for submodule in Tempest TinySoundFont ZenLib bullet3; do
    git submodule init "lib/${submodule}"
    git config submodule."lib/${submodule}".url "${srcdir}/${submodule#*/}"
    git submodule update "lib/${submodule}"
  done

  pushd lib/ZenLib
  git submodule init lib/libsquish
  git config submodule.lib/libsquish.url ${srcdir}/libsquish
  git submodule update lib/libsquish

  git submodule init lib/physfs
  git config submodule.lib/physfs.url ${srcdir}/PhysicsFS
  git submodule update lib/physfs
  popd
}

build() {
  cmake -B build -S OpenGothic \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DOpenGL_GL_PREFERENCE='GLVND' \
    -DPHYSFS_BUILD_SHARED='OFF'
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install

  # Remove conflicting files with bullet, libsquish & phyfs
  rm -rf "${pkgdir}"/usr/include/bullet
  rm -f "${pkgdir}"/usr/include/{physfs.h,squish.h}
  rm -f "${pkgdir}"/usr/lib/cmake/bullet/{BulletConfig.cmake,UseBullet.cmake}
  rm -f "${pkgdir}"/usr/lib/{libphysfs.a,libsquish.a}
  rm -f "${pkgdir}"/usr/lib/pkgconfig/{bullet.pc,physfs.pc}

  install -Dm644 OpenGothic/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
