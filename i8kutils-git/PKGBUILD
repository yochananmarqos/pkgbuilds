# https://aur.archlinux.org/packages/i8kutils-git
pkgname=i8kutils-git
pkgrel=1
pkgver=1.43.r90.611721a
pkgdesc="Fan control for Dell laptops"
arch=("i686" "x86_64")
url="https://launchpad.net/i8kutils"
license=('GPL')
makedepends=('git')
optdepends=('tcl: for i8kmon daemon'
            'acpi: for i8kmon daemon')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=("etc/${pkgname%-git}/i8kmon.conf")
source=('git+https://github.com/vitorafsr/i8kutils.git'
        'i8kmon.service')
sha256sums=('SKIP'
            '00bc2d2724805f7ed40c9d3f4c03a68b98bf58c0b92452c48d5ef90fe086fb16')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s.r%s.%s" "$(head -n 1 debian/changelog | cut -d'(' -f 2 | cut -d')' -f 1 | \
		sed 's/-/./')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	# conform to Arch Linux guidelines
	sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' i8kmon.1
	sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' i8kmon.conf
	sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' i8kmon
	echo "dell-smm-hwmon" > modules-load.d-dell-smm-hwmon.conf
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make clean
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir"/usr/{bin,share/man/man1}
	install -Dm644 i8kctl.1 i8kmon.1 "$pkgdir/usr/share/man/man1"
	install -Dm755 i8kctl i8kfan i8kmon "$pkgdir/usr/bin"

	install -Dm644 i8kmon.conf -t "$pkgdir/etc/${pkgname%-git}"
	install -Dm644 dell-smm-hwmon.conf -t "$pkgdir/etc/modprobe.d"
	install -Dm644 modules-load.d-dell-smm-hwmon.conf -t "$pkgdir/etc/modules-load.d"
	install -Dm644 "$srcdir/i8kmon.service" -t "$pkgdir/usr/lib/systemd/system"
	install -Dm644 "README.${pkgname%-git}" "$pkgdir/usr/share/doc/${pkgname%-git}/README"
	# rmmod dell-smm-hwmon && modprobe dell-smm-hwmon
}
