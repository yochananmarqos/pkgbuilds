pkgname=python-cheat
_gitname=cheat
pkgver=2.4.0
pkgrel=1
pkgdesc="Create and view interactive cheatsheets on the command-line."
arch=('any')
url="https://github.com/chrisallenlane/$_gitname"
depends=('python' 'python-docopt')
makedepends=('python-setuptools')
optdepends=('python-pygments' 'bash-completion' 'zsh-completions' 'fish')
license=('GPL3')
provides=("$_gitname")
conflicts=("$_gitname-git" "$_gitname-bash-git")
source=($pkgname-$pkgver.tar.gz::"https://github.com/chrisallenlane/$_gitname/archive/$pkgver.tar.gz")
sha256sums=('d56d0d01717507da59aa2e6b932b0c37a8e842e547f432d0a3f90faea5bdd2dc')

build() {
	cd "$_gitname-$pkgver"
	python setup.py build
}

package() {
	cd "$_gitname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -dm755 "$pkgdir/"usr/share/{bash-completion/completions,zsh/site-functions,fish/completions}
	install -m 644 "$srcdir/$_gitname-$pkgver/$_gitname/autocompletion/$_gitname.bash" "$pkgdir/usr/share/bash-completion/completions/$_gitname"
	install -m 644 "$srcdir/$_gitname-$pkgver/$_gitname/autocompletion/$_gitname.zsh" "$pkgdir/usr/share/zsh/site-functions/$_gitname"
	install -m 644 "$srcdir/$_gitname-$pkgver/$_gitname/autocompletion/$_gitname.fish" "$pkgdir/usr/share/fish/completions/$_gitname"
}

