# https://aur.archlinux.org/pkgbase/protonutils
groups=('modified')

pkgname=protonutils
pkgver=1.6.0
pkgrel=1
pkgdesc="Lists configured Proton version per game."
arch=('x86_64')
url="https://github.com/nning/protonutils"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('cdc1517468d8b0e1867a39cccbd050986973fef8174f833ea9ebf3b996f6df64')

prepare() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"

  # create directory for build output
  mkdir -p build

  # download dependencies
  go mod download -x
}

build() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"
  local VERSION="v$pkgver"
  local BUILDTIME=$(date -u +"%Y%m%d%H%M%S")
  local GOLDFLAGS="-X 'main.Version=${VERSION}' \
                   -X 'main.Buildtime=${BUILDTIME}'"
  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${GOLDFLAGS}" \
    -buildvcs=false \
    -o build ./cmd/...

  # Generate man page
  ./build/"$pkgname" -m man1

  # Clean module cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "build/$pkgname" -t "$pkgdir/usr/bin/"
  install -Dm644 man1/* -t "$pkgdir/usr/share/man/man1/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
