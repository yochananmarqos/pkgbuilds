pkgname=python-fonttools
_name=${pkgname#python-}
pkgver=4.5.0
pkgrel=1
pkgdesc="A library to manipulate font files from Python"
url="https://github.com/fonttools/fonttools"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
optdepends=('python-lxml: faster backend for XML files reading/writing'
            'python-fs: to read/write UFO source files'
            'python-brotli: to compress/decompress WOFF 2.0 web fonts'
            'python-zopfli: faster backend fom WOFF 1.0 web fonts compression'
            'python-scipy: for finding wrong contour/component order between different masters'
            'python-matplotlib: for visualizing DesignSpaceDocument and resulting VariationModel'
            'python-sympy: for symbolic font statistics analysis'
            'python-pyqt5: for drawing glyphs with QPainterPath'
            'python-reportlab: to drawing glyphs as PNG images')
source=("https://github.com/$_name/$_name/releases/download/$pkgver/$_name-$pkgver.zip")
#source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('bba1f50899dccd234fb3fa6ddf9ea9c15fe1a5435dfde42cbec9fe2a9c4398af')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
