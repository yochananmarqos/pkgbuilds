# https://www.archlinux.org/packages/community/any/python-fonttools
pkgname=python-fonttools
_name=${pkgname#python-}
pkgver=4.9.0
pkgrel=1
pkgdesc="A library to manipulate font files from Python"
url="https://github.com/fonttools/fonttools"
license=('MIT')
arch=('x86_64')
depends=('python')
makedepends=('cython')
optdepends=('python-lxml: faster backend for XML files reading/writing'
	    'python-fs: to read/write UFO source files'
	    'python-brotli: to compress/decompress WOFF 2.0 web fonts'
	    'python-zopfli: faster backend fom WOFF 1.0 web fonts compression'
	    'python-scipy: for finding wrong contour/component order between different masters'
	    'python-matplotlib: for visualizing DesignSpaceDocument and resulting VariationModel'
	    'python-unicodedata2: for displaying the Unicode character names when dumping the cmap table with ttx'
	    'python-sympy: for symbolic font statistics analysis'
	    'python-pyqt5: for drawing glyphs with Qt QPainterPath'
	    'python-reportlab: to drawing glyphs as PNG images'
	    'python-lz4: for graphite type tables in ttLib/tables')
source=("https://github.com/$_name/$_name/releases/download/$pkgver/$_name-$pkgver.zip")
#source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('16dd6ae4d057e170756339cde146e7295c721baad46e197b98a68d4cdd425c94')

build() {
	cd "$_name-$pkgver"
	python setup.py build_ext --inplace
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
