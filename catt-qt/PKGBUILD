# https://aur.archlinux.org/packages/catt-qt
groups=('modified')

pkgname=catt-qt
_name=cattqt
pkgver=4.0
pkgrel=3
pkgdesc="A control GUI for Chromecasts"
arch=('any')
url="https://github.com/soreau/catt-qt"
license=('GPL-3.0-or-later')
depends=(
  'catt'
  'python-brotli'
  'python-chardet'
  'python-mutagen'
  'python-pychromecast'
  'python-pycryptodomex'
  'python-pyqt5'
  'python-six'
  'python-websockets'
  'xdg-desktop-portal'
)
makedepends=(
  'python-build'
  'python-flit'
  'python-installer'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('c5f0b39b0b33ffa1ff2aa9db884da9dbfd0ad36611eb295b1b9296fb3814b46f'
            'e8b07e4bd990b6adbd6a1d65cb0b7b3e139faaad735ceb909a8d5e5e88795401')

build() {
  cd "$pkgname-$pkgver"

  # This package requires itself to build and can't find itself, 
  # thus --skip-dependency-check and set PYTHONPATH
  PYTHONPATH=./ python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 cattqt/chromecast.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
}
