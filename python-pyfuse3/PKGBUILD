# https://aur.archlinux.org/packages/python-pyfuse3
groups=('modified')

pkgname=python-pyfuse3
_name=${pkgname#python-}
pkgver=3.2.3
pkgrel=2
pkgdesc="Python 3 bindings for libfuse 3 with asynchronous API (Trio compatible)"
arch=('x86_64')
url="https://github.com/libfuse/pyfuse3"
license=('GPL2')
depends=('fuse3' 'python' 'python-trio')
makedepends=('cython' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
#checkdepends=('python-pytest' 'python-pytest-trio')
source=("$url/releases/download/$pkgver/$_name-$pkgver.tar.gz"{,.asc})
sha256sums=('0771e921fd2a1c36bd4ed6b74db0616f4d40c8ac68994a80f3cc8a249fa807d8'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F' # Nikolaus Rath <Nikolaus@rath.org>
              '6D5BEF9ADD2075805747B70F9F88FB52FAF7B393') # Thomas Waldmann <tw@waldmann-edv.de

build() {
  cd "$_name-$pkgver"
  python setup.py build_cython
  python -m build --wheel --no-isolation
}

#check() {
#  cd "$_name-$pkgver"
#  PYTHONPATH=./ pytest test/
#}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
