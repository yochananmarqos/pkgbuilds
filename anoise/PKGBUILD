# https://aur.archlinux.org/packages/anoise
pkgname=anoise
pkgver=0.0.36
pkgrel=1
pkgdesc="Ambient Noise Player. Relax or concentrate with a noise"
arch=('any')
url="https://costales.github.io/projects/anoise"
license=('GPL3')
depends=('gst-python' 'webkit2gtk' 'anoise-media')
makedepends=('python-distutils-extra')
optdepends=('anoise-community-extension1: Sounds and icons from the users'
            'anoise-community-extension2: Sounds and icons from the users'
            'anoise-community-extension3: Sounds and icons from the users'
            'anoise-community-extension4: Sounds and icons from the users'
            'anoise-community-extension5: Sounds and icons from the users'
            'anoise-gui: GUI for anoise'
            'libappindicator-gtk3: tray icon')
source=("https://launchpad.net/~costales/+archive/ubuntu/$pkgname/+sourcefiles/$pkgname/$pkgver/${pkgname}_${pkgver}.tar.gz")
sha256sums=('cd6e2e1e8691b950c503b4319f7f9ecf6e66c745e5194724be0c3e026e9dd3ac')

build() {
	cd "$srcdir/$pkgname "
	python setup.py build
}

package() {
	cd "$srcdir/$pkgname "
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build


	# Files setup.py does not install

	install -Dm644 "$pkgname"/* "$pkgdir/usr/share/$pkgname"
	# This file is included in anoise-gui
	rm "$pkgdir/usr/share/$pkgname/$pkgname.ui"

	install -d "$pkgdir/usr/share/icons"
	cp -r icons/hicolor "$pkgdir/usr/share/icons"

	install -d "$pkgdir/usr/locale"
	cp -r locale "$pkgdir/usr/locale"

	sed -i 's/anoise.desktop.in/Ambient Noise/g' \
		"build/share/applications/$pkgname.desktop"
	install -Dm644 "build/share/applications/$pkgname.desktop" -t \
		"$pkgdir/usr/share/applications"
}
