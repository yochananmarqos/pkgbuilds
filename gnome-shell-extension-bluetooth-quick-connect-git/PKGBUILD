# https://aur.archlinux.org/pkgbase/gnome-shell-extension-bluetooth-quick-connect-git
groups=('modified')

pkgname=gnome-shell-extension-bluetooth-quick-connect-git
_reponame=gnome-bluetooth-quick-connect
pkgver=26.r1.g9a1ef87
pkgrel=2
pkgdesc='Allow to connect bluetooth paired devices from gnome control panel'
arch=('any')
url="https://github.com/bjarosze/gnome-bluetooth-quick-connect"
license=('GPL3')
depends=('gnome-shell' 'bluez-utils')
makedepends=('git')
source=("git+https://github.com/bjarosze/gnome-bluetooth-quick-connect.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_reponame}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_reponame}"
  make
}

package() {
  cd "${srcdir}/${_reponame}"

  _uuid='bluetooth-quick-connect@bjarosze.gmail.com'

  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  cp -r * "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"

  install -Dm644 schemas/org.gnome.shell.extensions.bluetooth-quick-connect.gschema.xml -t \
    "$pkgdir/usr/share/glib-2.0/schemas"

  rm "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/LICENSE"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas/"
}
