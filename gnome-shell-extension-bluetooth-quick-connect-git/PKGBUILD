# https://aur.archlinux.org/pkgbase/gnome-shell-extension-bluetooth-quick-connect-git
groups=('modified')

pkgname=gnome-shell-extension-bluetooth-quick-connect-git
_uuid=bluetooth-quick-connect@bjarosze.gmail.com
pkgver=49.r0.g74ed30c
pkgrel=1
pkgdesc="Allow to connect Bluetooth paired devices from GNOME control panel."
arch=('any')
url="https://github.com/Extensions-Valhalla/gnome-bluetooth-quick-connect"
license=('GPL3')
depends=('gnome-shell' 'bluez-utils')
makedepends=('git' 'pnpm' 'zip')
source=('git+https://github.com/Extensions-Valhalla/gnome-bluetooth-quick-connect.git')
sha256sums=('SKIP')

pkgver() {
  cd gnome-bluetooth-quick-connect
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd gnome-bluetooth-quick-connect
  export PNPM_HOME="$srcdir/pnpm-home"
  pnpm install
  pnpm build
}

package() {
  cd gnome-bluetooth-quick-connect
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar -xvf "dist/${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "$pkgdir/usr/share"

  install -Dm644 dist/schemas/org.gnome.shell.extensions.bluetooth-quick-connect.gschema.xml -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"

  rm -rv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas"
}
