# https://aur.archlinux.org/packages/nautilus-copy-path
groups=('modified')

pkgname=nautilus-copy-path
pkgver=1.7.0
pkgrel=1
pkgdesc="Extension for Nautilus to copy files/dir path or name"
arch=('any')
url="https://github.com/chr314/nautilus-copy-path"
license=('MIT')
depends=('python-nautilus' 'python-gobject')
backup=("usr/share/nautilus-python/extensions/$pkgname/config.json")
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('0c52c28a368ba06b20240cdf504d2e5ba1f1c7632f593714b700100d4105a4ef')

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 "$pkgname.py" -t "$pkgdir/usr/share/nautilus-python/extensions"
  install -Dm644 nautilus_copy_path.py translation.py config.json -t \
    "$pkgdir/usr/share/nautilus-python/extensions/$pkgname"
  install -Dm644 translations/*.json -t \
    "$pkgdir/usr/share/nautilus-python/extensions/$pkgname/translations"

  # Compile Python bytecode:
  python -m compileall -d /usr/share "$pkgdir/usr/share"
  python -O -m compileall -d /usr/share "$pkgdir/usr/share"

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
