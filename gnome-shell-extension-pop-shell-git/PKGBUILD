# https://gitlab.manjaro.org/packages/community/gnome/gnome-shell-extension/-/tree/master/gnome-shell-extension-pop-shell
# https://aur.archlinux.org/packages/gnome-shell-extension-pop-shell-git
pkgname=gnome-shell-extension-pop-shell-git
pkgver=1.0.0.r0.g18243a2
pkgrel=1
pkgdesc="Pop Shell - Tiling window management in Gnome (WIP)"
arch=('any')
url="https://github.com/pop-os/shell"
license=('GPL3')
depends=('gnome-shell' 'pop-shell-shortcuts')
makedepends=('git' 'typescript')
optdepends=('gnome-shell-extensions: native-window extension for improved window placement')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install='pop-shell.install'
source=('git+https://github.com/pop-os/shell.git')
sha256sums=('SKIP')

# Fix dbus-launch error on X11 running install file commands
#if [[ $(echo $XDG_SESSION_TYPE) = x11 ]]; then
#	depends+=('dbus-x11')
#elif [[ $(echo $XDG_SESSION_TYPE) = wayland ]]; then
#	depends+=('dbus')
#fi

pkgver() {
    cd "$srcdir/shell"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/shell"
	make all
	make schemas
}

package() {
	cd "$srcdir/shell"
	make DESTDIR="$pkgdir" install

	install -Dm755 scripts/configure.sh -t \
		"$pkgdir/usr/share/gnome-shell/extensions/pop-shell@system76.com/scripts"
}
