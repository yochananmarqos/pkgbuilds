# https://aur.archlinux.org/packages/fresh-editor

pkgname=fresh-editor
_app_id=io.github.sinelaw.fresh
pkgver=0.2.11
pkgrel=1
pkgdesc="A lightweight, fast terminal-based text editor with LSP support and TypeScript plugins"
url="https://getfresh.dev"
license=('GPL-2.0-or-later')
arch=('x86_64' 'aarch64')
depends=(
  'bash'
  'glibc'
  'hicolor-icon-theme'
  'libgcc'
)
makedepends=(
  'cargo'
  'clang'
  'desktop-file-utils'
  'git'
)
checkdepends=(
  'appstream'
  'cargo-nextest'
  'xorg-server-xvfb'
)
source=("git+https://github.com/sinelaw/fresh.git#tag=v$pkgver")
sha256sums=('51134d23e22af7c13c6b34ba39c95b1fd6f30e3f46a1b30b550d32dd24fad520')

prepare() {
  cd fresh
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc --print host-tuple)"

  # Enable launching with experimental GUI
  desktop-file-edit --set-key=Exec --set-value="fresh %F --gui" \
    "crates/$pkgname/flatpak/${_app_id}.desktop"
}

build() {
  cd fresh
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export CC=clang

  # Enable --all-features to build experimental GUI
  cargo build --locked --release --all-features
}

check() {
  cd fresh
  export RUSTUP_TOOLCHAIN=stable

  # 3934 tests run: 3931 passed, 3 failed, 86 skipped
  WGPU_BACKEND=vulkan xvfb-run cargo nextest run -j=4 --no-fail-fast --locked --all-features --all-targets || :

  appstreamcli validate --no-net "crates/$pkgname/flatpak/${_app_id}.metainfo.xml"
  desktop-file-validate "crates/$pkgname/flatpak/${_app_id}.desktop"
}

package() {
  cd fresh
  install -Dm755 target/release/fresh -t "$pkgdir/usr/share/$pkgname/"
  cp -a "crates/$pkgname"/{keymaps,plugins,themes} "$pkgdir/usr/share/$pkgname/"

  install -d "$pkgdir/usr/bin"
  ln -s "/usr/share/$pkgname/fresh" "$pkgdir/usr/bin/"

  install -Dm644 "crates/$pkgname/flatpak/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "crates/$pkgname/flatpak/${_app_id}.metainfo.xml" -t \
    "$pkgdir/usr/share/metainfo/"
  install -Dm644 "crates/$pkgname/flatpak/${_app_id}.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 "crates/$pkgname/config.example.json" -t \
    "$pkgdir/usr/share/doc/$pkgname/"
}
