# https://aur.archlinux.org/packages/overskride

pkgname=overskride
pkgver=0.6.5
pkgrel=1
pkgdesc="A simple yet powerful bluetooth client"
arch=('x86_64' 'aarch64')
url="https://github.com/kaii-lb/overskride"
license=('GPL-3.0-or-later')
depends=(
  'bluez'
  'gtk4'
  'libadwaita'
  'libpulse'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'git'
  'meson'
)
checkdepends=('appstream-glib')
source=("git+https://github.com/kaii-lb/overskride.git#tag=v$pkgver")
sha256sums=('ba5eac152c4c4effea3e83c3179ac35555520680b34f4ac859f17fdc10333949')

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc --print host-tuple)"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "$pkgname" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
