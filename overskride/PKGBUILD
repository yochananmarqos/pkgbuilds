# https://aur.archlinux.org/packages/overskride

pkgname=overskride
pkgver=0.6.3
pkgrel=1
pkgdesc="A simple yet powerful bluetooth client"
arch=('x86_64' 'aarch64')
url="https://github.com/kaii-lb/overskride"
license=('GPL-3.0-or-later')
depends=(
  'bluez'
  'gtk4'
  'libadwaita'
  'libpulse'
)
makedepends=(
  'blueprint-compiler'
  'cargo'
  'git'
  'meson'
)
checkdepends=('appstream-glib')
source=("git+https://github.com/kaii-lb/overskride.git#tag=v$pkgver")
sha256sums=('e691485d5d847795e8e37f8bccab08eeed23d5696958e5d08c0741fa4f7a30f2')

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "$pkgname" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
