# https://archlinux.org/packages/community/any/python-dropbox/
pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.13.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-six python-stone)
makedepends=(python-setuptools)
#checkdepends=(python-pytest-mock python-mock python-pytest-runner python-coverage)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('9523d22375514d600399f730d9925232c64dee78511ebcb7775a722621a2f0224ae09c0f4403abdbd2c73d5a478bf60282840c5df54f2e99d591089e7ed3d753')

prepare() {
  cd "$_pkgname-$pkgver"

  # Pytest Runner not required to build
  sed -i '/pytest-runner/d' setup.py
}

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd $_pkgname-$pkgver"
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#}

package() {
  cd "$_pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
