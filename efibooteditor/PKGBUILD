# https://aur.archlinux.org/packages/efibooteditor

pkgname=efibooteditor
pkgver=1.5.6
pkgrel=1
pkgdesc="Boot Editor for (U)EFI based systems"
arch=('x86_64')
url="https://github.com/Neverous/efibooteditor"
license=('LGPL-3.0-or-later')
depends=(
  'efivar'
  'qt6-base'
  'qt6-svg'
)
makedepends=(
  'cmake'
  'git'
  'qt6-tools'
)
source=("git+https://github.com/Neverous/efibooteditor.git#tag=v$pkgver?signed")
sha256sums=('3c35715b93621a915c3d107fc530d2d46fb849e6fb460f204743ebec767afb5f')
validpgpkeys=('9E76A390AAF9772C8448DCCEE4F948F71FEE7375') # Maciej Szeptuch <neverous@neverous.info>

build() {
  export BUILD_VERSION="$pkgver"
  cmake -B build -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DQT_VERSION_MAJOR='6' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm -v "$pkgdir/usr/AppRun"
}
