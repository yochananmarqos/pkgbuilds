# https://aur.archlinux.org/packages/efibooteditor

pkgname=efibooteditor
pkgver=1.5.5
pkgrel=1
pkgdesc="Boot Editor for (U)EFI based systems"
arch=('x86_64')
url="https://github.com/Neverous/efibooteditor"
license=('LGPL-3.0-or-later')
depends=(
  'efivar'
  'qt6-base'
)
makedepends=(
  'cmake'
  'git'
  'qt6-tools'
  'vulkan-headers'
)
source=("git+https://github.com/Neverous/efibooteditor.git#tag=v$pkgver?signed")
sha256sums=('bf298671eec245f83e7b53a7126889934bcf7a95cf84131ba3515fddc67102f8')
validpgpkeys=('9E76A390AAF9772C8448DCCEE4F948F71FEE7375') # Maciej Szeptuch <neverous@neverous.info>

build() {
  export BUILD_VERSION="$pkgver"
  cmake -B build -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DQT_VERSION_MAJOR='6' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
