# https://aur.archlinux.org/packages/efibooteditor
groups=('modified')

pkgname=efibooteditor
pkgver=1.5.0
pkgrel=1
pkgdesc="GUI for editing EFI Boot Manager entries"
arch=('x86_64')
url="https://github.com/Neverous/efibooteditor"
license=('LGPL-3.0-or-later')
depends=(
  'efivar'
  'qt6-base'
)
makedepends=(
  'cmake'
  'qt6-tools'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('99a9824fc2d2c573803db91a7e1bf22de50758f43f5c50072da1fef17f695b7f')

build() {
  export BUILD_VERSION="$pkgver"
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DQT_VERSION_MAJOR='6' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
