# https://www.archlinux.org/packages/community/any/python-markdown
pkgname=python-markdown
_name=Markdown
pkgver=3.2.1
pkgrel=1
pkgdesc="Python implementation of John Gruber's Markdown."
arch=('any')
url="https://python-markdown.github.io"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-yaml')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('90fee683eeabe1a92e149f7ba74e5ccdc81cd397bd6c516d93a8da0ef90b6902')


prepare() {
	# bug in 2.4, some DOS line endings slipped in
	find "$_name-$pkgver/" -name '*py' -exec sed -i 's|\r||g' {} +

	# ImportError: No module named pkg_resources
	sed -i 's/^from pkg_resources.*$/import packaging.version/' "$_name-$pkgver/markdown/__init__.py"
}

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	python -m unittest discover tests

	[[ $(python -c "import markdown; print(markdown.version)") == "$pkgver" ]]
	[[ $(python -c "import markdown; print(markdown.markdown('*test*'))") == "<p><em>test</em></p>" ]]
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
