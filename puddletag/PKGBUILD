# https://aur.archlinux.org/packages/puddletag

pkgname=puddletag
pkgver=2.5.0
pkgrel=1
pkgdesc='An audio tag editor for GNU/Linux'
arch=('any')
url="https://docs.puddletag.net"
license=('GPL-3.0-or-later')
depends=(
  'python-certifi'
  'python-charset-normalizer'
  'python-configobj'
  'python-distro'
  'python-idna'
  'python-mutagen'
  'python-pyparsing'
  'python-pyqt5'
  'python-pyqt5-sip'
  'python-requests'
  'python-unidecode'
  'python-urllib3'
  'qt5-svg'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
optdepends=(
  'python-levenshtein: faster duplicate matching'
  'python-lxml: additional tag sources'
  'python-pyacoustid: AcoustID support'
  'quodlibet: QuodLibet library support'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/puddletag/puddletag/archive/${pkgver}.tar.gz")
sha512sums=('05190c3c03d25bde1c247929fa76036a1975565ca22484cfff195a0921310bcb1bbcbf254f69d96811866e900e81193040cd2a853c5301acbb8d0fe783f9e6ba')

build() {
  cd "${pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
