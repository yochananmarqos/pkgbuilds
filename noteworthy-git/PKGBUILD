# https://aur.archlinux.org/packages/noteworthy-git
groups=('modified')

pkgname=noteworthy-git
pkgver=r873.bc144ab
pkgrel=1
pkgdesc="Modern, Fast, and Version-Controlled Markdown Notes App"
arch=('x86_64')
url="https://github.com/SeaDve/Noteworthy"
license=('GPL-3.0-or-later')
depends=('gst-plugins-base' 'gstreamer' 'gtksourceview5' 'libadwaita' 'libpulse')
makedepends=('cargo' 'git' 'meson')
checkdepends=('appstream-glib' 'openssh' 'xorg-server-xvfb')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/SeaDve/Noteworthy.git')
sha256sums=('SKIP')

pkgver() {
  cd Noteworthy
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd Noteworthy
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  arch-meson Noteworthy build
  meson compile -C build
}

check() {
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  xvfb-run -a -s "-screen 0 1024x768x24" dbus-run-session meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}

