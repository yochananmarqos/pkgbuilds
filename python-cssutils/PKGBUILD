# https://aur.archlinux.org/packages/python-cssutils

pkgname=python-cssutils
_name=${pkgname#python-}
pkgver=2.11.1
pkgrel=1
pkgdesc="CSS Cascading Style Sheets library for Python"
arch=('any')
url="https://github.com/jaraco/cssutils"
license=('LGPL-3.0-or-later')
depends=(
  'python'
  'python-more-itertools'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-toml'
  'python-wheel'
)
checkdepends=(
  'python-jaraco.test'
  'python-pytest'
)
optdepends=('python-chardet: simplified encoding detection')
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('5f85d8181a412cd4ac8006625547141d634329a6b79ae5469155a16e265d0274')

build() {
  cd "$_name-$pkgver"
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd "$_name-$pkgver"
  PYTHONPATH=. pytest
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Remove installed tests
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "${pkgdir}${site_packages}/$_name/tests/"

  install -Dm644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname/"
}
