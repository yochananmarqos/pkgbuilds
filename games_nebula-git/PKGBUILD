pkgname=games_nebula-git
pkgver=r220.ebb4685
pkgrel=1
pkgdesc="Unofficial Linux client for GOG"
arch=('i686' 'x86_64')
url="https://github.com/yancharkin/games_nebula"
license=('GPL')
depends=('python-gobject' 'python-beautifulsoup4' 'python-lxml' 'python-pillow'
         'python-requests' 'python-pytz' 'python-dateutil' 'webkit2gtk' 'xorg-xrandr'
         'p7zip' 'htmlcxx' 'lgogdownloader')
makedepends=('git')
optdepends=('gksu' 'xterm' 'tar' 'cabextract' 'unshield' 'ffmpeg' 'wine' 'winetricks'
            'dosbox' 'scummvm' 'megatools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/yancharkin/${pkgname%-git}.git"
		"pygogapi::git+https://github.com/yancharkin/pygogapi.git"
		"goglib_scripts1::git+https://github.com/yancharkin/${pkgname%-git}_goglib_scripts.git"
		"goglib_scripts2::git+https://bitbucket.org/yancharkin/${pkgname%-git}_goglib_scripts.git"
		"goglib_scripts3::git+https://gitlab.com/yancharkin/${pkgname%-git}_goglib_scripts.git"
		"mylib_scripts1::git+https://github.com/yancharkin/${pkgname%-git}_mylib_scripts.git"
		"mylib_scripts2::git+https://bitbucket.org/yancharkin/${pkgname%-git}_mylib_scripts.git"
		"mylib_scripts3::git+https://gitlab.com/yancharkin/${pkgname%-git}_mylib_scripts.git"
		"goglib_images1::git+https://github.com/yancharkin/${pkgname%-git}_goglib_images.git"
		"mylib_images1::git+https://github.com/yancharkin/${pkgname%-git}_mylib_images.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git submodule init
	git submodule update
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/opt/${pkgname%-git}"
	cp -dr --no-preserve=ownership "$srcdir/${pkgname%-git}/." \
		"$pkgdir/opt/${pkgname%-git}/"

	cp -r "$srcdir/pygogapi/gogapi/." "$pkgdir/opt/${pkgname%-git}/gogapi/"
	cp -r "$srcdir/goglib_scripts1/." "$pkgdir/opt/${pkgname%-git}/scripts/goglib/"
	cp -r "$srcdir/goglib_scripts2/." "$pkgdir/opt/${pkgname%-git}/scripts/goglib/"
	cp -r "$srcdir/goglib_scripts3/." "$pkgdir/opt/${pkgname%-git}/scripts/goglib/"
	cp -r "$srcdir/mylib_scripts1/." "$pkgdir/opt/${pkgname%-git}/scripts/mylib/"
	cp -r "$srcdir/mylib_scripts2/." "$pkgdir/opt/${pkgname%-git}/scripts/mylib/"
	cp -r "$srcdir/mylib_scripts3/." "$pkgdir/opt/${pkgname%-git}/scripts/mylib/"
	cp -r "$srcdir/goglib_images1/." "$pkgdir/opt/${pkgname%-git}/images/goglib/"
	cp -r "$srcdir/mylib_images1/." "$pkgdir/opt/${pkgname%-git}/images/mylib/"

    install -dm755 "$pkgdir/usr/bin"
	echo '#!/bin/bash
	python "/opt/games_nebula/games_nebula.py"' > "$pkgdir/usr/bin/${pkgname%-git}"
	chmod 755 "$pkgdir/usr/bin/${pkgname%-git}"

	install -dm755 "$pkgdir/usr/share/applications"
	echo "[Desktop Entry]
Name=Games Nebula
Comment=Application for managing and playing games
Exec="/usr/bin/${pkgname%-git}"
Icon="/opt/${pkgname%-git}/images/icon.png"
Type=Application
Terminal=false
Categories=Game;" > "$pkgdir/usr/share/applications/${pkgname%-git}.desktop"
	chmod 644 "$pkgdir/usr/share/applications/${pkgname%-git}.desktop"

	# remove unneeded files
	rm -r "$pkgdir/opt/${pkgname%-git}/"{.git,.github}
	rm -f "$pkgdir/opt/${pkgname%-git}/"{setup.sh,start.sh,.gitignore}
}
