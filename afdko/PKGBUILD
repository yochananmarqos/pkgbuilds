pkgname=afdko
pkgver=3.1.0
pkgrel=1
pkgdesc="Adobe Font Development Kit for OpenType"
arch=('x86_64')
url="https://adobe-type-tools.github.io/afdko"
license=('Apache')
depends=('python-lxml' 'python-booleanoperations' 'python-cu2qu' 'python-mutatormath'
         'psautohint' 'python-ufonormalizer' 'python-ufoprocessor' 'lib32-glibc')
makedepends=('python-wheel' 'python-setuptools-scm')
checkdepends=('python-pytest')
source=("https://github.com/adobe-type-tools/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('a1ed382c44677e8c44f4735d1e127847d7dca41e30bd9aac266d7ca240bde1b7')

prepare() {
	cd "$pkgname-$pkgver"
	sed -i 's/ufoProcessor>=1.0.5,<=1.0.6/ufoProcessor>=1.0.6/g' requirements.txt
}

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	python setup.py test
}

package() {
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
