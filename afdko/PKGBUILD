# https://aur.archlinux.org/packages/afdko
pkgname=afdko
pkgver=3.2.1
pkgrel=3
pkgdesc="Adobe Font Development Kit for OpenType"
arch=('x86_64')
url="https://adobe-type-tools.github.io/afdko"
license=('Apache')
depends=('python-lxml' 'python-brotli' 'python-zopfli' 'python-unicodedata2'
         'python-booleanoperations' 'python-mutatormath' 'psautohint'
         'python-ufonormalizer' 'python-ufoprocessor')
makedepends=('python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('6525fdb2745428f6cf61ef14e82cfbf9252b821ac8a90f9ad32ce96e6211d218')

prepare() {
	cd "$pkgname-$pkgver"
	sed -i -e 's/==/>=/g;s/,<=[0-9.]\+//' requirements.txt
}

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	python setup.py test
}

package() {
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
