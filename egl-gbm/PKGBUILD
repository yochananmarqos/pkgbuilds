pkgname=egl-gbm
pkgver=1.1.2
pkgrel=1
pkgdesc="The GBM EGL external platform library"
arch=('x86_64')
url="https://github.com/NVIDIA/egl-gbm"
license=('MIT')
depends=(
  'eglexternalplatform'
  'glibc'
  'mesa'
  'libdrm'
)
makedepends=(
  'meson'
)
provides=('libnvidia-egl-gbm.so=1')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
        '15_nvidia_gbm.json')
sha256sums=('40c8a1a3df0639ea83031a92e9adcbef92ed696445307aa23cfc5895e63d11f0'
            'b22cd47d1a11a6c1324cff07f62f4695522ac41f945cfeb83fe484c6c56ff297')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 15_nvidia_gbm.json -t "$pkgdir/usr/share/egl/egl_external_platform.d/"
  install -Dm644 "$pkgname-$pkgver/COPYING" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
