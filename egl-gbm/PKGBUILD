pkgname=egl-gbm
pkgver=1.1.2
pkgrel=1
pkgdesc="The GBM EGL external platform library"
arch=('x86_64')
url="https://github.com/NVIDIA/egl-gbm"
license=('MIT')
depends=(
  'eglexternalplatform'
  'glibc'
  'mesa'
  'libdrm'
)
makedepends=(
  'meson'
)
provides=('libnvidia-egl-gbm.so=1')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('40c8a1a3df0639ea83031a92e9adcbef92ed696445307aa23cfc5895e63d11f0')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgname-$pkgver"
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
