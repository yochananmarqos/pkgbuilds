# https://aur.archlinux.org/pkgbase/openscq30

pkgname=(
  'openscq30-cli'
  'openscq30-gui'
)
pkgbase=openscq30
_app_id=com.oppzippy.OpenSCQ30
pkgver=2.4.0
pkgrel=1
pkgdesc="Cross platform application for controlling settings of Soundcore headphones"
arch=('x86_64' 'aarch64')
url="https://github.com/Oppzippy/OpenSCQ30"
license=('GPL-3.0-or-later')
depends=(
  'cosmic-icon-theme'
  'dbus'
  'gcc-libs'
  'gtk4'
  'libadwaita'
  'libxkbcommon'
  'sqlite'
  'xdg-utils'
)
makedepends=(
  'cargo'
  'just'
)
checkdepends=('xorg-server-xvfb')
source=("OpenSCQ30-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('cfdfbdebaeb0e3095cef6619f2569e6c7465cd1df7db8dc5c633bdd2bb4816b5')

prepare() {
	cd "OpenSCQ30-$pkgver"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --target "$(rustc --print host-tuple)"
}

build() {
	cd "OpenSCQ30-$pkgver"
	export RUSTUP_TOOLCHAIN=stable
  just build-cli
  just build-gui
}

check() {
	cd "OpenSCQ30-$pkgver"
	export RUSTUP_TOOLCHAIN=stable
  just lib/ test
  just cli/ test || : ## 36 passed; 5 failed
  xvfb-run --auto-servernum just gui/ test

  appstreamcli validate --no-net "gui/resources/${_app_id}.metainfo.xml"
  desktop-file-validate "gui/resources/${_app_id}.desktop"
}

package_openscq30-cli() {
	pkgdesc+=" - CLI application"
	depends=(
	  'dbus'
	  'gcc-libs'
	)

	cd "OpenSCQ30-$pkgver/"
  just cli/ install "$pkgdir/usr"
}

package_openscq30-gui() {
	pkgdesc+=" - GUI application"

	cd "OpenSCQ30-$pkgver"
	just gui/ install "$pkgdir/usr"
}
