pkgname=pegasus-frontend-git
pkgver=alpha13.r37.g67846c6
pkgrel=1
pkgdesc="A cross platform, customizable graphical frontend for launching emulators and managing your game collection."
arch=('i686' 'x86_64')
url="https://pegasus-frontend.org"
license=('GPL3')
depends=('qt-gstreamer' 'qt5-svg' 'qt5-multimedia' 'qt5-gamepad' 'qt5-graphicaleffects'
         'fontconfig' 'openssl-1.0')
makedepends=('git' 'qt5-tools'
)
optdepends=('polkit: for shutting down/restarting the device without admin rights')
provides=("${pkgname%-git}" 'pegasus-fe')
conflicts=("${pkgname%-git}" 'pegasus-fe')
source=("git+https://github.com/mmatyas/${pkgname%-git}.git"
        "git+https://github.com/mmatyas/${pkgname%-git}-translations.git"
        'git+https://github.com/mmatyas/pegasus-theme-grid.git'
        'git+https://github.com/mmatyas/SortFilterProxyModel.git'
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git submodule init
	git config submodule.lang.url "$srcdir/pegasus-frontend-translations"
	git config submodule.src/themes/pegasus-theme-grid.url "$srcdir/pegasus-theme-grid"
	git config submodule.thirdparty/SortFilterProxyModel.url "$srcdir/SortFilterProxyModel"
	git submodule update
}

build() {
	cd "$srcdir/${pkgname%-git}"
	mkdir -p ./build && cd build
	qmake .. \
		INSTALL_BINDIR=/usr/bin \
		INSTALL_ICONDIR=/usr/share/pixmaps \
		INSTALL_DESKTOPDIR=/usr/share/applications
	make
}

package() {
	cd "$srcdir/${pkgname%-git}/build"
	make INSTALL_ROOT="$pkgdir/" install
}
