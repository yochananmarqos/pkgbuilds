# https://aur.archlinux.org/packages/libbacktrace-git
groups=('modified')

pkgname=libbacktrace-git
pkgver=r77.9ae4f4a
pkgrel=1
pkgdesc="Library to produce symbolic backtraces"
arch=('x86_64')
url="https://github.com/ianlancetaylor/libbacktrace"
license=('BSD')
depends=('gcc-libs')
makedepends=('git' 'libunwind')
provides=("${pkgname%-git}" "${pkgname%-git}.so")
conflicts=("${pkgname%-git}")
options=('lto' 'staticlibs')
source=("git+https://github.com/ianlancetaylor/libbacktrace.git"
        '0001-Provide-project-information-to-autotools.patch')
sha256sums=('SKIP'
            '93c2e6238ceec2cf32ae8a30ea39e5fc5556c189a75990f0d1270450f3d53c22')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  patch -Np1 -i "$srcdir/0001-Provide-project-information-to-autotools.patch"
}

build() {
  cd "$srcdir/${pkgname%-git}"

  # prevent static lib mangling
  export CFLAGS+=" -ffat-lto-objects"

  autoreconf -fi
  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --with-system-libunwind
  make
}

#check() {
#  cd "$srcdir/${pkgname%-git}"
#  make check
#}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install

  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/${pkgname%-git}/"
}
