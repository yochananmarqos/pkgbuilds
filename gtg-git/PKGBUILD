# https://aur.archlinux.org/packages/gtg-git
pkgname=gtg-git
pkgver=0.4.r0.gb381aa03
pkgrel=1
pkgdesc="Personal GTD like organizer for the GNOME desktop environment."
url="https://wiki.gnome.org/Apps/GTG"
arch=('x86_64')
license=('GPL')
depends=('python-liblarch')
makedepends=('git' 'meson')
optdepends=('python-cheetah3: for the Export and print plugin'
            'texlive-bin: for the Export and print plugin'
            'texlive-core: for the Export and print plugin'
            'pdftk: for Export and print plugin')
checkdepends=('python-nose' 'python-mock')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/getting-things-gnome/gtg.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" build
	meson compile -C build
}

check() {
	cd "$srcdir/${pkgname%-git}"
	python run-tests
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}

