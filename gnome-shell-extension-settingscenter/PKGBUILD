pkgname=gnome-shell-extension-settingscenter
_uuid=SettingsCenter@lauinger-clan.de
pkgver=47.4
pkgrel=1
pkgdesc="A customizable drop-down menu for quickly launching frequently used apps in Gnome Shell via the quicksettings menu."
arch=('any')
url="https://github.com/ChrisLauinger77/gnome-shell-extension-SettingsCenter"
license=('GPL-2.0-or-later')
depends=('gnome-shell')
makedepends=('git')
source=("git+$url.git#tag=v$pkgver")
sha256sums=('5b980fa13b3bfd522551d74e5e20e3ef8af534aee05f38ea8e8d3eb1b7b5acf2')

build() {
  cd gnome-shell-extension-SettingsCenter
  gnome-extensions pack "${_uuid}" \
    --podir=../po/ \
    --extra-source=menu_items.js \
    --extra-source=translation.js \
    --force
}

package() {
  cd gnome-shell-extension-SettingsCenter
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "${pkgdir}/usr/share/"

  install -Dm644 "${_uuid}/schemas/org.gnome.shell.extensions.SettingsCenter.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas"
}
