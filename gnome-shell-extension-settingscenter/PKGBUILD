pkgname=gnome-shell-extension-settingscenter
_uuid=SettingsCenter@lauinger-clan.de
pkgver=49.3
pkgrel=1
pkgdesc="A customizable drop-down menu for quickly launching frequently used apps in GNOME Shell via the Quick Settings menu."
arch=('any')
url="https://github.com/ChrisLauinger77/gnome-shell-extension-SettingsCenter"
license=('GPL-3.0-or-later')
depends=('gnome-shell')
makedepends=('git')
source=("git+https://github.com/ChrisLauinger77/gnome-shell-extension-SettingsCenter.git#tag=v$pkgver")
sha256sums=('b0f4e2bce912b888ec97126c2eb93cac0056be10b9665f86638639342cab9060')

build() {
  cd gnome-shell-extension-SettingsCenter
  gnome-extensions pack "${_uuid}" \
    --podir=../po/ \
    --extra-source=lib/ \
    --extra-source=ui/ \
    --extra-source=menu_items.js \
    --force
}

package() {
  cd gnome-shell-extension-SettingsCenter
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "${pkgdir}/usr/share/"

  install -Dm644 "${_uuid}/schemas/org.gnome.shell.extensions.SettingsCenter.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas"
}
