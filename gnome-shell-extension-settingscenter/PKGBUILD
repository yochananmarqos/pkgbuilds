pkgname=gnome-shell-extension-settingscenter
_uuid=SettingsCenter@lauinger-clan.de
pkgver=47.0
pkgrel=1
pkgdesc="A customizable drop-down menu for quickly launching frequently used apps"
arch=('any')
url="https://github.com/ChrisLauinger77/XES-Settings-Center-Extension"
license=('GPL2')
depends=('gnome-shell')
makedepends=('git')
source=("git+$url.git#tag=v$pkgver")
sha256sums=('f919008287cb55358904c0d1989335ff6bf7ffe6e7056caccbebe84ed04a4fd9')

build() {
  cd XES-Settings-Center-Extension
  gnome-extensions pack "${_uuid}" \
    --podir=../po/ \
    --extra-source=menu_items.js \
    --extra-source=translation.js \
    --force
}

package() {
  cd XES-Settings-Center-Extension
  install -d "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"
  bsdtar xvf "${_uuid}.shell-extension.zip" -C \
    "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/" --no-same-owner

  mv "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/locale" "${pkgdir}/usr/share/"

  install -Dm644 "${_uuid}/schemas/org.gnome.shell.extensions.SettingsCenter.gschema.xml" -t \
    "$pkgdir/usr/share/glib-2.0/schemas/"
  rm -rf "$pkgdir/usr/share/gnome-shell/extensions/${_uuid}/schemas"
}
