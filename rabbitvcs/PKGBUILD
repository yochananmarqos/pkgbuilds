# https://aur.archlinux.org/packages/rabbitvcs
# https://aur.archlinux.org/packages/rabbitvcs-cli/
# https://aur.archlinux.org/packages/rabbitvcs-caja/
# https://aur.archlinux.org/packages/rabbitvcs-nautilus/
# https://aur.archlinux.org/packages/rabbitvcs-nemo/
# https://aur.archlinux.org/packages/rabbitvcs-thunar/

## Gedit plugin not working with Gedit later than version 3.12

## WIP


# Migrate to Gtk4
# https://github.com/rabbitvcs/rabbitvcs/pull/360

pkgname=('rabbitvcs'
         'rabbitvcs-cli'
         'rabbitvcs-caja'
         'rabbitvcs-dolphin'
#         'rabbitvcs-gedit'
         'rabbitvcs-nautilus'
         'rabbitvcs-nemo'
         'rabbitvcs-pcmanfm-qt'
         'rabbitvcs-pluma'
         'rabbitvcs-thunar'
         )
pkgbase=rabbitvcs
pkgver=0.19+30+g0b4e201
pkgrel=1
pkgdesc="A set of graphical tools written to provide simple and straightforward access to the version control systems you use"
arch=('any')
url="http://wiki.rabbitvcs.org/wiki"
license=('GPL-2.0-or-later')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
_commit=0b4e201d669ff137610c6f1608589d56604b66b5
source=("git+https://github.com/rabbitvcs/rabbitvcs.git#commit=${_commit}")
sha256sums=('24383b79518a4ef8ffcba9f39ee1cde7371c3724d00eb54a04358ea4ac31181c')

pkgver() {
  cd "$pkgbase"
  git describe --tags --abbrev=7 | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$pkgbase"
}

build() {
  cd "$pkgbase"
  python -m build --wheel --no-isolation
}

package_rabbitvcs(){
  depends=(
    'git'
    'gtk3'
    'meld'
    'python-configobj'
    'python-dulwich'
    'python-gobject'
    'python-pysvn'
    'python-simplejson'
    'tk'
  )
  optdepends=(
    'hunspell: For spell checking of commit messages'
    'python-gtkspellcheck: For spell checking of commit messages'
    'python-pygments: For syntax highlighting'
  )

  cd "$pkgbase"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

package_rabbitvcs-cli() {
  pkgdesc="A command line client for RabbitVCS"
  depends=('rabbitvcs')

  cd "$pkgbase"
  install -Dm755 "clients/cli/$pkgbase" -t "$pkgdir/usr/bin"
}

package_rabbitvcs-caja() {
  pkgdesc="Caja extension for RabbitVCS"
  depends=(
    'python-caja'
    'python-dbus'
    'rabbitvcs'
  )
  install="$pkgname.install"

  cd "$pkgbase"
  install -Dm644 clients/caja/RabbitVCS.py -t \
    "$pkgdir/usr/share/caja-python/extensions/"
}

package_rabbitvcs-dolphin() {
  pkgdesc="Dolphin integration for RabbitVCS"
  depends=(
    'dolphin'
    'rabbitvcs-cli'
  )

  cd "$pkgbase"
  install -Dm644 clients/dolphin/*.desktop -t \
    "$pkgdir/usr/share/kio/servicemenus/"
}

package_rabbitvcs-gedit() {
  pkgdesc="Gedit plugin for RabbitVCS"
  depends=(
    'gedit'
    'rabbitvcs'
  )

  cd "$pkgbase"
  install -Dm644 "clients/gedit/$pkgbase"{-gedit3.plugin,-plugin.py} -t \
    "$pkgdir/usr/lib/gedit/plugins/"
}

package_rabbitvcs-nautilus() {
  pkgdesc="Nautilus extension for RabbitVCS"
  depends=(
    'nautilus-python'
    'python-dbus'
    'rabbitvcs'
  )
  install="$pkgname.install"

  cd "$pkgbase"
  install -Dm644 clients/nautilus/RabbitVCS.py -t \
    "$pkgdir/usr/share/nautilus-python/extensions/"
}

package_rabbitvcs-nemo() {
  pkgdesc="Nemo extension for RabbitVCS"
  depends=(
    'nemo-python'
    'python-dbus'
    'rabbitvcs'
  )
  install="$pkgname.install"

  cd "$pkgbase"
  install -Dm644 clients/nemo/RabbitVCS.py -t \
    "$pkgdir/usr/share/nemo-python/extensions/"
}

package_rabbitvcs-pcmanfm-qt() {
  pkgdesc="PCManFM-Qt extension for RabbitVCS"
  depends=(
    'pcmanfm-qt'
    'rabbitvcs'
  )

  cd "$pkgbase"
  install -Dm644 clients/pcmanfm-qt/*.desktop -t \
    "$pkgdir/usr/share/file-manager/actions/"
}

package_rabbitvcs-pluma() {
  pkgdesc="Pluma plugin for RabbitVCS"
  depends=(
    'pluma'
    'rabbitvcs'
  )

  cd "$pkgbase"
  install -Dm644 "clients/pluma/$pkgbase"{-plugin.py,-pluma.plugin} -t \
    "$pkgdir/usr/lib/pluma/plugins/"
}

package_rabbitvcs-thunar() {
  pkgdesc="Thunar extension for RabbitVCS"
  depends=(
    'python-dbus'
    'rabbitvcs'
    'thunarx-python'
  )
  install="$pkgname.install"

  cd "$pkgbase"
  install -Dm644 clients/thunar/RabbitVCS.py -t \
    "$pkgdir/usr/share/thunarx-python/extensions/"
}
