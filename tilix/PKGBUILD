# https://archlinux.org/packages/community/x86_64/tilix
groups=('modified')

pkgname=tilix
pkgver=1.9.3
pkgrel=11.1
pkgdesc="A tiling terminal emulator for Linux using GTK+ 3"
arch=('x86_64')
url="https://github.com/gnunn1/tilix"
license=('MPL')
depends=('libx11' 'gtkd' 'vte3' 'dconf' 'gsettings-desktop-schemas')
makedepends=('ldc' 'po4a')
optdepends=('python-nautilus: for "Open Tilix Here" support in nautilus'
            'libsecret: for the password manager')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "Avoid_calling_values_on_a_shared_object.patch::https://github.com/gnunn1/tilix/commit/ce11b0bd936fe8fee9c5ac7025ea5864b99ed7d3.patch"
        'https://github.com/gnunn1/tilix/pull/1904.patch')
sha256sums=('f1b4dbbd60f4b9a91e0ab0954e3e6b8ebe5442d5fbb760a554a733903e44a1de'
            '29bbdb44fa026af00d82230376a0c18e4947d7c7cfbe1838a6cbb81733f6ba0a'
            '4454e93ca99e54ce6a86a77dd02135a51711b7a96f10e3389954068bc78566cc')

prepare() {
    cd "$pkgname-$pkgver"

    # Fixes compilation: https://github.com/gnunn1/tilix/commit/ce11b0bd936fe8fee9c5ac7025ea5864b99ed7d3
    patch -p1 < "$srcdir"/Avoid_calling_values_on_a_shared_object.patch

    # Added an option to strip trailing whitespace on paste
    git apply -3 "$srcdir"/1904.patch

    ./autogen.sh
}

build() {
    cd "$pkgname-$pkgver"

    ./configure \
    --prefix=/usr \
    PO4A_TRANS=/usr/bin/vendor_perl/po4a-translate \
    DC='ldmd' \
    DCFLAGS='-O -inline -release -version=StdLoggerDisableTrace -L='"\"$LDFLAGS\""
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}

