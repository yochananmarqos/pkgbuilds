# https://aur.archlinux.org/packages/emote
groups=('modified')

pkgname=emote
pkgver=4.0.1
pkgrel=2
pkgdesc="Emoji Picker for Linux written in GTK3"
arch=('any')
url="https://github.com/tom-james-watson/Emote"
license=('GPL3')
depends=(
  'gtk3'
  'hicolor-icon-theme'
  'libkeybinder3'
  'python-dbus'
  'python-gobject'
  'python-manimpango' 
  'python-setproctitle'
  'xdotool'
)
makedepends=('meson')
optdepends=('wl-clipboard: Clipboard access on Wayland')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('29814ec5dc5cef61f99a8c651fccefb8de7a034325a5b97a921f4b852509d3ca')

build() {
  arch-meson "Emote-$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
