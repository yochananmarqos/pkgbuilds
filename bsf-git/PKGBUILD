# https://aur.archlinux.org/packages/bsf-git
pkgname=('bsf-git' 'bsf-docs-git')
pkgbase=bsf-git
pkgver=1.1.0.r555.g41e122d24
pkgrel=1
pkgdesc="A C++ library that aims to provide a unified foundation for the development of
         real-time graphical applications, whether games, engines or tools"
arch=('x86_64')
url="https://www.bsframework.io"
license=('MIT')
makedepends=('git' 'cmake' 'doxygen' 'ninja')
source=('git+https://github.com/GameFoundry/bsf.git'
        'install-dir.patch'
        'https://data.banshee3d.com/bsfCompiledData_Master_35.zip'
        'https://data.banshee3d.com/bsfData_Master_7.zip'
        'https://data.banshee3d.com/bsfDocImagesData_Master_2.zip'
        'https://data.banshee3d.com/bsfDependencies_Linux_Master_11.zip'
        'https://data.banshee3d.com/bsfDep_XShaderCompiler_Linux_Master_6.zip'
        'https://data.banshee3d.com/SemanticUI.zip')
sha256sums=('SKIP'
            '51ed2032f8a100197edb915c73c9ea395cce0e119cdafe5235bd446b67721cb3'
            '848973eef189b16b00947655d2895d0195f4078159884d585653390ca781d125'
            '212ec9fd67be4f58278de6d35ea8dc6f350009cbc741202b957a8b0337da84b3'
            'a1f61551db50ddd48de1e2b67cc9a710578d5bf26288ae4535a0cebeb75d6588'
            '2c8726abb0f6ec35d00bf7d30a75a3b74de1820672db177799898d8cbb359426'
            '41ff32953fceeb1166ac1ec8ae3ab49d3a55acfa6de993d27862d80a9fbf701b'
            'd827f244a0dc9a41e012ebe165c3af2bce1da583e107635acd51012cec303121')

pkgver() {
	cd "$srcdir/${pkgbase%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


prepare() {
	cp -r Data Dependencies Documentation bsf
	cp -r Raw bsf/Data
	cp -r XShaderCompiler bsf/Dependencies

	cd "$srcdir/${pkgbase%-git}"
	rm -rf Dependencies/{OpenAL,lib*,free*}

	sed -i 's|local/opt|share|g' CMakeLists.txt

	mkdir -p build
	mkdir -p Documentation/Generated/{native,csharp}
}

build() {
	cd "$srcdir/${pkgbase%-git}/build"
	cmake ..\
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_BUNDLED_LIBRARIES=OFF \
		-G Ninja
	ninja

	cd ../Documentation/Doxygen
	doxygen native.doxyconfig
	doxygen csharp.doxyconfig
}

package_bsf-git() {
	depends=('icu' 'libxcursor' 'libxrandr' 'libxi' 'libgl' 'openal' 'flac' 'libvorbis'
             'snappy' 'freeimage' 'freetype2')
	provides=("${pkgbase%-git}")
	conflicts=("${pkgbase%-git}")

	cd "$srcdir/${pkgbase%-git}"
	DESTDIR="$pkgdir" ninja -C build install

	find "$pkgdir"/usr/lib -name "*.dbg" -delete

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/${pkgbase%-git}"
}

package_bsf-docs-git() {
	arch=('any')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/${pkgbase%-git}"
	install -dm644 "$pkgdir/usr/share/doc/${pkgbase%-git}"
	cp -r Documentation/Generated/{native,csharp}/* "$pkgdir/usr/share/doc/${pkgbase%-git}/"
	cp -r "$srcdir/SemanticUI" "$pkgdir/usr/share/doc/${pkgbase%-git}/native/"
	cp -r "$srcdir/SemanticUI" "$pkgdir/usr/share/doc/${pkgbase%-git}/csharp/"
}
