# https://aur.archlinux.org/packages/solaar-git
pkgname=solaar-git
pkgver=1.0.2.r0.g4a108b3
pkgrel=1
epoch=1
pkgdesc="Device manager for Logitech's Unifying receiver peripherals"
url="https://pwr-solaar.github.io/Solaar"
license=('GPL2')
arch=('any')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
depends=('libappindicator-gtk3' 'libnotify' 'python-dbus' 'python-gobject'
         'python-pyudev')
makedepends=('git')
source=("${pkgname%-git}::git+https://github.com/pwr-Solaar/Solaar.git")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --skip-build \
		--optimize=1 \
		--prefix=/usr \
		--root="$pkgdir/"
	# udev
	rm -rf "$pkgdir/etc"
	install -vDm 644 rules.d/42-logitech-unify-permissions.rules \
		"$pkgdir/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
	# docs
	install -vDm 644 {ChangeLog,README.md} \
		-t "$pkgdir/usr/share/doc/${pkgname%-git}/"
}
