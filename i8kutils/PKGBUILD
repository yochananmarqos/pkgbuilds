# http://aur.archlinux.org/packages/i8kutils
groups=('modified')

pkgname=i8kutils
pkgver=1.55
pkgrel=1
pkgdesc="Fan control for some Dell laptops"
arch=('any')
url="https://launchpad.net/i8kutils"
license=('GPL3')
depends=('tcl' 'tcllib' 'systemd')
makedepends=('meson')
optdepends=('acpi: For enabling i8kmon to read the battery status')
backup=("etc/$pkgname/i8kmon.conf")
source=("$pkgname-$pkgver.tar.gz::https://github.com/Wer-Wolf/i8kutils/archive/refs/tags/v$pkgver.tar.gz"
        'dell-smm-hwmon.conf')
sha256sums=('bc37b2f51a98fb6607a364390ced5688bb817b8d17d182428d44c5bf3ce3f8d9'
            '2c447f92ac5245fcd6ecec7e69622b0e31b790e08bcbf853e9f7a7bc631fea07')

prepare() {
  cd "$pkgname-$pkgver"

  # conform to Arch Linux guidelines
  sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' man/i8kmon.1
  sed -i "s|install_data('i8kmon.conf', install_dir : '/etc'|install_data('i8kmon.conf', install_dir : '/etc/i8kutils'|g" \
    etc/meson.build
}

build() {
  arch-meson "$pkgname-$pkgver" build -Dmoduledir='/usr/lib/tcl8.6'
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 dell-smm-hwmon.conf -t "$pkgdir/etc/modules-load.d/"
}
