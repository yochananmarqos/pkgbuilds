# https://aur.archlinux.org/packages/noisetorch-git
pkgname=noisetorch-git
pkgver=0.6.1.beta.r0.g371b3e3
pkgrel=1
pkgdesc='Real-time microphone noise suppression on Linux.'
arch=('x86_64')
url="https://github.com/lawl/NoiseTorch"
license=('GPL3')
depends=('pulseaudio' 'polkit' 'hicolor-icon-theme')
makedepends=('git' 'go' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/lawl/NoiseTorch.git"
        'git+https://github.com/werman/noise-suppression-for-voice.git'
        'main.patch')
sha256sums=('SKIP'
            'SKIP'
            '04bc943436caab9eb664531f6527017597e40ccf07dc7e6422e3b6c0ad0905e1')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git submodule init
	git config submodule.librnnoise_ladspa.url \
		"$srcdir/noise-suppression-for-voice"
	git submodule update

	patch -u main.go ../main.patch

	export GOPATH="$srcdir/gopath"
	go clean -modcache
}

build() {
	cd "$srcdir/${pkgname%-git}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	make

	# Clean mod cache for makepkg -C
	go clean -modcache
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm755 "bin/${pkgname%-git}" -t "$pkgdir/usr/bin"

	sed -i 's/noisetorch.png/noisetorch/g' "assets/${pkgname%-git}.desktop"
	install -Dm644 "assets/${pkgname%-git}.desktop" -t \
		"$pkgdir/usr/share/applications"

	install -Dm644 "assets/icon/${pkgname%-git}.png" -t \
		"$pkgdir/usr/share/icons/hicolor/256x256/apps"
}
