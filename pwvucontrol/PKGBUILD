# https://aur.archlinux.org/packages/pwvucontrol

pkgname=pwvucontrol
pkgver=0.5.0
pkgrel=1
pkgdesc="Pipewire Volume Control"
arch=('x86_64')
url="https://github.com/saivert/pwvucontrol"
license=('GPL-3.0-only')
depends=(
  'glib2'
  'gtk4'
  'libadwaita'
  'libpipewire'
  'libwireplumber-4.0-compat'
  'wireplumber')
makedepends=(
  'cargo'
  'clang'
  'git'
  'meson'
)
checkdepends=('appstream-glib')
source=("git+https://github.com/saivert/pwvucontrol.git#tag=$pkgver")
b2sums=('d6acf78dc67f6b3053d669ff9fd05630d732f72903ef7022d724db5fae99253e13474ff1ba51341b80a78a362f3a7652a554af3c66fdbaecbb0c5c4c96aab1c7')

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "$pkgname" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
