# https://aur.archlinux.org/packages/gittyup
groups=('modified')

pkgname=gittyup
pkgver=1.1.2
pkgrel=1
pkgdesc="A graphical Git client designed to help you understand and manage your source code history."
arch=('x86_64')
url="https://murmele.github.io/Gittyup"
license=('MIT')
depends=('cmark' 'hunspell' 'libgit2' 'libsecret' 'lua' 'qt5-base')
makedepends=('cmake' 'git' 'qt5-tools')
optdepends=('git: needed for the credential helpers'
            'git-lfs: git LFS support')
_commit=7fa0bce2adb87c301180277363fea0f1264cd118
source=("git+https://github.com/Murmele/Gittyup.git#commit=$_commit"
        'git+https://github.com/commonmark/cmark.git'
        'git+https://github.com/git/git.git'
        'git+https://github.com/hunspell/hunspell'
        'git+https://github.com/stinb/libgit2.git'
        'git+https://github.com/libssh2/libssh2.git'
        'git+https://github.com/openssl/openssl.git'
        'git+https://github.com/ScintillaOrg/lexilla.git'
        'git+https://github.com/orbitalquark/scintillua.git'
        'git+https://github.com/kuba--/zip.git'
        )
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/Gittyup"
  git describe --tags | sed "s/^${pkgname}_v//;s/-/+/g"
}

prepare() {
  cd "$srcdir/Gittyup"
  git submodule init

  for submodule in cmark git hunspell lexilla libgit2 libssh2 openssl scintillua; do
    git config "submodule.dep/${submodule}/${submodule}.url" "$srcdir/${submodule}"
    git -c protocol.file.allow=always submodule update
  done

#  for submodule in cmark git hunspell libgit2 libssh2 openssl; do
#    git -c submodule.dep/${submodule}/${submodule}.update=none submodule update
#  done

  sed -i -e 's/cmark_exe/cmark/' src/app/CMakeLists.txt
}

build() {
  cmake -B build -S Gittyup \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBUILD_SHARED_LIBS='OFF' \
    -DENABLE_REPRODUCIBLE_BUILDS='ON' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  _app_id='com.github.Murmele.Gittyup'

  cd "$srcdir/Gittyup"
  install -Dm644 LICENSE.md -t  "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "rsrc/linux/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "rsrc/linux/${_app_id}.appdata.xml.in" \
    "$pkgdir/usr/share/appdata/${_app_id}.appdata.xml"

  for s in 16x16 32x32 64x64 128x128 256x256 512x512; do
    install -Dm644 rsrc/Gittyup.iconset/icon_${s}.png \
      "$pkgdir/usr/share/icons/hicolor/${s}/apps/$pkgname.png"
  done

  install -d "$pkgdir/usr/"{share/${pkgname},bin}
  mv "$pkgdir/usr/"{git-credential-libsecret,Gittyup,indexer,relauncher} \
    "$pkgdir/usr/share/$pkgname/"
  mv "$pkgdir"/usr/{Plugins,Resources} "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/Gittyup" "$pkgdir/usr/bin/"

  # Remove shared files
  rm -f "$pkgdir/usr/"*.so.*
  rm -rf "$pkgdir/usr/"{include,lib}

  # Remove libssh2 docs & man pages
  rm -rf "$pkgdir"/usr/share/{doc,man}
}
