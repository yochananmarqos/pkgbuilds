# https://aur.archlinux.org/packages/gittyup
groups=('modified')

pkgname=gittyup
pkgver=1.2.1
pkgrel=1
pkgdesc="A graphical Git client designed to help you understand and manage your source code history."
arch=('x86_64')
url="https://murmele.github.io/Gittyup"
license=('MIT')
depends=('cmark' 'hicolor-icon-theme' 'hunspell' 'libgit2' 'libsecret' 'libzip'
         'lua' 'pcre' 'qt5-base')
makedepends=('cmake' 'git' 'libgnome-keyring' 'qt5-tools')
optdepends=('git: needed for the credential helpers'
            'git-lfs: git LFS support'
            'libgnome-keyring: for GNOME Keyring for auth credentials')
options=('lto')
_commit=f364ff0a333dbb8143f310903b5f098a3a804a58  # tags/gittyup_v1.2.1^0
source=("git+https://github.com/Murmele/Gittyup.git#commit=$_commit"
        'git+https://github.com/commonmark/cmark.git'
        'git+https://github.com/git/git.git'
        'git+https://github.com/hunspell/hunspell'
        'git+https://github.com/stinb/libgit2.git'
        'git+https://github.com/libssh2/libssh2.git'
        'git+https://github.com/openssl/openssl.git'
        'git+https://github.com/ScintillaOrg/lexilla.git'
        'git+https://github.com/orbitalquark/scintillua.git'
        'git+https://github.com/kuba--/zip.git'
        )
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/Gittyup"
  git describe --tags --exclude development | sed "s/^${pkgname}_v//;s/-/+/g"
}

prepare() {
  cd "$srcdir/Gittyup"
  git submodule init

  for submodule in cmark git hunspell libssh2 openssl; do
    git config "submodule.dep/${submodule}/${submodule}.update" none
  done

  for submodule in lexilla libgit2 scintillua zip; do
    git config "submodule.dep/${submodule}/${submodule}.url" "$srcdir/${submodule}"
  done

  git -c protocol.file.allow=always submodule update
}

build() {

  # prevent static lib mangling with LTO
  CFLAGS+=" -ffat-lto-objects"

  # Build fails with `-DUSE_SYSTEM_LIBGIT2='ON'`

  cmake -B build -S Gittyup \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATADIR='share' \
    -DENABLE_REPRODUCIBLE_BUILDS='ON' \
    -DBUILD_SHARED_LIBS='OFF' \
    -DDEBUG_OUTPUT='OFF' \
    -DUSE_SYSTEM_CMARK='ON' \
    -DUSE_SYSTEM_GIT='ON' \
    -DUSE_SYSTEM_HUNSPELL='ON' \
    -DUSE_SYSTEM_LIBSSH2='ON' \
    -DUSE_SYSTEM_LUA='ON' \
    -DUSE_SYSTEM_OPENSSL='ON' \
    -DUSE_SYSTEM_QT='ON' \
    -DLUA_MODULES_PATH='/usr/lib/lua/5.4' \
    -DGENERATE_APPDATA='ON' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  _app_id='com.github.Murmele.Gittyup'

  cd "$srcdir/Gittyup"
  mv "$pkgdir/usr/"{Gittyup,indexer,relauncher} \
    "$pkgdir/usr/share/$pkgname/"
  rm -rf "$pkgdir"/usr/Resources

  install -d "$pkgdir/usr/bin"
  ln -s "/usr/share/$pkgname/Gittyup" "$pkgdir/usr/bin/"
  
  install -Dm644 "rsrc/linux/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "rsrc/linux/${_app_id}.appdata.xml.in" \
    "$pkgdir/usr/share/appdata/${_app_id}.appdata.xml"

  for s in 16x16 32x32 64x64 128x128 256x256 512x512; do
    install -Dm644 rsrc/Gittyup.iconset/icon_${s}.png \
      "$pkgdir/usr/share/icons/hicolor/${s}/apps/$pkgname.png"
    install -Dm644 rsrc/Gittyup.iconset/icon_${s}@2x.png \
      "$pkgdir/usr/share/icons/hicolor/${s}@2x/apps/$pkgname.png"
  done

  install -Dm644 "rsrc/Gittyup.iconset/${pkgname}_logo.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"

  install -Dm644 LICENSE.md -t  "$pkgdir/usr/share/licenses/$pkgname/"

  # Remove shared files
  rm -f "$pkgdir/usr/"*.so.*
  rm -rf "$pkgdir/usr/"{include,lib}
}
