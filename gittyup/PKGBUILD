# https://aur.archlinux.org/packages/gittyup

pkgname=gittyup
_app_id=com.github.Murmele.Gittyup
pkgver=1.4.0
pkgrel=1
pkgdesc="A graphical Git client designed to help you understand and manage your source code history."
arch=('x86_64' 'aarch64')
url="https://murmele.github.io/Gittyup"
license=('MIT')
depends=(
  'cmark'
  'git'
  'hicolor-icon-theme'
  'hunspell'
  'libgit2'
  'libsecret'
  'libssh2'
  'libzip'
  'lua'
  'openssl'
  'pcre'
  'qt5-base'
)
makedepends=(
  'cmake'
  'git'
  'libgnome-keyring'
  'qt5-tools'
)
checkdepends=(
  'appstream'
  'desktop-file-utils'
)
optdepends=(
  'git-lfs: git LFS support'
  'libgnome-keyring: for GNOME Keyring for auth credentials'
)
source=("git+https://github.com/Murmele/Gittyup.git#tag=${pkgname}_v$pkgver"
        'git+https://github.com/stinb/libgit2.git'
        'git+https://github.com/libssh2/libssh2.git'
        'git+https://github.com/openssl/openssl.git'
        'git+https://github.com/git/git.git'
        'git+https://github.com/commonmark/cmark.git'
        'git+https://github.com/hunspell/hunspell.git'
        'git+https://github.com/kuba--/zip.git'
        'git+https://github.com/ScintillaOrg/lexilla.git'
        'git+https://github.com/orbitalquark/scintillua.git')
sha256sums=('06e1d79a3a3062c2ab37ad3a0a8f67d74d0c19210a9defac2b5e9e68e70feb1f'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd Gittyup

  # https://github.com/Murmele/Gittyup/issues/766
  git cherry-pick -n d36eba172a01d541945d59427b4f643aaed55da0

#  git submodule init

#  for submodule in cmark git hunspell libssh2 openssl; do
#    git config "submodule.dep/${submodule}/${submodule}.update" none
#  done

  git submodule init dep/libgit2/libgit2 test/dep/zip dep/scintilla/lexilla dep/scintilla/scintillua
  git config submodule.dep/libgit2/libgit2.url "$srcdir/libgit2"
  git config submodule.test/dep/zip.url "$srcdir/zip"
  git config submodule.dep/scintilla/lexilla.url "$srcdir/lexilla"
  git config submodule.dep/scintilla/scintillua.url "$srcdir/lexilla"
  git -c protocol.file.allow=always submodule update
}

build() {

  # Build fails with `-DUSE_SYSTEM_LIBGIT2='ON'`

  cmake -B build -S Gittyup \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATADIR='share' \
    -DBUILD_SHARED_LIBS='OFF' \
    -DUSE_SYSTEM_CMARK='ON' \
    -DUSE_SYSTEM_GIT='ON' \
    -DUSE_SYSTEM_HUNSPELL='ON' \
    -DUSE_SYSTEM_LIBSSH2='ON' \
    -DUSE_SYSTEM_LUA='ON' \
    -DUSE_SYSTEM_OPENSSL='ON' \
    -DUSE_SYSTEM_QT='ON' \
    -DLUA_MODULES_PATH='/usr/lib/lua/5.4' \
    -DGENERATE_APPDATA='ON' \
    -DENABLE_UPDATE_OVER_GUI='OFF' \
    -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure

  cd Gittyup
  appstreamcli validate --no-net "rsrc/linux/${_app_id}.appdata.xml.in" || :
  desktop-file-validate "rsrc/linux/${_app_id}.desktop"
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgdir/usr"
  rm -rf include lib
  mv bin/{indexer,relauncher} share/Gittyup
}
