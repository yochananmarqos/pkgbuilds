pkgname=plane-icon-theme-git
pkgver=r184.148b5f2
pkgrel=1
pkgdesc="A modern icon theme for GNOME Linux versions."
arch=('any')
url="https://github.com/wfpaisa/plane-icon-theme"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git' 'npm' 'gulp')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/wfpaisa/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install --cache "$srcdir/npm-cache" gulp-cli
	npm audit fix
	gulp
}

package() {
	cd "$srcdir/${pkgbase%-git}/build/variants"
	for i in $(ls -d *); do
		find "$i" -type f -exec install -D -m 644 '{}' "$pkgdir"/usr/share/icons/'{}' \; ;
	done
}
