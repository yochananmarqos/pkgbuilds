# http://aur.archlinux.org/packages/plane-icon-theme-git
pkgname=plane-icon-theme-git
pkgver=r191.3e94c25
pkgrel=1
pkgdesc="A simple and beautiful SVG perfect pixel iconset for GNOME made in Inkscape."
arch=('any')
url="https://www.pling.com/p/1178976"
license=('GPL3')
makedepends=('git' 'npm' 'gulp')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=(!strip)
source=("git+https://github.com/wfpaisa/plane-icon-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install --cache "$srcdir/npm-cache"
	gulp
}

package() {
	cd "$srcdir/${pkgname%-git}/build/variants"
	for i in $(ls -d *); do
		find "$i" -type f -exec install -D -m 644 '{}' \
			"$pkgdir"/usr/share/icons/'{}' \; ;
	done
}
