# https://aur.archlinux.org/packages/safeeyes-git
groups=('modified')

pkgname=safeeyes-git
_app_id=io.github.slgobinath.SafeEyes
pkgver=2.1.5.r18.g684d162
pkgrel=1
pkgdesc="A free and open source tool to reduce and prevent Repetitive Strain Injury (RSI)."
arch=('any')
url="https://slgobinath.github.io/SafeEyes"
license=('GPL3')
depends=('alsa-utils' 'dbus-python' 'gtk3' 'hicolor-icon-theme' 'libnotify'
         'python-babel' 'python-gobject' 'python-psutil' 'python-xlib' 'xorg-xprop')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('appstream-glib')
optdepends=('libayatana-indicator: show a tray icon in the notification area'
            'python-croniter: for Health Statistics plugin'
            'python-pyowm: for Weather plugin'
            'xprintidle: for Smart Pause plugin'
            'wlrctl: full screen detection on Wayland')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/slgobinath/SafeEyes.git'
        'git+https://github.com/safeeyes/safeeyes-plugins.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/SafeEyes"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/SafeEyes"
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir/SafeEyes"
  appstream-util validate-relax --nonet "${pkgname%-git}/platform/$_app_id.metainfo.xml"
  desktop-file-validate "${pkgname%-git}/platform/$_app_id.desktop"
}

package() {
  cd "$srcdir/SafeEyes"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "${pkgname%-git}/platform/$_app_id.metainfo.xml" -t \
    "$pkgdir/usr/share/metainfo/"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -r "${pkgdir}${site_packages}"/usr/* "$pkgdir/usr/"
  rm -rf "${pkgdir}${site_packages}/usr"

  # Remove duplicate icons and desktop file
  rm -rf "${pkgdir}${site_packages}/${pkgname%-git}/platform/"

  # Third-party plugins
  cp -r "$srcdir/${pkgname%-git}"-plugins/{weather,zoom} \
    "${pkgdir}${site_packages}/${pkgname%-git}/plugins/"
}
