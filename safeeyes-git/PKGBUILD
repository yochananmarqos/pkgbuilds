# https://aur.archlinux.org/packages/safeeyes-git

# https://wiki.archlinux.org/title/Arch_Build_System#Preserve_modified_packages
groups=('modified')

pkgname=safeeyes-git
pkgver=2.1.3.r11.g541f39f
pkgrel=2
pkgdesc="A free and open source tool to reduce and prevent Repetitive Strain Injury (RSI)."
arch=('any')
url="http://slgobinath.github.io/SafeEyes"
license=('GPL3')
depends=('alsa-utils' 'hicolor-icon-theme' 'libnotify' 'python-babel' 'dbus-python'
         'python-gobject' 'python-psutil' 'python-xlib' 'xorg-xprop')
makedepends=('python-setuptools' 'git')
optdepends=('libappindicator-gtk3: show a tray icon in the notification area'
            'python-croniter: for Health Statistics plugin'
            'python-pyowm: for Weather plugin'
            'xprintidle: for Smart Pause plugin'
            'wlrctl: full screen detection on Wayland')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/slgobinath/SafeEyes.git"
        'git+https://github.com/safeeyes/safeeyes-plugins.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python setup.py build
}

package() {
  cd "$srcdir/${pkgname%-git}"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  # Remove duplicate icons and desktop file.
  rm -rv "$pkgdir$site_packages/safeeyes/platform"

  # Third-party plugins
  cp -r "$srcdir/${pkgname%-git}"-plugins/{weather,zoom} \
    "$pkgdir$site_packages/${pkgname%-git}/plugins"
}
