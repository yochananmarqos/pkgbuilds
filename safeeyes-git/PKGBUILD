# https://aur.archlinux.org/packages/safeeyes-git
pkgname=safeeyes-git
pkgver=2.1.1.r0.g41464e4
pkgrel=1
pkgdesc="A free and open source tool to reduce and prevent Repetitive Strain Injury (RSI)."
arch=('any')
url="http://slgobinath.github.io/SafeEyes"
license=('GPL3')
depends=('alsa-utils'
         'hicolor-icon-theme'
         'libappindicator-gtk3'
         'python-babel'
         'python-dbus'
         'python-gobject'
         'python-psutil'
         'python-xlib'
         'xorg-xprop')
makedepends=('python-setuptools' 'git')
optdepends=('xprintidle: for Smart Pause plugin'
            'python-croniter: for Health Stats plugin'
            'python-pyowm: for weather plugin')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/slgobinath/SafeEyes.git"
        'git+https://github.com/safeeyes/safeeyes-plugins.git')
sha1sums=('SKIP'
          'SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	PYTHONUSERBASE="/usr" python setup.py install \
		--root="$pkgdir/" \
		--optimize=1 \
		--skip-build

	local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
	cp -r "$srcdir/${pkgname%-git}-plugins/weather" \
		"$pkgdir${site_packages}/${pkgname%-git}/plugins"
}
