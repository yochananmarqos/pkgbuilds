# https://aur.archlinux.org/packages/safeeyes-git

pkgname=safeeyes-git
_app_id=io.github.slgobinath.SafeEyes
pkgver=2.2.3.r35.g17bbcaa
pkgrel=1
pkgdesc="Protect your eyes from eye strain using this simple and beautiful, yet extensible break reminder"
arch=('any')
url="https://slgobinath.github.io/SafeEyes"
license=('GPL-3.0-or-later')
depends=(
  'alsa-utils'
  'gtk4'
  'hicolor-icon-theme'
  'libnotify'
  'python-babel'
  'python-dbus'
  'python-gobject'
  'python-packaging'
  'python-psutil'
  'python-xlib'
  'xorg-xprop'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('appstream')
optdepends=(
  'libayatana-indicator: show a tray icon in the notification area'
  'python-croniter: for Health Statistics plugin'
  'python-pyowm: for Weather plugin'
  'python-pywayland: for Smart Pause plugin (Wayland)'
  'swayidle: for Smart Pause plugin (Sway)'
  'xprintidle: for Smart Pause plugin (Xorg)'
  'wlrctl: full screen detection on Wayland'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/slgobinath/SafeEyes.git'
        'git+https://github.com/safeeyes/safeeyes-plugins.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd SafeEyes
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd SafeEyes
  python -m build --wheel --no-isolation
}

check() {
  cd SafeEyes
  appstreamcli validate --no-net "${pkgname%-git}/platform/${_app_id}.metainfo.xml" || :
  desktop-file-validate "${pkgname%-git}/platform/${_app_id}.desktop"
}

package() {
  cd SafeEyes
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "${pkgname%-git}/platform/${_app_id}.metainfo.xml" -t \
    "$pkgdir/usr/share/metainfo/"
  install -Dm644 "${pkgname%-git}/platform/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  mv "${pkgdir}${site_packages}/${pkgname%-git}/platform/icons" "$pkgdir/usr/share"

  # Remove duplicate icons and desktop file
  rm -rf "${pkgdir}${site_packages}/${pkgname%-git}/platform/"

  # Third-party plugins
  cp -r "$srcdir/${pkgname%-git}"-plugins/{system-notifications,weather,zoom} \
    "${pkgdir}${site_packages}/${pkgname%-git}/plugins/"
}
