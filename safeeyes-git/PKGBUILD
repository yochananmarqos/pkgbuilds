# https://aur.archlinux.org/packages/safeeyes-git

pkgname=safeeyes-git
_app_id=io.github.slgobinath.SafeEyes
pkgver=3.0.0b3.r1.gf964aa1
pkgrel=1
pkgdesc="Protect your eyes from eye strain using this simple and beautiful, yet extensible break reminder"
arch=('any')
url="https://slgobinath.github.io/SafeEyes"
license=('GPL-3.0-or-later')
depends=(
  'alsa-utils'
  'gtk4'
  'hicolor-icon-theme'
  'libnotify'
  'python-babel'
  'python-dbus'
  'python-gobject'
  'python-packaging'
  'python-psutil'
  'python-xlib'
  'xorg-xprop'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('appstream')
optdepends=(
  'gnome-shell-extension-appindicator: tray icon backend for GNOME'
  'libayatana-appindicator: tray icon backend for MATE'
  'python-croniter: for Health Statistics plugin'
  'python-pywayland: for Smart Pause plugin (Wayland)'
  'safeeyes-plugins: Third party plugins'
  'snixembed: tray icon backend for LXDE/MATE/Openbox/i3/other WMs'
  'swayidle: for Smart Pause plugin (Sway)'
  'wingpanel-community-indicators: tray icon backend for Pantheon'
  'wlrctl: full screen detection on Wayland'
  'xprintidle: for Smart Pause plugin (Xorg)'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/slgobinath/SafeEyes.git')
sha256sums=('SKIP')

pkgver() {
  cd SafeEyes
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd SafeEyes
  python -m build --wheel --no-isolation
}

check() {
  cd SafeEyes
  appstreamcli validate --no-net "${pkgname%-git}/platform/${_app_id}.metainfo.xml"
  desktop-file-validate "${pkgname%-git}/platform/${_app_id}.desktop"
}

package() {
  cd SafeEyes
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "${pkgname%-git}/platform/${_app_id}.metainfo.xml" -t \
    "$pkgdir/usr/share/metainfo/"
  install -Dm644 "${pkgname%-git}/platform/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  mv "${pkgdir}${site_packages}/${pkgname%-git}/platform/icons" "$pkgdir/usr/share"

  # Remove duplicate icons and desktop file
  rm -rf "${pkgdir}${site_packages}/${pkgname%-git}/platform/"
}
