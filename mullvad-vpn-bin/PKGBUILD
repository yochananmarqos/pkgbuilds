pkgname=mullvad-vpn-bin
_pkgname=mullvad-vpn
pkgver=2019.2
pkgrel=1
pkgdesc="VPN Client for Mullvad.net, a bitcoin-friendly VPN for anonymity and privacy"
url="https://www.mullvad.net"
arch=('x86_64')
license=('GPL3')
depends=('gconf' 'gtk3' 'libnotify' 'libappindicator-gtk2' 'libxtst' 'nss' 'libxss')
provides=("$_pkgname")
conflicts=("$_pkgname")
install=$pkgname.install
source=("https://github.com/mullvad/mullvadvpn-app/releases/download/$pkgver/MullvadVPN-${pkgver}_x86_64.rpm"{,.asc}
        "mullvad-daemon.service")
sha256sums=('4a09ae6000c20f73805026b0a57db658d3ef3b05e99e44f47625c04cb44c1387'
            'SKIP'
            '77b71d613ef09c6ffbcfa66a59f787246f64d4463843e648edc6fbbea18dd55e')
validpgpkeys=('A1198702FC3E0A09A9AE5B75D5A1D4F266DE8DDF')

package() {

    # Systemd service
    install -Dm644 mullvad-daemon.service "$pkgdir/usr/lib/systemd/system/mullvad-daemon.service"

    # Main files
    install -d "$pkgdir/opt/$_pkgname"
    cp -a "opt/Mullvad VPN/." "$pkgdir/opt/$_pkgname"

    # Desktop Entry
    install -Dm644 "usr/share/applications/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
    sed -i 's%opt/Mullvad VPN%usr/bin%g' $pkgdir/usr/share/applications/$_pkgname.desktop

    # CLI binary
    install -Dm755 "usr/bin/mullvad" "$pkgdir/usr/bin/mullvad"

    # Main binary
    chmod 755 "$pkgdir/opt/$_pkgname/$_pkgname"
    ln -s "/opt/$_pkgname/$_pkgname" "$pkgdir/usr/bin/$_pkgname"

    # Icons
    install -d "$pkgdir/usr/share/icons/hicolor"
    cp -a "usr/share/icons/hicolor/." "$pkgdir/usr/share/icons/hicolor/"

}
