# https://aur.archlinux.org/packages/python-notify2
pkgname=('python-notify2' 'python2-notify2')
pkgbase=python-notify2
_name=${pkgbase#python-}
pkgver=0.3.1
pkgrel=1
pkgdesc="A pure-Python reimplementation of notify-python, using python-dbus."
arch=('any')
url="https://bitbucket.org/takluyver/pynotify2"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('33fa108d50c42f3cd3407cc437518ad3f6225d1bb237011f16393c9dd3ce197d')

prepare() {
	cp -a "$_name-${pkgver}"{,-py2}
}

build() {
	cd "$_name-$pkgver"
	python setup.py build

	cd "$srcdir/$_name-$pkgver-py2"
	python2 setup.py build
}

package_python-notify2() {
	depends=('python-dbus')

	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-notify2() {
	depends=('python2-dbus')

	cd "$_name-$pkgver-py2"
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
