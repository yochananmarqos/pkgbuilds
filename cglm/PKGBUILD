# https://aur.archlinux.org/packages/cglm

pkgname=cglm
pkgver=0.9.6
pkgrel=1
pkgdesc="Highly Optimized 2D / 3D Graphics Math (glm) for C"
arch=('x86_64')
url="https://github.com/recp/cglm"
license=('MIT')
depends=('glibc')
makedepends=('meson')
provides=('libcglm.so=0')
source=("$pkgname-$pkgver.tar.gz::https://github.com/recp/cglm/archive/v$pkgver.tar.gz")
sha256sums=('be5e7d384561eb0fca59724a92b7fb44bf03e588a7eae5123a7d796002928184')

build() {
  arch-meson "$pkgname-$pkgver" build -Dbuild_tests=true
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  cd "$pkgname-$pkgver"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
