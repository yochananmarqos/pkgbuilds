# https://aur.archlinux.org/packages/cglm

pkgname=cglm
pkgver=0.9.5
pkgrel=1
pkgdesc="Highly Optimized 2D / 3D Graphics Math (glm) for C"
arch=('x86_64')
url="https://github.com/recp/cglm"
license=('MIT')
depends=('glibc')
makedepends=('meson')
provides=('libcglm.so=0')
source=("$pkgname-$pkgver.tar.gz::https://github.com/recp/cglm/archive/v$pkgver.tar.gz")
sha256sums=('1e40e87bc934ba8567ef84484d31d5d64ca084d3f0a7523ec67988bc94d76435')

build() {
  arch-meson "$pkgname-$pkgver" build -Dbuild_tests=true
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  cd "$pkgname-$pkgver"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
