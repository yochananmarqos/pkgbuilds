pkgname=lib32-gamemode
_pkgname=gamemode
pkgver=1.4
pkgrel=1
pkgdesc="A daemon/lib combo for Linux that allows games to request a set of optimisations be temporarily applied to the host OS."
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause License (Revised)')
depends=('polkit' 'gamemode' 'lib32-glibc' 'lib32-systemd')
makedepends=('meson' 'pkg-config' 'git')
provides=("$pkgname")
conflicts=("$pkgname")
source=("https://github.com/FeralInteractive/$_pkgname/releases/download/$pkgver/$_pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('e83f07dc91bbd27d1faadd69f7fda899810001e2a256be8061e36dee0e858f4a'
            'SKIP')
validpgpkeys=('5AB43EC644CE6A84C39289F5048EF7331E7B6424')

build() {
	cd "$_pkgname-$pkgver"
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	meson build  --prefix /usr \
		-Dwith-daemon=false -Dwith-examples=false -Dwith-systemd=false -Dwith-util=false --libdir lib32
	ninja -C build
}

package() {
	cd "$_pkgname-$pkgver"
	DESTDIR=$pkgdir ninja -C build install
	rm -rf $pkgdir/usr/include
	install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
