pkgname=lib32-gamemode
_pkgname=gamemode
pkgver=1.5
pkgrel=1
pkgdesc="A daemon/lib combo for Linux that allows games to request a set of optimisations be temporarily applied to the host OS."
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause License (Revised)')
depends=('polkit' 'gamemode' 'lib32-glibc' 'lib32-systemd' 'lib32-dbus')
makedepends=('meson' 'pkg-config' 'git')
source=("$url/releases/download/$pkgver/$_pkgname-$pkgver.tar.xz") #{,.asc})
sha256sums=('262389e0a625670c5f53f89d0c30a28cd5f9569a8e6b012dccf78b7fdcb98408')
            #'SKIP')
#validpgpkeys=('5AB43EC644CE6A84C39289F5048EF7331E7B6424')
              # Alex Smith <asmith@feralinteractive.com>

build() {
	cd "$_pkgname-$pkgver"
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	meson build  \
		--prefix /usr \
		--libdir lib32 \
		-Dwith-daemon=false \
		-Dwith-examples=false \
		-Dwith-systemd=false \
		-Dwith-util=false
	ninja -C build
}

package() {
	cd "$_pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install

	rm -rf "$pkgdir/usr/include"

	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
