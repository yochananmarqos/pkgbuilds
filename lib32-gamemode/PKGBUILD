pkgname=lib32-gamemode
_pkgname=${pkgname#lib32-}
pkgver=1.5.1
pkgrel=2
pkgdesc="Optimise Linux system performance on demand"
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('polkit' 'gamemode' 'lib32-glibc' 'lib32-systemd' 'lib32-dbus')
makedepends=('git' 'meson')
source=("$url/releases/download/$pkgver/$_pkgname-$pkgver.tar.xz")
sha256sums=('3a5ea5aafe1b7ec69ac2c054198e9aa6b6fd4dd8ee7a8bcfb71d0b0a40313101')

build() {
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	arch-meson "$_pkgname-$pkgver" build  \
		--libdir lib32 \
		-Dwith-daemon=false \
		-Dwith-examples=false \
		-Dwith-systemd=false \
		-Dwith-util=false
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	rm -rf "$pkgdir/usr/include"

	cd "$_pkgname-$pkgver"
	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
