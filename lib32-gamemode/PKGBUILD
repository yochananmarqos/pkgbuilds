# https://gitlab.manjaro.org/packages/multilib/lib32-gamemode
# http://aur.archlinux.org/packages/lib32-gamemode
pkgname=lib32-gamemode
_pkgname=${pkgname#lib32-}
pkgver=1.6
pkgrel=1
pkgdesc="Optimise Linux system performance on demand"
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('gamemode' 'lib32-glibc' 'lib32-dbus')
makedepends=('meson' 'pkg-config' 'lib32-systemd')
source=("$url/releases/download/$pkgver/$_pkgname-$pkgver.tar.xz")
sha256sums=('4fec5a0900e8f559b37cf1711ca7dcea7e1cebfe1237ce314001f65c1e346a5d')

build() {
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	arch-meson "$_pkgname-$pkgver" build  \
		--libdir lib32 \
		-Dwith-sd-bus-provider=no-daemon \
		-Dwith-examples=false \
		-Dwith-systemd=false \
		-Dwith-util=false
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	rm -rf "$pkgdir/usr/include"

	cd "$_pkgname-$pkgver"
	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
