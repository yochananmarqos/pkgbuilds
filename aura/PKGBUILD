# https://aur.archlinux.org/packages/aura
pkgname=aura
pkgver=3.1.0
pkgrel=1
pkgdesc="A secure package manager for Arch Linux and the AUR"
arch=('x86_64')
url="https://github.com/fosskers/aura"
license=('GPL3')
depends=('gmp')
makedepends=('stack' 'ghc')
optdepends=('bash-completion: for bash completions'
            'cronie: automatic package record backups with cron')
backup=("etc/$pkgname.conf")
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('ca6ac764a5e2599d8c214978ad1a41eb8b1329c75d7aa604e8caa8c98fe9721b')

build() {
	cd "$pkgname-$pkgver"
	stack clean "$pkgname"
	stack build --system-ghc --pedantic "$pkgname"
}

package() {
	cd "$pkgname-$pkgver"
	stack install --local-bin-path="$pkgdir/usr/bin" "$pkgname"

	# Install man pages
	install -Dm644 "$pkgname/doc/$pkgname.8" -t "$pkgdir/usr/share/man/man8"
	install -Dm644 "$pkgname/doc/$pkgname.conf.5" -t "$pkgdir/usr/share/man/man5"

	# Install docs
	install -Dm644 "$pkgname/doc/backups-with-cron.rst" -t \
		"$pkgdir/usr/share/doc/$pkgname"

	# Install conf file
    install -Dm644 "$pkgname/doc/$pkgname.conf" -t "$pkgdir/etc"

	# Install bash completions
	install -Dm644 "$pkgname/doc/completions/bashcompletion.sh" \
		"$pkgdir/usr/share/bash-completion/completions/$pkgname"

	# Install zsh completions
	install -Dm644 "$pkgname/doc/completions/_$pkgname" -t \
		"$pkgdir/usr/share/zsh/site-functions"

	# Directories for storing PKGBUILDs, source packages & installed package states
	install -d "$pkgdir/var/cache/$pkgname/"{pkgbuilds,src,states}
}
