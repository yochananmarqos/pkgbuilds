# https://aur.archlinux.org/packages/dragonfly-navigator-git

pkgname=dragonfly-navigator-git
pkgver=22.10.1.r11.gbc8a225
pkgrel=1
pkgdesc="A simple and efficient dual pane file manager"
arch=('x86_64')
url="https://github.com/suncore/dflynav"
license=('GPL-3.0-or-later')
depends=(
  'kcoreaddons'
  'ki18n'
  'python-exifread'
  'python-inotify-simple'
  'python-pillow'
  'python-pillow-heif'
  'python-pyqt6'
  'qt6-base'
)
makedepends=(
  'cmake'
  'desktop-file-utils'
  'extra-cmake-modules'
  'git'
  'gtkmm3'
  'kio'
  'ktextwidgets'
  'kxmlgui'
  'vulkan-headers'
)
optdepends=(
  'gnome-terminal: default terminal for non-KDE environments'
  'gtkmm3: required for non-KDE environments'
  'konsole: default terminal on KDE Plasma'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/suncore/dflynav.git')
sha256sums=('SKIP')

pkgver() {
  cd dflynav
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd dflynav/src
  sed "s/VERSION/$pkgver/g" < helptext.html.pre > helptext.html

  # Fix desktop file
  desktop-file-edit --set-key=Exec --set-value=dragonfly \
    --set-icon=dragonfly \
    --set-key=Terminal --set-value=false \
    --remove-key=TryExec \
    --remove-key=TerminalOptions \
    dragonfly.desktop
}

build() {
  cd dflynav/src
  pyuic6 mainwin.ui  > Df_UiMainwin.py
  pyuic6 config.ui  > Df_UiConfig.py
  pyuic6 jobstatus.ui  > Df_UiJobstatus.py
  pyuic6 help.ui  > Df_UiHelp.py
  pyuic6 find.ui  > Df_UiFind.py

 cd openwith
  cmake -B qt/build -S qt \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DQT_MAJOR_VERSION='6' \
    -Wno-dev
  cmake --build qt/build

  cmake -B gtk/build -S gtk \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build gtk/build
}

package() {
  cd dflynav/src
  install -Dm755 dragonfly -t "$pkgdir/usr/share/dragonfly/"
  install -Dm755 terminal -t "$pkgdir/usr/share/dragonfly/"
  install -Dm755 openwith/openwith -t "$pkgdir/usr/share/dragonfly/openwith/"
  install -Dm755 openwith/qt/build/openwith \
    "$pkgdir/usr/share/dragonfly/openwith/openwith.qt"
  install -Dm755 openwith/gtk/build/gtk-open-with \
    "$pkgdir/usr/share/dragonfly/openwith/openwith.gtk"
  install -Dm644 {*.py,helptext.html,showStandardIcons} -t \
    "$pkgdir/usr/share/dragonfly/"
  cp -r icons vfs "$pkgdir/usr/share/dragonfly/"
  install -Dm644 dragonfly.desktop -t "$pkgdir/usr/share/applications/"

  install -d "$pkgdir"/usr/share/{doc/dragonfly,pixmaps}
  ln -s "/usr/share/dragonfly/icons/dragonfly.png" "$pkgdir/usr/share/pixmaps/"
  ln -s "/usr/share/dragonfly/helptext.html" "$pkgdir/usr/share/doc/dragonfly/"

  install -d "$pkgdir/usr/bin"
  ln -s "/usr/share/dragonfly/dragonfly" "$pkgdir/usr/bin/"

  # Compile Python bytecode
  python -m compileall -d / "$pkgdir/usr/share/dragonfly/"
  python -O -m compileall -d / "$pkgdir/usr/share/dragonfly/"
}
