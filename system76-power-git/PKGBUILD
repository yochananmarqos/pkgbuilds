# https://aur.archlinux.org/packages/system76-power-git

pkgname=system76-power-git
pkgver=1.2.1.r1.g290805e
pkgrel=1
pkgdesc="System76 Power Management"
arch=('x86_64' 'aarch64')
url="https://github.com/pop-os/system76-power"
license=('GPL-3.0-or-later')
depends=('dbus' 'libusb' 'polkit')
makedepends=('cargo' 'git')
optdepends=('system76-acpi-dkms: needed for systems with open firmware - only for kernels <5.16'
            'system76-dkms: needed for systems with proprietary firmware')
provides=("${pkgname%-git}" 'power-profiles-daemon')
conflicts=("${pkgname%-git}")
source=('git+https://github.com/pop-os/system76-power.git'
        'mkinitcpio.patch')
sha256sums=('SKIP'
            'e4417c1e2013e51d08a73705adf271939ff66b6d5cf6e3b67a1fe914ba769d05')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # use 'mkinitcpio -P' in place of 'update-initramfs -u'
  patch -Np1 -i "$srcdir/mkinitcpio.patch"
}

build() {
  cd "${pkgname%-git}"
  CFLAGS+=" -ffat-lto-objects"
  export RUSTUP_TOOLCHAIN=stable
  ARGS+="--frozen" make
}

package() {
  cd "${pkgname%-git}"
  make DESTDIR="${pkgdir}" install
}
