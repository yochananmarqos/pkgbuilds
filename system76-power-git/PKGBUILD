# https://aur.archlinux.org/packages/system76-power-git
groups=('modified')

pkgname=system76-power-git
pkgver=1.1.26.r1.g6bbe1c2
pkgrel=1
pkgdesc="System76 Power Management"
arch=('x86_64' 'aarch64')
url='https://github.com/pop-os/system76-power'
license=('GPL-3.0-or-later')
depends=('dbus' 'libusb' 'polkit')
makedepends=('cargo' 'git')
optdepends=('system76-acpi-dkms: needed for systems with open firmware - only for kernels <5.16'
            'system76-dkms: needed for systems with proprietary firmware')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/pop-os/system76-power.git'
        'mkinitcpio.patch')
sha256sums=('SKIP'
            'e4417c1e2013e51d08a73705adf271939ff66b6d5cf6e3b67a1fe914ba769d05')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"

  # use mkinitcpio -P inplace of update-initramfs -u
  patch -Np1 -i "${srcdir}/mkinitcpio.patch"
}

build() {
  cd "${pkgname%-git}"
  CFLAGS+=" -ffat-lto-objects"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  make
}

package() {
  cd "${pkgname%-git}"
  make sysconfdir="/usr/lib" DESTDIR="${pkgdir}" install
}
