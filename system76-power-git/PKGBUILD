# https://aur.archlinux.org/packages/system76-power-git
groups=('modified')

pkgname=system76-power-git
pkgver=1.1.24.r16.g0a68a7f
pkgrel=1
pkgdesc="System76 Power Management"
arch=('x86_64')
url='https://github.com/pop-os/system76-power'
license=('GPL')
depends=('dbus' 'libusb' 'polkit')
makedepends=('cargo' 'git')
optdepends=('system76-acpi-dkms: some systems might need this driver'
            'system76-dkms: some systems might need this driver'
            'system76-io-dkms: some systems might need this driver')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!lto')
source=('git+https://github.com/pop-os/system76-power.git'
        'mkinitcpio.patch')
sha256sums=('SKIP'
            'e4417c1e2013e51d08a73705adf271939ff66b6d5cf6e3b67a1fe914ba769d05')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  # use mkinitcpio -P inplace of update-initramfs -u
  patch -Np1 -i "${srcdir}/mkinitcpio.patch"

  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/${pkgname%-git}"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make sysconfdir="/usr/lib" DESTDIR="${pkgdir}" install
}
