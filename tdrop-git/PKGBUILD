# http://aur.archlinux.org/packages/tdrop-git
pkgname=tdrop-git
pkgver=0.3.0.r0.g2b15530
pkgrel=1
pkgdesc="A WM-Independent dropdown window and terminal creator"
arch=('any')
url="https://github.com/noctuid/tdrop"
license=('BSD')
depends=('xdotool' 'xorg-xwininfo' 'xorg-xprop')
optdepends=('xorg-xrandr: monitor awareness support'
            'tmux: session starting support'
            'tmuxinator: session starting support')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/noctuid/tdrop.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make PREFIX=/usr DESTDIR="$pkgdir/" install
}
