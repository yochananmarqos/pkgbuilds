# https://aur.archlinux.org/packages/bitwarden
groups=('modified')

pkgname=bitwarden
pkgver=1.28.2
pkgrel=3
pkgdesc='A secure and free password manager for all of your devices.'
arch=('x86_64')
url="https://bitwarden.com"
license=('GPL3')
depends=('electron11' 'libnotify' 'libsecret' 'libxtst' 'libxss')
makedepends=('git' 'npm' 'nvm' 'jq')
optdepends=('libappindicator-gtk3: for tray icon')
source=("git+https://github.com/bitwarden/desktop.git#tag=v$pkgver"
        'git+https://github.com/bitwarden/jslib.git'
        'package.json.patch'
        'messaging.main.ts.patch'
        "$pkgname.sh"
        "$pkgname.desktop")
sha256sums=('SKIP'
            'SKIP'
            'ec62ccd29cb85e6c1f324a16495578d6d16efcc9660a02b76231b60ced896a8e'
            'a789a04bad47b74e3f9fd700abbe0636ba785f28fbcadf2d679e0efb1d63b20a'
            'cd4bf43483034d98a3698baffdca78e04b5c6f6208a03eefd1aad1dcfbea7105'
            '6ca2d16c056174f3a00cf5def6221089dfb5d10ad6a8b30640ceecbcb4625efa')

_ensure_local_nvm() {
  # let's be sure we are starting clean
  which nvm >/dev/null 2>&1 && nvm deactivate && nvm unload
  export NVM_DIR="$srcdir/.nvm"

  # The init script returns 3 if version specified
  # in ./.nvrc is not (yet) installed in $NVM_DIR
  # but nvm itself still gets loaded ok
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
}

prepare() {
  cd "$srcdir/desktop"
  git submodule init
  git config submodule.jslib.url "$srcdir/jslib"
  git submodule update

  # Remove pre and postinstall routines from package.json.
  # This is required to build without a git repo and to make the builds more reproducible
  patch --strip=1 package.json "$srcdir/package.json.patch"

  # This patch is required to make "Start automatically on login" work
  patch --strip=1 src/main/messaging.main.ts "$srcdir/messaging.main.ts.patch"

  # Patch build to make it works with system electron
  local SYSTEM_ELECTRON_VERSION=$(tail /usr/lib/electron11/version | cut -d '%' -f1)
  jq < package.json --arg ver $SYSTEM_ELECTRON_VERSION\
  '.build["electronVersion"]=$ver | .build["electronDist"]="/usr/lib/electron11"'\
  > package.json.patched
  mv package.json.patched package.json

  export npm_config_cache="$srcdir/npm_cache"
  _ensure_local_nvm
  nvm install 14.17.6
}

build() {
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1
  export npm_config_cache="$srcdir/npm_cache"
  _ensure_local_nvm

  cd "$srcdir/desktop"

  pushd jslib
  npm install
  popd

  pushd jslib/common
  npm install --only=dev
  popd

  pushd jslib/angular
  npm install
  popd

  npm install
  npm run build
  npm run clean:dist
  npx electron-builder build --dir
}

package() {
  cd "$srcdir/desktop"
  install -d "$pkgdir/usr/lib/$pkgname"
  cp -r dist/linux-unpacked/resources "$pkgdir/usr/lib/$pkgname"

  for i in 16 32 48 64 128 256 512; do
    install -Dm644 resources/icons/${i}x${i}.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  install -Dm644 resources/icon.png \
    "$pkgdir/usr/share/icons/hicolor/1024x1024/apps/$pkgname.png"
  done

  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/bitwarden-desktop"
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}
