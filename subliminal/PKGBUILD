# http://aur.archlinux.org/packages/subliminal
groups=('modified')

pkgname=subliminal
pkgver=2.2.0
pkgrel=1
pkgdesc="Python library and CLI tool for searching and downloading subtitles."
arch=('any')
url="https://github.com/Diaoul/subliminal"
license=('MIT')
depends=(
  'python-babelfish'
  'python-beautifulsoup4'
  'python-chardet'
  'python-click'
  'python-click-option-group'
  'python-dogpile.cache'
  'python-enzyme'
  'python-guessit'
  'python-platformdirs'
  'python-pysubs2'
  'python-rarfile'
  'python-requests'
  'python-srt'
  'python-stevedore'
  'python-tomli'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-sphinx'
  'python-sphinx_rtd_theme'
  'python-sphinxcontrib-programoutput'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('edebed3eafe8e6d0254c514e13eceb6c791a77bf218bbed9be5581ee3f96b4b2')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's/SPHINXOPTS    = -n -W/SPHINXOPTS    = -n/g' docs/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation

  cd docs
  make man
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "docs/_build/man/$pkgname.1" -t "$pkgdir/usr/share/man/man1/"
}

