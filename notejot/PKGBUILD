# https://aur.archlinux.org/packages/notejot

pkgname=notejot
pkgver=3.5.1
pkgrel=1
pkgdesc="Stupidly-simple notes app."
arch=('x86_64')
url="https://github.com/lainsce/notejot"
license=('GPL-3.0-or-later')
depends=(
  'json-glib'
  'libadwaita'
  'libgee'
)
makedepends=(
  'meson'
  'vala'
)
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
        '0001-Simplify-an-overcomplicated-Boolean-expression.patch')
sha256sums=('09fc420322646976594aba1c4d02a01e47d241f196d1c2a519553ad034784213'
            'e40c7d0d8698210bd71b08652921c2608b28d85c0346f0cb3b3c0576e85b7ac8')

prepare() {
  cd "$pkgname-$pkgver"

  # https://github.com/lainsce/notejot/issues/395
  patch -Np1 -i ../0001-Simplify-an-overcomplicated-Boolean-expression.patch
}

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  ln -s /usr/bin/io.github.lainsce.Notejot "$pkgdir/usr/bin/$pkgname"
}
