# https://aur.archlinux.org/packages/lua-lgi-git

pkgname=lua-lgi-git
pkgver=0.9.2.r116.ga412921
pkgrel=1
_LUA_VER=5.4
pkgdesc="Lua bindings for gnome/gobject using gobject-introspection library"
arch=('x86_64')
url="https://github.com/lgi-devs/lgi"
license=('MIT')
depends=(
  'glibc'
  'glib2'
  'libffi'
  'libgirepository'
  'lua'
)
makedepends=(
  'git'
  'gobject-introspection'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/lgi-devs/lgi.git')
sha256sums=('SKIP')

pkgver() {
  cd lgi
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  CFLAGS="${CFLAGS} -I/usr/include" make -C lgi all
}

check() {
  cd lgi
  # TODO: fix cairo-gobject dependency
  #make check
}

package() {
  cd lgi
  CFLAGS="${CFLAGS} -I/usr/include" make \
    LUA_LIBDIR=/usr/lib/lua/${_LUA_VER}\
    LUA_SHAREDIR=/usr/share/lua/${_LUA_VER} \
    DESTDIR="$pkgdir/" install

  install -Dm755 tools/dump-typelib.lua "$pkgdir/usr/bin/dump-typelib"
  install -Dm644 README.md docs/* -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 samples/*.lua -t "$pkgdir/usr/share/$pkgname/samples/"
  install -Dm644 samples/gtk-demo/* -t "$pkgdir/usr/share/$pkgname/samples/gtk-demo/"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
