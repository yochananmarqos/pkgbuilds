pkgname=cheat-git
_gitname=cheat
pkgver=2.5.1
pkgrel=1
pkgdesc="Create and view interactive cheatsheets on the command-line"
arch=('any')
license=('GPL3')
url="https://github.com/chrisallenlane/$_gitname"
depends=('python' 'python-docopt' 'python-pygments' 'python-termcolor')
makedepends=('git' 'python-setuptools')
optdepends=('bash-completion' 'zsh-completions' 'fish')
provides=("$_gitname")
conflicts=("$_gitname-bash-git" "python-$_gitname")
source=("git+https://github.com/chrisallenlane/$_gitname.git")
md5sums=('SKIP')


pkgver() {
	cd "$_gitname"
	git describe | sed 's/^v//;s/-/./g'
}

package(){
	cd "$_gitname"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -dm755 "$pkgdir/"usr/share/{bash-completion/completions,zsh/site-functions,fish/completions}
	install -m 644 "$srcdir/$_gitname/$_gitname/autocompletion/$_gitname.bash" "$pkgdir/usr/share/bash-completion/completions/$_gitname"
	install -m 644 "$srcdir/$_gitname/$_gitname/autocompletion/$_gitname.zsh" "$pkgdir/usr/share/zsh/site-functions/$_gitname"
	install -m 644 "$srcdir/$_gitname/$_gitname/autocompletion/$_gitname.fish" "$pkgdir/usr/share/fish/completions/$_gitname"
}
