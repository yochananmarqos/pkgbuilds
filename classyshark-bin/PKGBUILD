# https://aur.archlinux.org/packages/classyshark

pkgname=classyshark-bin
pkgver=8.2
pkgrel=1
pkgdesc="Analyze any Android/Java based app or game"
arch=('any')
url="https://github.com/google/android-classyshark"
license=('Apache')
depends=('java-runtime')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source=("${pkgname%-bin}-$pkgver.jar::$url/releases/download/$pkgver/ClassyShark.jar"
        "https://github.com/google/android-classyshark/raw/$pkgver/LICENSE.txt"
        "${pkgname%-bin}"
        "${pkgname%-bin}.desktop"
        "${pkgname%-bin}.png")
noextract=("${pkgname%-bin}-$pkgver.jar")
sha256sums=('b3bd94ce2177f2423183070c3dd096cb9b695a70e8e04d8d7d3cca530d9b855d'
            'cfc7749b96f63bd31c3c42b5c471bf756814053e847c10f3eb003417bc523d30'
            'bef261f64afb2dbad2cf329b133cb0aba9647515bfd8a27cade133c7c4910caa'
            '128b7e30f0ed60b969f346d59b71f5f34c7bb4980f60767281ff092779c9f889'
            '0c5ffc9ca5e4ca40ee2091f4fd039ea9f7559ad9068c9c11ccbb402b249a6e32')

package() {
  # Install jar
  install -Dm644 "$srcdir/${pkgname%-bin}-$pkgver.jar" \
    "$pkgdir/usr/share/java/${pkgname%-bin}/${pkgname%-bin}.jar"

  # Install run script
  install -Dm755 "$srcdir/${pkgname%-bin}" -t "$pkgdir/usr/bin/"

  # Install desktop file
  install -Dm644 "$srcdir/${pkgname%-bin}.desktop" -t \
    "$pkgdir/usr/share/applications/"

  # Install icon
  install -Dm644 "$srcdir/${pkgname%-bin}.png" -t "$pkgdir/usr/share/pixmaps/"

  # Install license
  install -Dm644 "$srcdir/LICENSE.txt" -t \
    "$pkgdir/usr/share/licenses/${pkgname%-bin}/"
}
