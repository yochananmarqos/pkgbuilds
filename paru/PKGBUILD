# https://aur.archlinux.org/packages/paru

pkgname=paru
pkgver=2.1.0
pkgrel=1
pkgdesc="Feature packed AUR helper"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/morganamilo/paru"
license=('GPL-3.0-or-later')
depends=(
  'git'
  'libalpm.so>=15'
  'pacman'
)
makedepends=(
  'cargo'
)
optdepends=(
  'bat: colored pkgbuild printing'
  'devtools: build in chroot and download pkgbuilds'
)
backup=("etc/$pkgname.conf")
source=("git+https://github.com/morganamilo/paru.git#tag=v${pkgver}?signed")
sha256sums=('19898d4f2ccea34f3ebc2dba3761b41026a2a9453cfec1ad856c7cb36d3b7d2f')
validpgpkeys=('F850562FCDA369F80D33000AE48D0A8326DE47C5') # Morgan Adamiec <morganamilo@archlinux.org>

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build () {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  if pacman -T pacman-git > /dev/null; then
    _features+="git,"
  fi

  if [[ $CARCH != x86_64 ]]; then
    export CARGO_PROFILE_RELEASE_LTO=off
  fi

  cargo build --frozen --features "${_features:-}" --release
  ./scripts/mkmo locale/
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/$pkgname" -t "${pkgdir}/usr/bin/"
  install -Dm644 "$pkgname.conf" -t "${pkgdir}/etc/"
  install -Dm644 "man/$pkgname.8" -t "$pkgdir/usr/share/man/man8/"
  install -Dm644 "man/$pkgname.conf.5" -t "$pkgdir/usr/share/man/man5/"
  install -Dm644 completions/bash \
    "${pkgdir}/usr/share/bash-completion/completions/$pkgname.bash"
  install -Dm644 completions/fish \
    "${pkgdir}/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 completions/zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  cp -r locale "${pkgdir}/usr/share"
}
