# https://aur.archlinux.org/packages/paru
groups=('modified')

pkgname=paru
pkgver=2.0.3
pkgrel=1
pkgdesc="Feature packed AUR helper"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/morganamilo/paru"
license=('GPL-3.0-or-later')
depends=('git' 'pacman>=6.1.0')
makedepends=('cargo')
optdepends=('bat: colored pkgbuild printing'
            'devtools: build in chroot and download pkgbuilds')
backup=("etc/$pkgname.conf")
_commit=130a7c678610421a077659fbe1a70355c958d2f2  # tags/v2.0.3^0
source=("git+https://github.com/morganamilo/paru.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=('F850562FCDA369F80D33000AE48D0A8326DE47C5') # Morgan Adamiec <morganamilo@archlinux.org>

pkgver() {
  cd "$pkgname"
  git describe --tags --abbrev=7 | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build () {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  if pacman -T pacman-git > /dev/null; then
    _features+="git,"
  fi

  if [[ $CARCH != x86_64 ]]; then
    export CARGO_PROFILE_RELEASE_LTO=off
  fi

  cargo build --frozen  --release --features "${_features:-}"
  ./scripts/mkmo locale/
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/$pkgname" -t "${pkgdir}/usr/bin/"
  install -Dm644 "$pkgname.conf" -t "${pkgdir}/etc/"
  install -Dm644 "man/$pkgname.8" -t "$pkgdir/usr/share/man/man8/"
  install -Dm644 "man/$pkgname.conf.5" -t "$pkgdir/usr/share/man/man5/"
  install -Dm644 completions/bash \
    "${pkgdir}/usr/share/bash-completion/completions/$pkgname.bash"
  install -Dm644 completions/fish \
    "${pkgdir}/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 completions/zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  cp -r locale "${pkgdir}/usr/share"
}
