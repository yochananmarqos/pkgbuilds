# https://aur.archlinux.org/pkgbase/qadwaitadecorations-git

pkgname=(
  'qadwaitadecorations-qt5-git'
  'qadwaitadecorations-qt6-git'  # no longer needed with Qt 6.10
)
pkgbase=qadwaitadecorations-git
pkgver=0.1.7.r1.ge6da80a
pkgrel=1
pkgdesc="Qt decoration plugin implementing Adwaita-like client-side decorations"
arch=('x86_64')
url="https://github.com/FedoraQt/QAdwaitaDecorations"
license=('LGPL-2.1-or-later')
makedepends=(
  'cmake'
  'git'
  'qt5-base'
  'qt5-svg'
  'qt5-wayland-decorations'
  'qt6-base'
  'qt6-svg'
  'vulkan-headers'
)
source=('git+https://github.com/FedoraQt/QAdwaitaDecorations.git')
sha256sums=('SKIP')

pkgver() {
  cd QAdwaitaDecorations
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd QAdwaitaDecorations
}

build() {
  cmake -B build-qt5 -S QAdwaitaDecorations \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DHAS_QT6_SUPPORT='true' \
    -DUSE_QT6='OFF' \
    -Wno-dev
  cmake --build build-qt5

  cmake -B build-qt6 -S QAdwaitaDecorations \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DUSE_QT6='ON' \
    -Wno-dev
  cmake --build build-qt6
}

package_qadwaitadecorations-qt5-git() {
  pkgdesc="Qt5 decoration plugin implementing Adwaita-like client-side decorations"
  depends=(
    'qt5-base'
    'qt5-svg'
    'qt5-wayland-decorations'
  )
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  DESTDIR="$pkgdir" cmake --install build-qt5
}

package_qadwaitadecorations-qt6-git() {
  pkgdesc="Qt6 decoration plugin implementing Adwaita-like client-side decorations"
  depends=(
    'qt6-base'
    'qt6-svg'
  )
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  DESTDIR="$pkgdir" cmake --install build-qt6
}
