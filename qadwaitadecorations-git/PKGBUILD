# https://aur.archlinux.org/pkgbase/qadwaitadecorations-git

pkgname=('qadwaitadecorations-qt5-git' 'qadwaitadecorations-qt6-git')
pkgbase=qadwaitadecorations-git
pkgver=0.1.6.r1.ga4350db
pkgrel=1
pkgdesc="Qt decoration plugin implementing Adwaita-like client-side decorations"
arch=('x86_64')
url="https://github.com/FedoraQt/QAdwaitaDecorations"
license=('LGPL-2.1-or-later')
makedepends=(
  'cmake'
  'git'
  'qt5-svg'
  'qt5-wayland-decorations'
  'qt6-svg'
  'qt6-wayland'
)
source=('git+https://github.com/FedoraQt/QAdwaitaDecorations.git'
        'https://github.com/FedoraQt/QAdwaitaDecorations/pull/80.patch')
sha256sums=('SKIP'
            'b183b4837800c4942f0a8f07638e03d6149c8598a383fc9fcc68dd91893539f8')

pkgver() {
  cd QAdwaitaDecorations
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd QAdwaitaDecorations
  
  # Update colors for libadwaita 1.7
  patch -Np1 -i ../80.patch
}

build() {
  cmake -B build-qt5 -S QAdwaitaDecorations \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DHAS_QT6_SUPPORT='true' \
    -DUSE_QT6='OFF' \
    -Wno-dev
  cmake --build build-qt5

  cmake -B build-qt6 -S QAdwaitaDecorations \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DUSE_QT6='ON' \
    -Wno-dev
  cmake --build build-qt6
}

package_qadwaitadecorations-qt5-git() {
  pkgdesc="Qt5 decoration plugin implementing Adwaita-like client-side decorations"
  depends=(
    'qt5-svg'
    'qt5-wayland-decorations'
  )
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  DESTDIR="$pkgdir" cmake --install build-qt5
}

package_qadwaitadecorations-qt6-git() {
  pkgdesc="Qt6 decoration plugin implementing Adwaita-like client-side decorations"
  depends=(
    'qt6-svg'
    'qt6-wayland'
  )
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  DESTDIR="$pkgdir" cmake --install build-qt6
}
