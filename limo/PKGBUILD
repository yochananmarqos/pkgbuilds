# https://aur.archlinux.org/packages/limo


## WIP

pkgname=limo
_app_id="io.github.${pkgname}_app.$pkgname"
pkgver=1.2.2
pkgrel=1
pkgdesc="A simple Qt based mod manager"
arch=('x86_64')
url="https://github.com/limo-app/limo"
license=('GPL-3.0-or-later')
depends=(
  'cpr'
  'curl'
  'hicolor-icon-theme'
  'jsoncpp'
  'libarchive'
  'libloot'
  'libunrar'
  'lz4'
  'pugixml'
  'qt5-base'
  'qt5-svg'
  'xdg-utils'
  'zlib'
  'zstd'
)
makedepends=(
  'cmake'
#  'doxygen'  # docs
  'git'
)
checkdepends=('catch2')
source=("git+https://github.com/limo-app/limo.git#tag=v$pkgver")
sha256sums=('d9d9870d56ffef64400c40e19a358ed422ded57dd4d09cfec2b2b4dfb3a2f60b')

prepare() {
  cd "$pkgname"
}

build() {
  cmake -B build -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLIMO_INSTALL_PREFIX='/usr' \
    -DUSE_SYSTEM_LIBUNRAR='ON' \
    -DBUILD_TESTING='ON' \
    -Wno-dev
  cmake --build build

  cd "$pkgname"

  # build documentation
#  doxygen src/lmm_Doxyfile
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"
  install -Dm644 "flatpak/${_app_id}.metainfo.xml" -t "$pkgdir/usr/share/metainfo/"
  install -Dm644 resources/logo.png \
    "$pkgdir/usr/share/icons/hicolor/1024x1024/apps/${_app_id}.png"
  install -Dm644 resources/logo_small.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/${_app_id}.png"
}
