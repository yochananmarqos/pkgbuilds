pkgname=python-pytest-qt
_name=${pkgname#python-}
pkgver=4.2.0
pkgrel=1
pkgdesc="Pytest plugin for Qt (PyQt5/PyQt6 and PySide2/PySide6) application testing"
arch=('any')
url="https://github.com/pytest-dev/pytest-qt"
license=('MIT')
depends=('python-pytest')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('00a17b586dd530b6d7a9399923a40489ca4a9a309719011175f55dc6b5dc8f41')

prepare() {
  cd "$_name-$pkgver"
}

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
