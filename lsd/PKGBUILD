# https://www.archlinux.org/packages/community/x86_64/lsd
groups=('modified')

pkgname=lsd
pkgver=0.20.1
pkgrel=1
pkgdesc="Modern ls with a lot of pretty colors and awesome icons"
url="https://github.com/Peltoche/lsd"
arch=('x86_64')
license=('Apache')
depends=('gcc-libs' 'ttf-nerd-fonts-symbols')
makedepends=('cargo' 'pandoc')
source=("https://github.com/Peltoche/lsd/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('a2086aa049b8bd21c880f23b21b0e9ef21d3c3829d40641aa8810c08be936c19')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  SHELL_COMPLETIONS_DIR=completions cargo build --frozen --release --all-features
  pandoc "doc/$pkgname.md" --standalone --to=man -o "doc/$pkgname.1"
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$pkgname" -t "${pkgdir}/usr/bin"
  install -Dm644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 "completions/$pkgname.bash" \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "completions/$pkgname.fish" \
    "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 "completions/_$pkgname" \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm 644 "doc/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}
