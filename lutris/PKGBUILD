# https://www.archlinux.org/packages/community/any/lutris
# https://gitlab.manjaro.org/packages/community/lutris
groups=('modified')

pkgname=lutris
_pkgver=0.5.13-beta1
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('cabextract' 'curl' 'glib2' 'gtk3' 'mesa-utils' 'p7zip' 'psmisc'
         'python-certifi' 'python-dbus' 'python-distro' 'python-gobject'
         'python-lxml' 'python-pillow' 'python-requests' 'python-yaml' 'unzip'
         'webkit2gtk-4.1' 'xdg-utils' 'xorg-xrandr')
makedepends=('meson')
checkdepends=('appstream-glib')
optdepends=(
  'gamemode: Allows games to request a temporary set of optimisations'
  'gamescope: Draw the game window isolated from your desktop'
  'gnome-desktop: Get display and resolution using GnomeDesktop'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-glibc: for 32bit games support'
  "lib32-mangohud: Display the games' FPS + other information"
  'lib32-vkd3d: Vulkan 3D support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'libstrangle: Set FPS limit'
  'linux-steam-integration: start Steam with LSI'
  "mangohud: Display the games' FPS + other information"
  'python-evdev: Controller support'
  'python-moddb: Handle ModDB downloads in installers'
  'python-pefile: Extract icons from Windows executables'
  'python-protobuf: BattleNet integration'
  'python-pypresence: Discord Rich Presence integration'
  'vkd3d: Vulkan 3D support'
  'vulkan-icd-loader: Vulkan support'
  'wine: easiest way to get all the libraries missing from the Lutris runtime'
  'winetricks: use system winetricks'
  'xemu: support for Xbox games'
  'xorg-xgamma: Restore gamma on game exit')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/v$_pkgver.tar.gz")
sha512sums=('83f3c212c1f4290568bce911e4d40aa778d513f444dc6ddf42870242d9c70ae79fbb141d81e144aa8cd918a7fa794c815cd1254b1864e0154fb73d4e1e2aa5bd')

build() {
  arch-meson "$pkgname-$_pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
