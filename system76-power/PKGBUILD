# https://aur.archlinux.org/packages/system76-power

pkgname=system76-power
pkgver=1.2.4
pkgrel=1
pkgdesc="System76 Power Management"
arch=('x86_64' 'aarch64')
url="https://github.com/pop-os/system76-power"
license=('GPL-3.0-or-later')
depends=(
  'dbus'
  'libusb'
  'polkit'
)
makedepends=('cargo')
optdepends=(
  'system76-acpi-dkms: only needed for systems with open firmware with kernels <5.16'
  'system76-dkms: needed for systems with proprietary firmware'
)
provides=('power-profiles-daemon')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        'use-mkinitcpio.patch')
sha256sums=('ab27688b22256006b2ca12cbaf41c99abd17213fe33f4e7c1ed41e1ac05613b7'
            'e4417c1e2013e51d08a73705adf271939ff66b6d5cf6e3b67a1fe914ba769d05')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # use 'mkinitcpio -P' in place of 'update-initramfs -u'
  patch -Np1 -i "$srcdir/use-mkinitcpio.patch"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=" -ffat-lto-objects"
  export RUSTUP_TOOLCHAIN=stable
  ARGS+=" --frozen" make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
