# https://aur.archlinux.org/packages/extension-manager
groups=('modified')

pkgname=extension-manager
pkgver=0.5.0
pkgrel=1
pkgdesc="A native tool for browsing, installing, and managing GNOME Shell Extensions"
arch=('x86_64')
url="https://mattjakeman.com/apps/extension-manager"
license=('GPL3')
depends=('libadwaita' 'libsoup3' 'text-engine')
makedepends=('blueprint-compiler' 'meson')
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mjakeman/extension-manager/archive/v$pkgver.tar.gz")
sha256sums=('d1d1a2540c2cc74d3e3bb46dd58bf18d4d46e600293a973cd6269cc2467415dc')

build() {
  arch-meson "$pkgname-$pkgver" build -Dbacktrace=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
