# https://aur.archlinux.org/packages/extension-manager
groups=('modified')

pkgname=extension-manager
pkgver=0.5.1
pkgrel=1
pkgdesc="A native tool for browsing, installing, and managing GNOME Shell Extensions"
arch=('x86_64')
url="https://mattjakeman.com/apps/extension-manager"
license=('GPL3')
depends=('libadwaita' 'libsoup3' 'text-engine')
makedepends=('blueprint-compiler' 'git' 'meson')
checkdepends=('appstream-glib')
source=("git+https://github.com/mjakeman/extension-manager.git#tag=v$pkgver")
sha256sums=('8343d8376f11cbfe8fd08c8d1a4bbf65852ef5a85ba106943889d4c49f9e32bf')

prepare() {
  cd "$pkgname"
  git revert -n d63d3018c575032872f750d5bf57b9eac3a15e34
}

build() {
  arch-meson "$pkgname" build -Dbacktrace=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
