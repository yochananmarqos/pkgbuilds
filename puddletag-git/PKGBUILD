pkgname=puddletag-git
pkgver=r881.aab842a
pkgrel=1
epoch=1
pkgdesc="An audio tag editor for GNU/Linux."
arch=('i686' 'x86_64')
url="http://docs.puddletag.net"
license=('GPL')
depends=('python2-pyqt5' 'python2-pyparsing' 'python2-mutagen' 'python2-configobj' 'python2-musicbrainz2' 'python2-pillow' 'qt5-svg')
makedepends=('git')
optdepends=('quodlibet: import / edit a QuodLibet library'
			'chromaprint: for AcoustID support')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/keithgg/${pkgname%-git}#branch=pyqt5")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}/source"
	sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
	cd "$srcdir/${pkgname%-git}/source"
	python2 setup.py config
}

package() {
	cd "$srcdir/${pkgname%-git}/source"
	python2 setup.py install --root="$pkgdir" --optimize=1
}
