# https://aur.archlinux.org/packages/gst-plugins-rs
groups=('modified')

pkgname=gst-plugins-rs
pkgver=1.24.3
pkgrel=1
pkgdesc="GStreamer plugins written in Rust"
arch=('x86_64')
url="https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs"
license=('Apache-2.0 AND LGPL-2.1-or-later AND MIT AND MPL-2.0')
depends=(
  'cairo'
#  'dav1d' ## requires <1.3
  'gst-plugins-base-libs'
  'gst-plugins-bad-libs'
  'graphene'
  'gstreamer'
  'gtk4'
  'libwebp'
  'libsodium'
  'openssl'
  'pango'
)
makedepends=(
  'cargo'
  'cargo-c'
  'clang'
  'git'
  'hotdoc'
  'meson'
  'nasm'
  'python-tomli'
)
provides=('gst-plugin-gtk4')
conflicts=('gst-plugin-gtk4')
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs.git#tag=gstreamer-$pkgver")
sha256sums=('c350793466ebca167839156a30587c6a8cce29fdcbf3f33d665c57c8fb9cc2b2')

prepare() {
  cd "${pkgname}"
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target x86_64-unknown-linux-gnu
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "${pkgname}" build \
    -D sodium-source='system' \
    -D dav1d='disabled'
  meson compile -C build
}

check() {
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "${pkgdir}"

  cd "${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
