# https://aur.archlinux.org/packages/gst-plugins-rs
groups=('modified')

pkgname=gst-plugins-rs
pkgver=0.10.8
pkgrel=1
pkgdesc="GStreamer plugins written in Rust"
arch=('x86_64')
url="https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs"
license=('Apache' 'LGPL2.1' 'MIT' 'MPL2')
depends=('cairo' 'dav1d' 'gst-plugins-base-libs' 'gst-plugins-bad-libs' 'graphene'
         'gstreamer' 'gtk4' 'libwebp' 'libsodium' 'openssl' 'pango')
makedepends=('cargo' 'cargo-c' 'clang' 'git' 'hotdoc' 'meson' 'nasm' 'python-tomli')
options=('!lto')
_commit=47a213b3222eb689182f680ff8b4d616ae9886c3  # tags/0.10.8^0
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "${srcdir}/${pkgname}"
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target x86_64-unknown-linux-gnu
}

build() {
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  arch-meson "${pkgname}" build \
    -D sodium-source='system'
  meson compile -C build
}

check() {
  export CARGO_HOME="${srcdir}/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "${pkgdir}"

  cd "${srcdir}/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
