# https://aur.archlinux.org/packages/lagrange

pkgname=lagrange
pkgver=1.19.2
pkgrel=1
pkgdesc="A Beautiful Gemini Client"
arch=('x86_64' 'aarch64')
url="https://gmi.skyjake.fi/lagrange"
license=('BSD-2-Clause')
depends=(
  'fribidi'
  'harfbuzz'
  'hicolor-icon-theme'
  'libjxl'
  'libunistring'
  'libwebp'
  'libx11'
  'mpg123'
  'openssl'
  'opusfile'
  'pcre2'
  'sdl2'
  'zlib'
)
makedepends=(
  'cmake'
  'git'
  'zip'
)
install="$pkgname.install"
source=("https://git.skyjake.fi/skyjake/lagrange/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('9f31ebf1be41f86d0abae7c9aa0b78b453fef3b42b73f3a92759cbbac13b844f'
            'SKIP')
validpgpkeys=('15674AE498667047A3EB9431BACCFCFB98DB2EDC') # Jaakko Ker√§nen (skyjake) <jaakko.keranen@iki.fi>

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
