# https://aur.archlinux.org/packages/lagrange

pkgname=lagrange
_app_id=fi.skyjake.Lagrange
pkgver=1.19.3
pkgrel=1
pkgdesc="A Beautiful Gemini Client"
arch=('x86_64' 'aarch64')
url="https://gmi.skyjake.fi/lagrange"
license=('BSD-2-Clause')
depends=(
  'fribidi'
  'harfbuzz'
  'hicolor-icon-theme'
  'libjxl'
  'libunistring'
  'libwebp'
  'libx11'
  'mpg123'
  'ncurses'
  'openssl'
  'opusfile'
  'pcre2'
  'sdl2'
#  'the-foundation'  ## TODO
  'zlib'
)
makedepends=(
  'cmake'
  'zip'
)
checkdepends=(
  'appstream'
  'desktop-file-utils'
)
install="$pkgname.install"
source=("https://git.skyjake.fi/skyjake/lagrange/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('65025f37804634330cfc5442145169f2cc27bd52f26bc58d6b33683aa12a2d6b'
            'SKIP')
validpgpkeys=('15674AE498667047A3EB9431BACCFCFB98DB2EDC') # Jaakko Ker√§nen (skyjake) <jaakko.keranen@iki.fi>

prepare() {
  cd "$pkgname-$pkgver"

  # remove bundled libs
  for lib in fribidi harfbuzz; do
    rm -rf "lib/${lib}"
  done
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_TUI="ON" \
    -Wno-dev
  cmake --build build
}

check() {
  desktop-file-validate "build/${_app_id}.desktop"
  appstreamcli validate --no-net "$pkgname-$pkgver/res/${_app_id}.appdata.xml"
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
