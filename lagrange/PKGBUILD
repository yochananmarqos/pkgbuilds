# https://aur.archlinux.org/packages/lagrange
groups=('modified')

pkgname=lagrange
pkgver=1.18.3
pkgrel=1
pkgdesc="A Beautiful Gemini Client"
arch=('x86_64' 'aarch64')
url="https://gmi.skyjake.fi/lagrange"
license=('BSD-2-Clause')
depends=(
  'fribidi'
  'harfbuzz'
  'hicolor-icon-theme'
  'libunistring'
  'libwebp'
  'mpg123'
  'openssl'
  'pcre2'
  'sdl2'
  'zlib'
)
makedepends=(
  'cmake'
  'git'
  'zip'
)
install="$pkgname.install"
source=("https://git.skyjake.fi/skyjake/lagrange/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('c432e2f4dd35a6cee0f4d5a77974708ec6fed76bc13cc630bb50650acec87e62'
            'SKIP')
validpgpkeys=('15674AE498667047A3EB9431BACCFCFB98DB2EDC') # Jaakko Ker√§nen (skyjake) <jaakko.keranen@iki.fi>

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
