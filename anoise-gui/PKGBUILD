# https://aur.archlinux.org/packages/anoise-gui

pkgname=anoise-gui
pkgver=0.0.4
pkgrel=7
pkgdesc="Ambient Noise Player GUI"
arch=('any')
url="https://costales.github.io/projects/anoise"
license=('GPL-3.0-or-later')
depends=('anoise')
source=("https://launchpad.net/~costales/+archive/ubuntu/anoise/+sourcefiles/$pkgname/$pkgver/${pkgname}_${pkgver}.tar.gz")
noextract=("${pkgname}_${pkgver}.tar.gz")
sha256sums=('b97553fe661c55d3563dc8031834b51a36387ca3eb856eb78c046e4d8efc868b')

prepare() {
  mkdir -p "$pkgname-$pkgver"
  bsdtar xf "${pkgname}_${pkgver}.tar.gz" --strip-components 1 -C "$pkgname-$pkgver"

  cd "$pkgname-$pkgver"

  # Update URL
  sed -i "s|http://anoise.tuxfamily.org|$url|g" anoise/view.py
}

package() {
  cd "$pkgname-$pkgver"
  install -d "$pkgdir/usr/share"
  cp -a anoise "$pkgdir/usr/share"
}
