# https://aur.archlinux.org/packages/anoise-gui
groups=('modified')

pkgname=anoise-gui
pkgver=0.0.4
pkgrel=6
pkgdesc="Ambient Noise Player GUI"
arch=('any')
url="https://costales.github.io/projects/anoise"
license=('GPL-3.0-or-later')
depends=(
  'anoise'
)
makedepends=(
  'python-build'
  'python-distutils-extra'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("https://launchpad.net/~costales/+archive/ubuntu/anoise/+sourcefiles/$pkgname/$pkgver/${pkgname}_${pkgver}.tar.gz")
noextract=("${pkgname}_${pkgver}.tar.gz")
sha256sums=('b97553fe661c55d3563dc8031834b51a36387ca3eb856eb78c046e4d8efc868b')

prepare() {
  mkdir -p "$pkgname-$pkgver"
  bsdtar xf "${pkgname}_${pkgver}.tar.gz" -C "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver/$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver/$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -rv "${pkgdir}${site_packages}/usr/share" "$pkgdir/usr/"
  rm -rv "${pkgdir}${site_packages}/usr"
}
