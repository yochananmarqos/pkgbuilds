# http://aur.archlinux.org/packages/phantomjs-beta-bin
pkgname=phantomjs-beta-bin
pkgver=2.5.0
pkgrel=3
pkgdesc="Headless WebKit with JavaScript API (official beta binaries)"
url="https://www.phantomjs.org"
license=('BSD')
arch=('x86_64')
depends=('fontconfig' 'freetype2' 'hyphen' 'icu55' 'libpng' 'libpng12'
         'openssl-1.0')
provides=("${pkgname%-*-*}")
conflicts=("${pkgname%-*-*}")
_path="${pkgname%-*-*}-$pkgver-beta-ubuntu-xenial"
_filename="${pkgname%-*-*}-$pkgver-beta-linux-ubuntu-xenial-x86_64.tar.gz"
source=("https://bitbucket.org/ariya/phantomjs/downloads/$_filename")
sha256sums=('b478bb44e7a77468683a615bda082716d76e99dc17ebbc005d31d06b6715429e')

package() {
    install -Dm755 "$srcdir/$_path/bin/${pkgname%-*-*}" -t "$pkgdir/usr/bin"
    install -Dm755 "$srcdir/$_path/LICENSE.BSD" \
    	"$pkgdir/usr/share/licenses/${pkgname%-*-*}/LICENSE"
    install -dm755 "$pkgdir/usr/share/${pkgname%-*-*}/examples"
    cp -r "$srcdir/$_path/examples" "$pkgdir/usr/share/${pkgname%-*-*}/"
}
