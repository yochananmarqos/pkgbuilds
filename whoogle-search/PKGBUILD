# https://aur.archlinux.org/packages/whoogle

pkgname=whoogle-search
pkgver=0.8.4
pkgrel=1
pkgdesc="A self-hosted, ad-free, privacy-respecting metasearch engine"
arch=('any')
url="https://github.com/benbusby/whoogle-search"
license=('MIT')
depends=(
  'python'
  'python-attrs'
  'python-beautifulsoup4'
  'python-brotli'
  'python-cachelib'
  'python-certifi'
  'python-cffi'
  'python-chardet'
  'python-click'
  'python-cryptography'
  'python-cssutils'  # AUR
  'python-dateutil'
  'python-defusedxml'
  'python-dotenv'
  'python-flask'
  'python-idna'
  'python-itsdangerous'
  'python-jinja'
  'python-markupsafe'
  'python-more-itertools'
  'python-packaging'
  'python-pluggy'
  'python-pycodestyle'
  'python-pycparser'
  'python-pyparsing'
  'python-pysocks'
  'python-requests'
  'python-soupsieve'
  'python-stem'
  'python-urllib3'
  'python-validators'
  'python-waitress'
  'python-wcwidth'
  'python-werkzeug'
)
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
provides=('whoogle')
conflicts=('whoogle')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        'whoogle'
        'whoogle.conf'
        'whoogle.service')
install="$pkgname.install"
sha256sums=('982ffc76ab72553badf9f7c23b071d0af396036ab1a247a73898bcdb86fb7ce5'
            'e30ff5ecef199ce2a37b097709461c51ca07bdbbcc4609db74203834b62c60b1'
            'ce55f1282034540478ad4e08e42d820da3fe989ddf56e818a140da0ae5024668'
            '3e9df7ec3fae86b413cfcf9e0ef9da89cf8774b25e09f2bd7d52fca228128c34')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "$srcdir/whoogle" -t "$pkgdir/etc/default/"
  install -Dm644 "$srcdir/whoogle.conf" -t "$pkgdir/usr/lib/sysusers.d/"
  install -Dm644 "$srcdir/whoogle.service" -t "$pkgdir/usr/lib/systemd/system/"
}
