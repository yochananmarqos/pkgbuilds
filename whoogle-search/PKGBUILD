# https://aur.archlinux.org/packages/whoogle

## WIP

pkgname=whoogle-search
pkgver=0.9.3
pkgrel=1
pkgdesc="A self-hosted, ad-free, privacy-respecting metasearch engine"
arch=('any')
url="https://github.com/benbusby/whoogle-search"
license=('MIT')
depends=(
  'python'
  'python-attrs'
  'python-beautifulsoup4'
  'python-brotli'
  'python-cachelib'
  'python-certifi'
  'python-cffi'
  'python-chardet'
  'python-click'
  'python-cryptography'
  'python-cssutils'  # AUR
  'python-defusedxml'
  'python-dotenv'
  'python-flask'
  'python-idna'
  'python-itsdangerous'
  'python-jinja'
  'python-markupsafe'
  'python-more-itertools'
  'python-packaging'
  'python-pluggy'
  'python-pycparser'
  'python-pyopenssl'
  'python-pyparsing'
  'python-pysocks'
  'python-requests'
  'python-soupsieve'
  'python-stem'
  'python-urllib3'
  'python-validators'
  'python-waitress'
  'python-wcwidth'
  'python-werkzeug'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-dateutil'
  'python-pytest'
)
provides=('whoogle')
conflicts=('whoogle')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        'whoogle'
        'whoogle.conf'
        'whoogle.service')
install='whoogle.install'
sha256sums=('aab48638e61fdd1765ef06686ad454492817442523d426c79fec462ca13c020d'
            'e30ff5ecef199ce2a37b097709461c51ca07bdbbcc4609db74203834b62c60b1'
            '1f716def8842def76527b18104c6f7a77120590cfeef75775020ae0ebbfd1c0c'
            '94b8598d7a707384a40f715c961ff83749b8c7ae4a973609e0baf448b669611c')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  pytest
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 "$srcdir/whoogle" -t "$pkgdir/etc/default/"
  install -Dm644 "$srcdir/whoogle.conf" -t "$pkgdir/usr/lib/sysusers.d/"
  install -Dm644 "$srcdir/whoogle.service" -t "$pkgdir/usr/lib/systemd/system/"
}
