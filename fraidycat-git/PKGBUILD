pkgname=fraidycat-git
pkgver=1.1.0.r0.2caf33f
_pkgver=1.1.0
pkgrel=1
pkgdesc="Follow blogs, wikis and social media from a single page."
arch=('x86_64')
url="https://fraidyc.at"
license=('BlueOak-1.0.0')
depends=('libnotify' 'nss' 'libxss' 'libxtst' 'xdg-utils'
         'libappindicator-gtk3' 'libsecret')
makedepends=('git-lfs' 'npm')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")

prepare() {
	rm -rf "$srcdir/${pkgname%-git}"

	git clone https://github.com/kickscondor/fraidycat.git

	cd "$srcdir/${pkgname%-git}"
	git lfs install
	git lfs pull

	# Disable building of AppImage
	sed -i '48d' package.json
}

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install --cache "$srcdir/npm-cache"
	npm run electron:linux
}

package() {
	cd "$srcdir/${pkgname%-git}"

	install -dm755 "$pkgdir/opt/Fraidycat"
	cp -dr --no-preserve=ownership dist/linux-unpacked/* \
		"$pkgdir/opt/Fraidycat"

	install -dm755 "$pkgdir/usr/bin"
	ln -s "/opt/Fraidycat/${pkgname%-git}" -t "$pkgdir/usr/bin"

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/${pkgname%-git}"

	for icon_size in 16 32 64 128 512; do
		icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d "$pkgdir/$icons_dir"
		install -m644 src/images/flatcat-${icon_size}.png \
			"$pkgdir$icons_dir/${pkgname%-git}.png"
	done

	# Install desktop file from .deb
	cd dist
	ar x "${pkgname%-git}_${_pkgver}_amd64.deb"
	tar -xf data.tar.xz
	install -Dm644 "usr/share/applications/${pkgname%-git}.desktop" -t \
		"$pkgdir/usr/share/applications"
}
