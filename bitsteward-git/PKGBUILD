## WIP

pkgname=bitsteward-git
pkgver=0.0.4.r13.g63053bc
pkgrel=1
pkgdesc="A GTK4 + Libadwaita client for Bitwarden"
arch=('any')
url="https://github.com/Bitsteward/bitsteward"
license=('GPL-3.0-or-later')
depends=(
  'bitwarden-cli'
  'libadwaita'
  'python-dotenv'
  'python-requests'
)
makedepends=(
  'git'
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Bitsteward/bitsteward.git'
        "${pkgname%-git}.desktop"
        "${pkgname%-git}.sh")
sha256sums=('SKIP'
            '4d76c8236496ac0584f330daf27176d69554c1810b4c4dbd3d633b39151b989b'
            'ca2bf033524d7a76c0c542d4b4061d07bd999750e67b6e50ba0c2c5f8e5f818d')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"

  # invalid classifer
#  sed -i '/License ::/d' pyproject.toml
}

build() {
  cd "${pkgname%-git}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname%-git}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "${pkgdir}${site_packages}/${pkgname%-git}"
  mv "${pkgdir}${site_packages}/src" "${pkgdir}${site_packages}/${pkgname%-git}/"

  install -Dm755 "$srcdir/${pkgname%-git}.sh" "$pkgdir/usr/bin/${pkgname%-git}"
  install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t \
    "$pkgdir/usr/share/applications/"

  # Install password store file
  touch "${pkgdir}${site_packages}/${pkgname%-git}/.env"
  chmod 777 "${pkgdir}${site_packages}/${pkgname%-git}/.env"

  # Symlink Bitwarden CLI binary
  ln -s /usr/bin/bw "${pkgdir}${site_packages}/${pkgname%-git}/"
}
