# https://www.archlinux.org/packages/community/x86_64/nnn
pkgname=nnn
pkgver=3.0
pkgrel=1
pkgdesc="The fastest terminal file manager ever written."
arch=('x86_64')
url="https://github.com/jarun/nnn"
license=('BSD')
depends=('bash')
optdepends=('xdg-utils: desktop opener'
            'zip: for zip archive format'
            'unzip: for zip archive format'
            'atool: for more archive formats'
            'libarchive: for more archive formats'
            'archivemount: mount remotes'
            'fuse2: unmount remotes'
            'sshfs: mount remotes'
            'rclone: mount remotes'
            'trash-cli: to trash files'
            'vlock-original: terminal locker'
            'vlock-original-noroot: terminal locker'
            'cmatrix: terminal locker (fallback)'
            'advcp: copy, move progress'
            'bash-completion: bash completions'
            'fish: fish completions'
            'zsh: zsh completions')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/nnn-$pkgver.tar.gz.sig")
sha256sums=('04db6d6710ce1232c779bf70137a86557e486614e20327717122bb63f36348f7'
            'SKIP')
validpgpkeys=('BBAD0B2F3093A7C3377A8F6BA75979F35C080412')
              # Arun Prakash Jana <engineerarun at gmail dot com>

prepare() {
	cd "$pkgname-$pkgver"
	sed -i 's/install: all/install:/g' Makefile
}

build() {
	cd "$pkgname-$pkgver"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir/" PREFIX=/usr install

	install -Dm644 misc/auto-completion/fish/nnn.fish \
		"$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
	install -Dm644 misc/auto-completion/bash/nnn-completion.bash \
		"$pkgdir/usr/share/bash-completion/completions/$pkgname"
	install -Dm644 misc/auto-completion/zsh/_nnn \
		"$pkgdir/usr/share/zsh/site-functions/_$pkgname"

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
