# https://www.archlinux.org/packages/community/any/python-gitdb
pkgname=python-gitdb
_name=${pkgname#python-}
pkgver=4.0.4
pkgrel=1
pkgdesc="A pure-Python git object database"
arch=('any')
url="https://github.com/gitpython-developers/gitdb"
license=('BSD')
depends=('git' 'python-smmap')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz") #{,.asc})
sha256sums=('6f0ecd46f99bb4874e5678d628c3a198e2b4ef38daea2756a2bfd8df7dd5c1a5')
#            'SKIP')
#validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60')
              # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

# some tests need to be disabled, because of coupling with the project
# repository: https://github.com/gitpython-developers/gitdb/issues/51
check() {
	cd "$_name-$pkgver"
	local TEST_TMPDIR=$(mktemp -d)
	(
		cd "$TEST_TMPDIR"
		git init
		git config user.name "Test User"
		git config user.email "test@user.org"
		for commit in {1..50}; do
			touch "file${commit}"
			git add "file${commit}"
			git commit -m "file${commit}"
		done
	)
	export GITDB_TEST_GIT_REPO_BASE="${TEST_TMPDIR}/.git"
	nosetests -vd -e 'test_pack_writing'
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --skip-build \
		--optimize=1 \
		--prefix=/usr \
		--root="$pkgdir"
	install -vDm 644 {AUTHORS,README.rst} \
		-t "$pkgdir/usr/share/doc/$pkgname/"
	install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
