pkgname=cage-git
pkgver=r55.14aadbc
pkgrel=1
pkgdesc="A Wayland kiosk"
arch=('any')
url="https://www.hjdskes.nl/projects/cage/"
license=('MIT')
depends=('wayland' 'wlroots' 'libxkbcommon' 'xorg-server-xwayland')
makedepends=('meson' 'ninja')
provides=('cage')
source=("$pkgname"::'git+https://github.com/Hjdskes/cage.git'
		'https://raw.githubusercontent.com/Hjdskes/cage/master/LICENSE')
md5sums=('SKIP'
         '8998d04d20cc4c8db034a037c188e634')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$pkgname"
	rm -rf build
	meson build -Dxwayland=true
	ninja -C build
}

package() {
	cd "$srcdir/$pkgname"
	DESTDIR="$pkgdir/" meson install -C build
	install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
