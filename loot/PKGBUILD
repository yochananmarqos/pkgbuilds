# https://aur.archlinux.org/packages/loot

pkgname=loot
_app_id="io.github.$pkgname.$pkgname"
pkgver=0.26.1
pkgrel=1
pkgdesc="A modding utility for Starfield and some Elder Scrolls and Fallout games."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-only')
depends=(
  'fmt'
  'hicolor-icon-theme'
  'icu'
  'libloot'
  'minizip-ng'
  'onetbb'
  'spdlog'
  'qt6-base'
  'tomlplusplus'
  'zlib-ng'
)
makedepends=(
  'boost'
  'cbindgen'
  'cmake'
#  'doxygen'  # docs
  'git'
  'ogdf'
  'python'
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
  'vulkan-headers'
)
# Use the Git repository so that loot's commit hash gets embedded in the build
source=("git+https://github.com/loot/loot.git#tag=$pkgver")
sha256sums=('876349f90fee64526fa6e95e887392bb710c50120e29dbff77a77c8c34dc8efb')

build() {
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"
  cmake -B build -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLOOT_BUILD_TESTS='OFF' \
    -Wno-dev
  cmake --build build

  # translations
  cd "$pkgname"
  python scripts/po_to_mo.py

  # docs
#  sphinx-build -b html docs build/docs/html
}

#check() {
#  ctest --test-dir build --output-on-failure
#}

package() {
  install -Dm755 build/LOOT -t "$pkgdir/usr/bin/"

  cd "$pkgname"  
  install -Dm644 "resources/linux/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "resources/linux/${_app_id}.metainfo.xml" -t \
    "$pkgdir/usr/share/metainfo/"
  install -Dm644 "resources/icons/$pkgname.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"

  cd resources/l10n
  # en directory has no translation file
  rm -rf en
  for lang in $(ls -d */); do
    install -Dm644 "${lang%%/}/LC_MESSAGES/$pkgname.mo" -t \
      "$pkgdir/usr/share/locale/${lang%%/}/LC_MESSAGES/"
  done
}
