# https://aur.archlinux.org/packages/loot
groups=('modified')

pkgname=loot
pkgver=0.24.0
pkgrel=1
pkgdesc="A modding utility for Starfield and some Elder Scrolls and Fallout games."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-only')
depends=(
  'hicolor-icon-theme'
  'libloot'
  'qt6-base'
)
makedepends=(
  'boost'
  'cmake'
#  'doxygen'  # docs
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/loot/loot/archive/$pkgver.tar.gz"
        'add-missing-algorithm-include.patch'
        'loot.desktop')
sha256sums=('14ccb1652079c7df9088bca837deee55bbe352ae44afb10f1139e1c4cff199a3'
            '71f605e0d52d71680b0ca0bd921ef56d2877750711703ed337839f8cc410a630'
            'c8a8776d7a752f227c5c90d23bab6800b18c3da11376a3ca8279532cd1695901')

prepare() {
  cd "$pkgname-$pkgver"

  # Fix ‘stable_sort’ is not a member of ‘std’ error
  patch -Np1 -i ../add-missing-algorithm-include.patch
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLOOT_BUILD_TESTS='OFF' \
    -Wno-dev
  cmake --build build

  # docs
#  cd "$pkgname-$pkgver"
#  sphinx-build -b html docs build/docs/html
}

#check() {
#  ctest --test-dir build --output-on-failure
#}

package() {
  install -Dm755 build/LOOT -t "$pkgdir/usr/bin/"
  install -Dm644 "$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "$pkgname-$pkgver/resources/icons/$pkgname.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
}
