# https://aur.archlinux.org/packages/loot
groups=('modified')

pkgname=loot
pkgver=0.23.0
pkgrel=1
pkgdesc="A modding utility for Starfield and some Elder Scrolls and Fallout games."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-or-later')
depends=(
  'hicolor-icon-theme'
  'icu'
  'onetbb'
  'qt6-base'
)
makedepends=(
  'boost'
  'cargo'
  'cbindgen'
  'cmake'
  'git'
)
source=("git+https://github.com/loot/loot.git#tag=$pkgver"
        "git+https://github.com/loot/libloot.git#tag=$pkgver"
        'LOOT.desktop'
        'icu.patch')
sha256sums=('0fa8e11acd628dfd16b82cf85f6d96b8162e90fe40353b5485f00aab2a3c6130'
            '319821c84df81815fd61eeb1a8602948c8cda22175b9a097aab7f8487a75fd1a'
            '3dd063fdbe33dc82a4298bd5bcd3b4e7490adab4128389c153d12c6b074b27fb'
            'b37b8853966340764ee811222d95df837a9c0d3b5169b39dfac650aaaeaac377')

prepare() {
  mkdir -p "lib${pkgname}/pkg/lib"

  cd "$pkgname"

  # Update ICU version to match system version
  patch -Np1 -i ../icu.patch
}

build() {

  # libloot
  pushd "lib${pkgname}"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DLIBLOOT_BUILD_TESTS='OFF' \
    -DLIBLOOT_INSTALL_DOCS='OFF' \
    -Wno-dev
  cmake --build build

  cp "build/lib${pkgname}.so" ./pkg/lib
  cp -r ./include/ ./pkg/
  bsdtar -zcf "lib${pkgname}-$pkgver.tar.gz" ./pkg/
  popd

  # loot
  pushd "$pkgname"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DLIBLOOT_URL="$srcdir/lib${pkgname}/lib${pkgname}-$pkgver.tar.gz" \
    -DCMAKE_BUILD_WITH_INSTALL_RPATH='TRUE' \
    -DCMAKE_INSTALL_RPATH="/opt/$pkgname" \
    -Wno-dev
  cmake --build build
  popd
}

package() {
  install -Dm755 -t "$pkgdir/opt/$pkgname" \
    "$pkgname/build/LOOT" \
    "lib${pkgname}/build/lib${pkgname}.so.$pkgver"
  ln -s "/opt/$pkgname/lib${pkgname}.so.$pkgver" "$pkgdir/opt/$pkgname/lib${pkgname}.so.0"
  ln -s "/opt/$pkgname/lib${pkgname}.so.0" "$pkgdir/opt/$pkgname/lib${pkgname}.so"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/$pkgname/LOOT" "$pkgdir/usr/bin"

  install -Dm644 "$pkgname/resources/icons/loot.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
  install -Dm644 "$srcdir/LOOT.desktop" -t "$pkgdir/usr/share/applications/"
}
