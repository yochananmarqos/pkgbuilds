# https://aur.archlinux.org/packages/loot

pkgname=loot
_app_id="io.github.$pkgname.$pkgname"
pkgver=0.28.0
pkgrel=1
pkgdesc="A modding utility for Starfield and some Elder Scrolls and Fallout games."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-only')
depends=(
  'fmt'
  'hicolor-icon-theme'
  'icu'
  'libloot'
  'minizip-ng'
  'ogdf'
  'onetbb'
  'spdlog'
  'qt6-base'
  'tomlplusplus'
  'zlib-ng'
)
makedepends=(
  'boost'
  'cmake'
#  'doxygen'  # docs
  'git'
  'python'
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
)
checkdepends=(
  'appstream'
  'desktop-file-utils'
)
source=("git+https://github.com/loot/loot.git#tag=$pkgver")
sha256sums=('aca334960cc73dfa33e7ef0e2d853897876dbc174e30d3d7f7d8a3bb8dd5bf71')

prepare() {
  cd "$pkgname"

  # Set StartupWMClass
  desktop-file-edit --set-key=StartupWMClass --set-value=LOOT \
    "resources/linux/${_app_id}.desktop"
}

build() {
  cmake -B build -S "$pkgname" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLOOT_BUILD_TESTS='OFF' \
    -Wno-dev
  cmake --build build

  # translations
  cd "$pkgname"
  python scripts/po_to_mo.py

  # docs
#  sphinx-build -b html docs build/docs/html
}

check() {
#  ctest --test-dir build --output-on-failure

  cd "$pkgname"
  appstreamcli validate --no-net "resources/linux/${_app_id}.metainfo.xml"
  desktop-file-validate "resources/linux/${_app_id}.desktop"
}

package() {
  install -Dm755 build/LOOT -t "$pkgdir/usr/bin/"

  cd "$pkgname"  
  install -Dm644 "resources/linux/${_app_id}.desktop" -t \
    "$pkgdir/usr/share/applications/"
  install -Dm644 "resources/linux/${_app_id}.metainfo.xml" -t \
    "$pkgdir/usr/share/metainfo/"
  install -Dm644 "resources/icons/$pkgname.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/${_app_id}.svg"

  cd resources/l10n
  # en directory has no translation file
  rm -rf en
  for lang in $(ls -d */); do
    install -Dm644 "${lang%%/}/LC_MESSAGES/$pkgname.mo" -t \
      "$pkgdir/usr/share/locale/${lang%%/}/LC_MESSAGES/"
  done
}
