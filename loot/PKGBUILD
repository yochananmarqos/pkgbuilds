# https://aur.archlinux.org/packages/loot

pkgname=loot
pkgver=0.25.2
pkgrel=1
pkgdesc="A modding utility for Starfield and some Elder Scrolls and Fallout games."
arch=('x86_64')
url="https://loot.github.io"
license=('GPL-3.0-only')
depends=(
  'hicolor-icon-theme'
  'libloot'
  'qt6-base'
)
makedepends=(
  'boost'
  'cmake'
#  'doxygen'  # docs
  'ogdf'
#  'python-sphinx'  # docs
#  'python-sphinx_rtd_theme'  # docs
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/loot/loot/archive/$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('30e65e0409c71ea06eb3437a1501e41bdc603c674d976be86ef3aa14d790bf94'
            'c8a8776d7a752f227c5c90d23bab6800b18c3da11376a3ca8279532cd1695901')

build() {
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DLOOT_BUILD_TESTS='OFF' \
    -Wno-dev
  cmake --build build

  # docs
#  cd "$pkgname-$pkgver"
#  sphinx-build -b html docs build/docs/html
}

#check() {
#  ctest --test-dir build --output-on-failure
#}

package() {
  install -Dm755 build/LOOT -t "$pkgdir/usr/bin/"
  install -Dm644 "$pkgname.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dm644 "$pkgname-$pkgver/resources/icons/$pkgname.svg" -t \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
}
