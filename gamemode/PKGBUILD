pkgname=gamemode
pkgver=1.4
pkgrel=1
pkgdesc="A daemon/lib combo for Linux that allows games to request a set of optimisations be temporarily applied to the host OS."
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause License (Revised)')
depends=('polkit')
optdepends=('systemd')
makedepends=('meson' 'pkg-config' 'git')
provides=("$pkgname")
conflicts=("$pkgname")
source=("https://github.com/FeralInteractive/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('e83f07dc91bbd27d1faadd69f7fda899810001e2a256be8061e36dee0e858f4a'
            'SKIP')
validpgpkeys=('5AB43EC644CE6A84C39289F5048EF7331E7B6424')

build() {
	cd "$pkgname-$pkgver"
	meson build --prefix /usr -Dwith-systemd-user-unit-dir=/usr/lib/systemd/user -Dwith-pam-group=gamemode
	ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR=$pkgdir ninja -C build install
	install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
