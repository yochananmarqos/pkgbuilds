# https://gitlab.manjaro.org/packages/community/gamemode
# https://aur.archlinux.org/packages/gamemode
pkgname=gamemode
pkgver=1.5.1
pkgrel=2
pkgdesc="Optimise Linux system performance on demand"
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('polkit')
optdepends=('systemd')
makedepends=('git' 'meson')
install="$pkgname.install"
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('3a5ea5aafe1b7ec69ac2c054198e9aa6b6fd4dd8ee7a8bcfb71d0b0a40313101')

build() {
	arch-meson "$pkgname-$pkgver" build \
		--libexecdir lib/gamemode \
		-Dwith-systemd-user-unit-dir=/usr/lib/systemd/user
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	cd "$pkgname-$pkgver"
	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "example/$pkgname.ini" -t "$pkgdir/usr/share/doc/$pkgname/example"
}
