pkgname=gamemode
pkgver=1.5.1
pkgrel=1
pkgdesc="Optimise Linux system performance on demand"
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('polkit')
optdepends=('systemd')
makedepends=('git' 'meson')
install="$pkgname.install"
source=("git+https://github.com/FeralInteractive/gamemode.git#tag=$pkgver"
        'git+https://github.com/benhoyt/inih.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
	cd "$srcdir/$pkgname"
	git submodule init subprojects/inih
	git config submodule.inih.url $srcdir/inih
	git submodule update
}

build() {
	arch-meson "$pkgname" build \
		--libexecdir lib/gamemode \
		-Dwith-systemd-user-unit-dir=/usr/lib/systemd/user
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	cd "$srcdir/$pkgname"
	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "example/$pkgname.ini" -t "$pkgdir/usr/share/doc/$pkgname/example"
}
