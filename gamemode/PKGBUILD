# https://gitlab.manjaro.org/packages/community/gamemode
# https://aur.archlinux.org/packages/gamemode
pkgname=gamemode
pkgver=1.6
pkgrel=1
pkgdesc="Optimise Linux system performance on demand"
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('polkit')
optdepends=('systemd')
makedepends=('git' 'meson')
install="$pkgname.install"
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('4fec5a0900e8f559b37cf1711ca7dcea7e1cebfe1237ce314001f65c1e346a5d')

build() {
	arch-meson "$pkgname-$pkgver" build \
		--libexecdir lib/gamemode \
		--force-fallback-for=inih \
		-Dwith-pam-group=gamemode \
		-Dwith-systemd-user-unit-dir=/usr/lib/systemd/user
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	cd "$pkgname-$pkgver"
	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "example/$pkgname.ini" -t "$pkgdir/usr/share/doc/$pkgname/example"
}
