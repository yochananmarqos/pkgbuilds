# https://aur.archlinux.org/packages/whatip-git
groups=('modified')

pkgname=whatip-git
pkgver=1.0.r3.g4fdfa66
pkgrel=1
pkgdesc="Info on your IP"
arch=('any')
url="https://gabmus.gitlab.io/whatip"
license=('GPL3')
depends=('gtk4' 'iproute2' 'libadwaita' 'python-requests' 'python-netaddr'
         'python-gobject' 'python-iperf3')
makedepends=('git' 'meson' 'gobject-introspection' 'gtk3')
optdepends=('avahi: hostname resolution')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.gnome.org/GabMus/whatip.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
