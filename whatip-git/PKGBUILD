# https://aur.archlinux.org/packages/whatip-git

pkgname=whatip-git
pkgver=1.2.r22.g81729d4
pkgrel=1
pkgdesc="Info on your IP"
arch=('any')
url="https://whatip.gabmus.org"
license=('GPL-3.0-or-later')
depends=(
  'iproute2'
  'libadwaita'
  'python-requests'
  'python-netaddr'
  'python-gobject'
)
makedepends=(
  'blueprint-compiler'
  'git'
  'gobject-introspection'
  'meson'
)
optdepends=('avahi: hostname resolution')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.gnome.org/GabMus/whatip.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson "${pkgname%-git}" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
