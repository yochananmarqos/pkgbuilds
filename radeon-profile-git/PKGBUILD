# https://aur.archlinux.org/packages/radeon-profile-git
pkgname=radeon-profile-git
pkgver=20200824.r0.g1ea721b
pkgrel=1
pkgdesc="Application to read current clocks of ATI Radeon cards"
arch=('x86_64')
url="http://github.com/marazmista/radeon-profile"
license=('GPL2')
depends=('qt5-charts' 'libxrandr' 'libdrm')
makedepends=('git')
optdepends=('radeon-profile-daemon: system daemon for reading card info'
            'mesa-demos: info about OpenGL, Mesa'
            'xorg-xdriinfo: driver info'
            'xorg-xrandr: connected displays'
            'xf86-video-ati: radeon open source driver'
            'xf86-video-amdgpu: amdgpu open source driver')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/marazmista/radeon-profile.git'
        "${pkgname%-git}-launcher")
sha256sums=('SKIP'
            '59b17b74ed3f3c1ac05cd0c6c76ceeef7d04072e46e3ee3f3caf15146dc0c092')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i 's/Exec=radeon-profile/Exec=radeon-profile-launcher/g' \
		"${pkgname%-git}/extra/${pkgname%-git}.desktop"
}

build() {
	cd "$srcdir/${pkgname%-git}/${pkgname%-git}"
	qmake
	make
}

package() {
	cd "$srcdir/${pkgname%-git}/${pkgname%-git}"
	make INSTALL_ROOT="$pkgdir" install

	install -Dm755 "$srcdir/${pkgname%-git}-launcher" -t "$pkgdir/usr/bin"
}
