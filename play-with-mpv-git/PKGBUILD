# http://aur.archlinux.org/packages/play-with-mpv-git
groups=('modified')

pkgname=play-with-mpv-git
pkgver=0.1.0.r9.g07a9c1d
pkgrel=2
pkgdesc="Python server that allows you to play videos in webpages with MPV instead."
arch=('any')
url="https://github.com/Thann/play-with-mpv"
license=('Unlicense')
depends=('mpv' 'youtube-dl')
makedepends=('git' 'python-build' 'python-install-freedesktop' 'python-installer' 'python-wheel')
optdepends=('peerflix: Stream torrents'
            'mkchromecast: Cast to network device')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Thann/play-with-mpv.git'
        "${pkgname%-git}.service")
sha256sums=('SKIP'
            'b273cd8ac9c48e04ac3c7fbcdd5657ed7eefa384212060341c138fd39d44c0d9')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/${pkgname%-git}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
  install -Dm644 "$srcdir/${pkgname%-git}.service" -t "$pkgdir/usr/lib/systemd/user/"
}
