# http://aur.archlinux.org/packages/play-with-mpv-git
pkgname=play-with-mpv-git
pkgver=0.1.0.r3.g0c27cd4
pkgrel=1
pkgdesc='Chrome extension and Python server that allows you to play videos in webpages with MPV instead.'
arch=('any')
url="https://github.com/Thann/play-with-mpv"
license=('MIT')
depends=('python-wheel' 'youtube-dl' 'mpv')
makedepends=('git' 'python-setuptools' 'python-wheel')
optdepends=('peerflix: stream torrents'
            'mkchromecast: Cast to network device')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Thann/play-with-mpv.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
