# https://aur.archlinux.org/packages/tabby
groups=('modified')

pkgname=tabby
pkgver=1.0.199
pkgrel=1
pkgdesc="A terminal for a more modern age"
arch=('x86_64')
url="https://tabby.sh/"
license=('MIT')
depends=('alsa-lib' 'gtk3' 'nss')
makedepends=('git' 'libxcrypt-compat' 'python' 'yarn')
optdepends=('libsecret: config sync'
            'gnome-keyring: password storage backend')
conflicts=('terminus-terminal')
replaces=('terminus-terminal')
_commit=0becf8cc76b08eae1bf570b51d4b2ccce3c14041  # tags/v1.0.199^0
source=("git+https://github.com/Eugeny/tabby.git#commit=$_commit?signed"
        "$pkgname.desktop")
sha256sums=('SKIP'
            'd0a70b3c59e423a6b7a11bed2a01b898f719dffba25d7f8b389ac981ce290569')
validpgpkeys=('F1A5C116840C8DAE045984275896FCBBDD1CF4F4') # Eugene Pankov <e@ajenti.org>

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd "$srcdir/$pkgname"

  # Build targets
  sed -i "s/'deb', 'tar.gz', 'rpm', 'pacman', 'appimage'/'deb'/g" scripts/build-linux.mjs
}

build(){
  cd "$srcdir/$pkgname"
  export YARN_CACHE_FOLDER="$srcdir/yarn-cache"
  yarn install
  yarn run build
  node scripts/prepackage-plugins.mjs
  node scripts/build-linux.mjs
}

package() {
  cd "$srcdir/$pkgname"
  install -d "$pkgdir/opt/$pkgname"
  cp -r dist/linux-unpacked/* "$pkgdir/opt/$pkgname/"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/$pkgname/$pkgname" "$pkgdir/usr/bin/"

  for i in 16 32 64 128 256 512; do
    install -Dm644 "build/icons/${i}x${i}.png" \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications/"

  # Remove prebuilt binaries for other architectures
  rm -rf "$pkgdir/opt/$pkgname"/resources/app.asar.unpacked/node_modules/@serialport/bindings-cpp/prebuilds/{android*,darwin*,linux-arm*,win*}
}
