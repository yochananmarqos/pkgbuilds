# https://gitlab.manjaro.org/packages/community/gnome/gnome-shell-extension/-/tree/master/gnome-shell-extension-pop-shell
# http://aur.archlinux.org/packages/gnome-shell-extension-pop-shell-git
pkgname=gnome-shell-extension-pop-shell
pkgver=0.1.0.r506.75b88b6
pkgrel=1
pkgdesc="Pop Shell - Tiling window management in Gnome (WIP)"
arch=('any')
url="https://github.com/pop-os/shell"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git' 'typescript')
optdepends=('pop-shell-shortcuts: show custom keybindings')
source=('git+https://github.com/pop-os/shell.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/shell"
	printf "%s.r%s.%s" "$(head -n 1 debian/changelog | cut -d'(' -f 2 | cut -d')' -f 1 | \
		sed 's/-/./')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/shell"
	make all
}

package() {
	cd "$srcdir/shell"
	make DESTDIR="$pkgdir" install
}
