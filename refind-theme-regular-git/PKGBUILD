# https://aur.archlinux.org/packages/refind-theme-regular-git/

pkgname=refind-theme-regular-git
pkgver=r90.26dcea4
pkgrel=1
pkgdesc="A simplistic clean and minimal theme for rEFInd"
arch=('any')
url="https://github.com/bobafetthotmail/refind-theme-regular"
license=('AGPL-3.0-or-later AND OFL-1.1 AND Ubuntu-font-1.0')
depends=('refind')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=("boot/efi/EFI/refind/themes/${pkgname%-git}/theme.conf")
#backup=("boot/efi/EFI/BOOT/themes/${pkgname%-git}/theme.conf")
source=('git+https://github.com/bobafetthotmail/refind-theme-regular.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

package() {
   # Specify path to the refind directory
  _refind_home="$pkgdir/boot/efi/EFI/refind"
#  _refind_home="$pkgdir/boot/efi/EFI/BOOT"

  cd "${pkgname%-git}"
  install -Dm644 theme.conf -t "$_refind_home/themes/${pkgname%-git}/"
  cp -r fonts icons "$_refind_home/themes/${pkgname%-git}"

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  warning 'To enable the theme add "include themes/refind-theme-regular/theme.conf"
           at the end of refind.conf, and comment out or delete any other themes
           you might have installed.'
}
