# https://aur.archlinux.org/packages/papirus-folders-git

pkgname=papirus-folders-git
pkgver=1.14.0.r0.g0f838ee
pkgrel=1
pkgdesc="Allows changing the color of folders in Papirus icon theme and its forks"
arch=('any')
url="https://github.com/PapirusDevelopmentTeam/papirus-folders"
license=('MIT')
depends=(
  'bash'
  'papirus-icon-theme'
)
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/PapirusDevelopmentTeam/papirus-folders.git'
        "${pkgname%-git}.hook")
sha256sums=('SKIP'
            'b6f52cab4c7a42e85b7e3cc0160bb9cf060aed20ab78ee2135a3f18dcaf6e92c')

pkgver() {
	cd "${pkgname%-git}"
	git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "${pkgname%-git}"
	make ZSHCOMPDIR="/usr/share/zsh/site-functions" DESTDIR="$pkgdir" install

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
	install -Dm644 "$srcdir/${pkgname%-git}.hook" -t "$pkgdir/usr/share/libalpm/hooks/"
}
