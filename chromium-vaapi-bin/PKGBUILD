pkgname=chromium-vaapi-bin
pkgver=83.0.4103.61
_pkgrel=1
pkgrel=1
pkgdesc='Chromium with VA-API support to enable hardware acceleration, pre-compiled'
url='https://www.chromium.org/Home'
arch=('x86_64')
license=('BSD')
provides=("${pkgname%-vaapi-bin}" "${pkgname%-bin}")
conflicts=("${pkgname%-vaapi-bin}" "${pkgname%-bin}")
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
         'ttf-liberation' 'systemd' 'dbus' 'libpulse' 'pciutils' 'json-glib' 'libva'
         'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('pepper-flash: support for Flash content'
            'libpipewire02: WebRTC desktop sharing under Wayland'
            'kdialog: needed for file dialogs in KDE'
            'org.freedesktop.secrets: password storage backend on GNOME / Xfce'
            'kwallet: for storing passwords in KWallet on KDE desktops')
source=("https://cdn.repo.archlinuxcn.org/x86_64/${pkgname%-bin}-${pkgver}-${_pkgrel}-${arch}.pkg.tar.zst") #{,.sig})
sha256sums=('a3e8a2d5dc36fa6b6b38668a1787a9315b5dce8bbfa42d4abf1ba86529db97f1')
#            'SKIP')
#validpgpkeys=('83F817213361BF5F02E7E124F9F9FA97A403F63E') # lilac (build machine) <lilac@build.archlinuxcn.org>

# Keep in sync with AUR/chromium-vaapi:
#
# Possible replacements are listed in build/linux/unbundle/replace_gn_files.py
# Keys are the names in the above script; values are the dependencies in Arch
declare -gA _system_libs=(
	[ffmpeg]=ffmpeg
	[flac]=flac
	[fontconfig]=fontconfig
	[freetype]=freetype2
	[harfbuzz-ng]=harfbuzz
	[icu]=icu
	[libdrm]=
	[libjpeg]=libjpeg
	#[libpng]=libpng		# https://crbug.com/752403#c10
	[libvpx]=libvpx
	[libwebp]=libwebp
	[libxml]=libxml2
	[libxslt]=libxslt
	[opus]=opus
	[re2]=re2
	[snappy]=snappy
	[yasm]=
	[zlib]=minizip
)
_unwanted_bundled_libs=(
	$(printf "%s\n" ${!_system_libs[@]} | sed 's/^libjpeg$/&_turbo/')
)
depends+=(${_system_libs[@]})

package() {
	cp -ar "$srcdir/usr" "$pkgdir/usr"
	chmod 4755 "$pkgdir/usr/lib/chromium/chrome-sandbox"
}
