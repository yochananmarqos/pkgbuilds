# https://aur.archlinux.org/packages/waydroid-helper

pkgname=waydroid-helper
pkgver=0.2.7
pkgrel=1
pkgdesc="GUI helper for Waydroid configuration and extensions"
arch=('any')
url="https://github.com/ayasa520/waydroid-helper"
license=('GPL-3.0-or-later')
depends=(
  'android-tools'
  'gtk4'
  'fakeroot'
  'libadwaita'
  'python'
  'python-aiofiles'
  'python-cairo'
  'python-dbus'
  'python-gobject'
  'python-httpx'
  'python-pywayland'
  'python-yaml'
  'waydroid'
)
makedepends=('meson')
optdepends=('bindfs: shared folders support')  # AUR
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('2f742b2a7d9c7b95fee155448bba3082ea4e53d6b7019a140d3b5b7708197979')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs || :
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
